hideincompatibleservers = 0
setpersist hideincompatibleservers 1
setcomplete hideincompatibleservers 1
setdesc "hideincompatibleservers" "0 = do not hide protocol-incompatible servers in the browser, 1 = hide protocol-incompatible servers in the browser" "value"

shown_server_ui = 0
serverinfo_index = 0
serverinfo_wait = 0
serverinfo_retry = ""
serverinfo_password = ""
server_count = 0
serverinfo_disp = 0
serverinfo_players = 0
serverinfo_servers = 0
serverinfo_server_count = 0
serverinfo_ui_time = 0
serverinfo_search_str = ""

number_of_servers_shown_at_once = 5

serverfavs = ""
setpersist serverfavs 1
setcomplete serverfavs 1

remove_from_sort = [
    serverinfo_type_list = ""
    serverinfo_tabsl = (? (> $serverinfo_type_list 0) $serverinfo_type_list (- 0 $serverinfo_type_list))

    // go through all items in serversort and add all of them to new_serversort except serverinfo_type_list
    loop i (listlen $serversort) [
        serverinfo_sort_type = (at $serversort $i)

        if $serverinfo_sort_type [
            if (!= $arg1 $serverinfo_sort_type) [
                serverinfo_sabsl = (? (> $serverinfo_sort_type 0) $serverinfo_sort_type (- 0 $serverinfo_sort_type))

                if (!= $serverinfo_tabsl $serverinfo_sabsl) [
                    append serverinfo_type_list $serverinfo_sort_type
                ]
            ]
        ]
    ]
    serversort $serverinfo_type_list
]


serverinfo_types = [ "" "status" "name" "port" "qport" "desc" "mode" "muts" "map" "time" "players" "maxplayers" "ping" "priority" ]

serverinfo_modify = [
    serverinfo_type_list = $arg1
    serverinfo_tabsl = (? (> $serverinfo_type_list 0) $serverinfo_type_list (- 0 $serverinfo_type_list))

    // loop through the serversort
    loop i (listlen $serversort) [
        serverinfo_sort_type = (at $serversort $i)
        if $serverinfo_sort_type [
            serverinfo_sabsl = (? (> $serverinfo_sort_type 0) $serverinfo_sort_type (- 0 $serverinfo_sort_type))

            if (!= $serverinfo_tabsl $serverinfo_sabsl) [
                append serverinfo_type_list $serverinfo_sort_type
            ]
        ]
    ]
    serversort $serverinfo_type_list
]
initialise_server_menu = [
    serverinfo_index = 0
    serverinfo_wait = 0
    serverinfo_retry = ""
    serverinfo_password = ""
    serverinfo_players = 0
    serverinfo_search_str = ""
    serverinfo_servers = 0
    serverinfo_server_count = 0
    serverinfo_ui_time = (getmillis 1)
    shown_server_ui = 0
    update_server_ui = 0
]
server_menu_iterator = [
    if (! $shown_server_ui) [
        map_list = ""
        map_path = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $map_list [=s $mcurmap $lcurmap]) 0) [
                append map_list $lcurmap
                append map_path [maps/@lcurmap]
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                append map_list $lcurmap
                append map_path [base/@lcurmap]
            ]
        ]
        shown_server_ui = 1
    ]
]
server_menu = [
    serverinfo_pause = 0
    serverinfo_timer = (- (getmillis 1) $serverinfo_ui_time)
    ui_big_macro serverinfo_ $number_of_servers_shown_at_once 99.5 4 [getserver] (getserver) 0 [
        serverinfo_status         = (get_server_status $i)
        serverinfo_name           = (get_server_name $i)
        serverinfo_port           = (get_server_port $i)
        serverinfo_name_port_id   = [@[serverinfo_name]:[@@serverinfo_port]] // previous name was "sinfonpid"
        serverinfo_title          = (get_server_title $i) // this is the server name
        serverinfo_map_name       = (get_server_map_name $i)
        serverinfo_player_count   = (get_server_player_count $i)
        serverinfo_ping           = (get_server_ping $i)
        serverinfo_last           = (get_server_last $i)
        serverinfo_handle         = (get_server_handle $i)
        serverinfo_flags          = (get_server_flags $i)
        serverinfo_branch         = (get_server_version_branch $i)
        serverinfo_priority       = (get_server_priority $i)
        serverinfo_status_flag    = 0
        loop fi (stringlen $serverinfo_flags) [
            if (=s (substring $serverinfo_flags $fi 1) "s") [ serverinfo_status_flag = 1 ]
        ]
        // attributes
        serverinfo_attribute      = (get_server_attribute_count $i)
        serverinfo_game_version   = (get_server_game_version $i)
        serverinfo_mode           = (get_server_game_mode $i)
        serverinfo_mutators       = (get_server_game_mutators $i)
        serverinfo_time           = (get_server_game_time $i)
        serverinfo_max_players    = (get_server_max_players $i)
        serverinfo_mstr           = (get_server_mstr $i)
        serverinfo_variables      = (get_server_variables $i)
        serverinfo_modifications  = (get_server_modifications $i)
        serverinfo_version_major  = (get_server_version_major $i)
        serverinfo_version_minor  = (get_server_version_minor $i)
        serverinfo_version_patch  = (get_server_version_patch $i)
        serverinfo_version_server = (get_server_version_server $i)
        serverinfo_version_a      = (get_server_version_a $i)
        serverinfo_game_st        = (get_server_game_st $i)
        serverinfo_game_tl        = (get_server_game_tl $i)
        serverinfo_offline_time   = (? (>= $serverinfo_last 0) (div (max (- (getmillis 1) (- $serverinfo_last (div $serverinfo_ping 2))) 0) 1000) 0)
        serverinfo_active         = (? (< $serverinfo_ping $serverwaiting) 1 0)

        search_buffer             = (concat $serverinfo_title $serverinfo_name_port_id $serverinfo_map_name (gamename $serverinfo_mode $serverinfo_mutators 0) $serverinfo_handle)
        if (> $serverinfo_player_count 0 ) [
            loop j $serverinfo_player_count [
                append search_buffer (getserver $i 2 $j) // the player name
                append search_buffer (getserver $i 3 $j) // and handle
            ]
        ]

        // To display a server, it must be included in the search filter (if applicable) and protocol compatible (if applicable)
        serverinfo_filter_include = (|| (=s $serverinfo_search_str "") (> (stringcasestr $search_buffer $serverinfo_search_str) -1))
        serverinfo_filter_compatible = (|| (! $hideincompatibleservers) (= $serverinfo_game_version (getversion 1)))
        serverinfo_display = (&& $serverinfo_filter_include $serverinfo_filter_compatible)

        // To count a server it must be displayable and active (returning pings)
        serverinfo_include_in_count = (&& $serverinfo_display $serverinfo_active)

        // If this is the first server, reset the counts.
        if (= $i 0) [
            // The number of players total.
            serverinfo_players = 0
            // The number of servers with players.
            serverinfo_servers = 0
             // The number of servers total.
            serverinfo_server_count = 0
        ]

        // Add this server to the counts if applicable.
        if $serverinfo_include_in_count [
            serverinfo_players = (+ $serverinfo_players $serverinfo_player_count)
            // Only add to the "servers with players" count if this server has players.
            if (> $serverinfo_player_count 0) [
                serverinfo_servers = (+ $serverinfo_servers 1)
            ]
            serverinfo_server_count = (+ $serverinfo_server_count 1)
        ]

        if (=s $serverinfo_name_port_id $serverinfo_retry) [
            if (= $serverinfo_wait 1) [
                if (stringlen $serverinfo_password) [
                    savewarnchk (format "connect %1 %2 %3" $serverinfo_name $serverinfo_port $serverinfo_password)
                    serverinfo_password = ""
                    serverinfo_retry = ""
                    serverinfo_wait = 0
                ]
            ] [
                if (|| [hasauthkey 1] [!= $serverinfo_status 3]) [
                    savewarnchk (format "connect %1 %2" $serverinfo_name $serverinfo_port)
                    serverinfo_password = ""
                    serverinfo_retry = ""
                    serverinfo_wait = 0
                ]
            ]
        ]
    ] [
        // Only display servers that are allowed
        $serverinfo_display
    ] [
        if $update_server_ui [
            ui_button "^fwThere are no servers to display, maybe ^fgupdate ^fwthe list?" updatefrommaster [] "info"
        ] [
            sleep 1 [ update_server_ui = 1; updatefrommaster; serverinfo_ui_time = (getmillis) ]
        ]
    ] [
        /// bar above the list
        if (getserver) [ sleep 0.1 [ updateservers ] ]

        ui_list [
            ui_center_z [
                ui_strut 0.5

                /// Reset List Button in red
                ui_button (format "^fr%1" "Reset") clearservers; ui_strut 1.5

                /// Refresh List Button in green
                ui_button (format "^fg%1" "Refresh") updatefrommaster; ui_strut 15

            ]

            // Search bar
            ui_textfield serverinfo_search_str 40 [serverinfo_index = 0] -1 0 "" 0 "^fd Search" 1; ui_strut 0.5

            ui_center_z [
                // Reset search button
                if (!=s $serverinfo_search_str "") [
                    ui_button "^frX" [ serverinfo_search_str = "" ]
                ] [
                    ui_strut 1
                ]
            ]

            ui_spring 1
            /// show how many players are online on how many servers currently
            ui_list [
                ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border
                ui_list [
                    ui_strut 1
                    ui_text (format "^fc%1 ^fwPlayers on ^fc%2 ^fwof ^fc%3 ^fwServer%4" $serverinfo_players $serverinfo_servers $serverinfo_server_count (? (!= $serverinfo_server_count 1) "s"))
                    ui_strut 1
                ]
            ]

        ]

        // -- Sort Options -- //
        if $showserversortoptions [
            ui_bar

            ui_list [
                ui_center_z [
                    ui_button "Reset" [serversortreset] [serversortreset] "" 0xFF8888
                ]

                ui_strut 1.5

                ui_center_z [
                    loop i (listlen $serversort) [
                        serverinfo_sort_type = (at $serversort $i)
                        if $serverinfo_sort_type [

                            serverinfo_sabsl = (? (> $serverinfo_sort_type 0) $serverinfo_sort_type (- 0 $serverinfo_sort_type))
                            serverinfo_server_name = (at $serverinfo_types $serverinfo_sabsl)

                            ui_big_spacer


                            ui_button (? (> $serverinfo_sort_type 0) $serverinfo_server_name [-@serverinfo_server_name]) [
                                serverinfo_modify @serverinfo_sort_type
                            ] [
                                serverinfo_modify (- 0 @serverinfo_sort_type)
                            ] "" 0x00FFFF

                            if (> (listlen $serversort) 1) [
                                ui_strut 0.5
                                ui_button (format "^fr%1" "x") [ remove_from_sort @serverinfo_sort_type ]
                            ]
                        ]
                    ]
                ]
                ui_spring 1
                ui_center_z [
                    ui_font "little" [
                        loop i (listlen $serverinfo_types) [
                            if (&& $i [< (listfind serverinfo_client_type $serversort [= $i (? (> $serverinfo_client_type 0) $serverinfo_client_type (- 0 $serverinfo_client_type))]) 0]) [
                                serverinfo_server_name = (at $serverinfo_types $i)
                                ui_strut 1

                                ui_button $serverinfo_server_name [
                                    serverinfo_modify @i
                                ] [
                                    serverinfo_modify (- 0 @i)
                                ] "" 0x888888
                            ]
                        ]
                    ]
                ]
            ]

            ui_strut 0.25
            ui_list [
                ui_checkbox (? $pausesortservers "^fdAuto Sort" "^fbAuto Sort") autosortservers // auto sort checkbox

                ui_strut 1.5

                // if auto sort is off, show sort now button
                if $autosortservers [ ] [
                    ui_button (format "^fy%1" "Sort Now") sortservers
                ]
            ]

            ui_bar
        ]



        // if a new version is available, show a banner that holds a link where you can download it
        ui_small_spacer
        if (> (getversion 3) (getversion 1)) [
            ui_list [ ui_button "^fzoynew version released! ^fwget it now from: ^fchttps://go.blue-nebula.org/releases" "" ]
            ui_small_spacer
        ]

    ] [ // the actual list
        ui_list [
            ui_background 0x222222

            ui_list [ ui_merge 88 [
                ui_center [
                    ui_strut 8 1
                    if $serverinfo_active [
                        ui_center [
                            ui_font "huge" [ ui_button (format "^fw%1" $serverinfo_player_count) ] // players playing
                            ui_center [ ui_button (format "^fd/^fa%1" $serverinfo_max_players) ] // max players
                        ]
                        ui_strut 0.125
                        ui_center [
                            ui_button (format "^f%1%2" (? (< $serverinfo_ping 200) "g" (? (< $serverinfo_ping 400) "y" "r")) $serverinfo_ping) // ping info
                            ui_font "little" [ ui_button " ^fams" ]
                        ]
                        ui_strut 0.125
                        ui_center [
                            serverinfo_seconds = (? (>= $serverinfo_last 0) (div (- (getmillis 1) $serverinfo_last) 1000) -1) // ??last ping info??
                            ui_font "little" [ ui_button (? (>= $serverinfo_seconds 0) (format "^fa%1 ^fds ago" $serverinfo_seconds) "^fdwaiting..") ]
                        ]
                    ] [ ui_center [ ui_font "huge" [ ui_button "?" ] ] ]
                ]
                // START OF SERVER INFORMATION LIST
                serverinfo_image = "textures/emblem"
                if (&& [< $serverinfo_ping $serverwaiting] [= $serverinfo_game_version (getversion 1)]) [
                    serverinfo_path = (listfind curmap $map_list [|| [=s $curmap $serverinfo_map_name] [=s [base/@curmap] $serverinfo_map_name] [
                        && [> $hasoctapaks 0] [=s [base/@curmap] $serverinfo_map_name]
                    ]])
                    if (> $serverinfo_path -1) [ serverinfo_image = (at $map_path $serverinfo_path) ]
                ]
                ui_image $serverinfo_image "" 2 1 "textures/emblem" // display the server emblem
                ui_strut 1

                ui_list [
                    // make sure the entry takes all the horizontal space available
                    ui_list2 [ ui_strut 80 1 ]

                    ui_list [
                        ui_list [ ui_strut 4 ]
                        ui_center [
                            ui_list [
                                // Server title
                                title = (format "^fw%1" $serverinfo_title)

                                if (= $serverinfo_game_version (getversion 1)) [
                                    case $serverinfo_status 0 [
                                        title = (format "^fw%1" $serverinfo_title)
                                    ] 1 [
                                        title = (format "^fy%1" $serverinfo_title)
                                    ] 2 [
                                        title = (format "^fm%1" $serverinfo_title)
                                    ] 3 [ // Server Incompatible
                                        title = (format "^fo%1" $serverinfo_title)
                                    ] () [
                                        title = (format "^fa%1" $serverinfo_title)
                                    ]
                                ] [
                                    title = (format "^fo%1" $serverinfo_title)
                                ]

                                ui_font "emphasis" [ ui_button (limitstring $title 64) ]

                                // show ip and port
                                ui_font "little" [ ui_center_z [ ui_button (format "^fd^fa %1^fd" $serverinfo_name_port_id) ] ]

                                if $serverinfo_handle [
                                    ui_font "little" [ ui_center [ ui_button (format "^fd[^fc%1^fd]" $serverinfo_handle) ] ]
                                ]
                                if $serverinfo_status_flag [
                                    ui_font "little" [ ui_center [ ui_button "^fd[^fcstatistics^fd]" ] ]
                                ]

                                // if enabled, show priority
                                if $showserverpriority [
                                    ui_font "little" [ ui_center [ ui_button (format "^fd[^fcpriority %1^fd]" $serverinfo_priority) ] ]
                                ]
                            ]
                            ui_spring 1

                            // information beneigh the server title
                            if $serverinfo_active [
                                ui_list [
                                    ui_small_spacer
                                    if (= $serverinfo_game_version (getversion 1)) [

                                        if (!= $serverinfo_mode $modeidxediting) [
                                            ui_button (format "^fd[^fc%1^fd: ^fw%2^fd]" (at $gamestatename $serverinfo_game_st) (timestr (* (? $serverinfo_player_count (max (- $serverinfo_game_tl $serverinfo_offline_time) 0) $serverinfo_time) 1000) 3))
                                        ]
                                        gname = (gamename $serverinfo_mode $serverinfo_mutators 0 32)
                                        ui_font "little" [ ui_center [ ui_list [
                                            // show map name
                                            ui_button (format " ^fy%1 ^faon ^fo%2" $gname $serverinfo_map_name)

                                            // show how modified the server is
                                            ui_button (format " ^fa  (%1modified^fa)" (? $serverinfo_modifications (format "^fc%1%% " (precf (*f (divf $serverinfo_modifications $serverinfo_variables) 100) 2)) "^faun"))

                                            // show server platform and branch
                                            if (&& ($serverbrowser_show_server_platform_and_branch) (>= $serverinfo_attribute 13)) [
                                                ui_font "little" [ ui_center_z [
                                                    ui_text (format " ^fa[^fa%1" $serverinfo_version_major)
                                                    ui_text (format "^fa.%1" $serverinfo_version_minor)
                                                    ui_text (format "^fa.%1" $serverinfo_version_patch)
                                                    ui_text (format "^fa-%1" (platname $serverinfo_version_server) )
                                                    ui_text (format "^fa%1" $serverinfo_version_a)
                                                    if $serverinfo_branch [
                                                        ui_text (format "^fa-%1^fa]" $serverinfo_branch)
                                                    ] [
                                                        ui_text "^fa]"
                                                    ]
                                                ] ]
                                            ]
                                        ] ] ]
                                    ] [
                                        ui_no_hit_fx [ ui_font "default" [ ui_button "^foIncompatible" [] [] "textures/servers/failed" ] ]
                                        ui_button (concat " ^faServer is using" (? (> $serverinfo_game_version (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol")
                                    ]
                                ]
                                ui_spring 1
                                // display password field
                                ui_list [
                                    if (=s $serverinfo_name_port_id $serverinfo_retry) [
                                        if (= $serverinfo_status 3) [ ui_button "^fd[ ^fwWaiting for slot ^fd] " ]
                                        ui_button "^fwPassword ^fd= "
                                        serverinfo_passwordval = $serverinfo_password
                                        ui_textfield serverinfo_passwordval 20 [serverinfo_password = $serverinfo_passwordval]
                                    ] [
                                        if (> $serverinfo_player_count 0) [
                                            ui_small_spacer
                                            serverinfo_player_count = (getserver $i 2)
                                            if (> $serverinfo_player_count 0) [
                                                ui_font "little" [
                                                    pname = ""
                                                    plist = ""
                                                    pmore = 0
                                                    loop j $serverinfo_player_count [
                                                        if (|| $pmore (>= (ui_text_width $plist) 1400)) [ pmore = (+ $pmore 1) ] [
                                                            append pname (format ["%1"] (getserver $i 2 $j))
                                                            plist = (prettylist $pname)
                                                        ]
                                                    ]
                                                    ui_button (concat $plist (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                                    //[] [] "" -1 -1 1900
                                                ]
                                            ] [ ui_button "^faPlayer info not available" ]
                                        ] [ ui_button "^faNo players online" ]
                                    ]
                                ]
                            ] [
                                ui_list [
                                    ui_small_spacer
                                    ui_font "default" [ ui_button "^foUnresponsive" [] [] "textures/servers/failed" ]
                                    ui_button " ^faServer is not replying to queries"
                                ]
                            ]
                        ]
                    ]
                ]
            ] [
                serverinfo_password = ""
                serverinfo_retry = @(escape $serverinfo_name_port_id)
                serverinfo_wait = (! (|| [hasauthkey 1] [!= @@serverinfo_mstr 4]))
            ] [
                serverinfo_password = ""
                serverinfo_retry = @(escape $serverinfo_name_port_id)
                serverinfo_wait = (! (hasauthkey 1))
            ] [
                serverinfo_pause = 1
                if $serverinfo_active [
                    serverinfo_player_count = (getserver $i 2)
                    if (> $serverinfo_player_count 0) [
                        phover = ""
                        loop j $serverinfo_player_count [
                            if $j [ append phover "^n" ]
                            phandle = (getserver $i 3 $j)
                            if (stringlen $phandle) [
                                append phover (format "%1 (%2)" (getserver $i 2 $j) $phandle)
                            ] [
                                append phover (format "%1" (getserver $i 2 $j))
                            ]
                        ]
                        ui_tooltip $phover
                    ] [ ui_tooltip "^faNo players online" ]
                ] [ ui_tooltip "^faNo information available" ]
            ] ]
            //ui_center_z [
            //    ui_image "textures/icons/fontawesome/favourite_off" [ echo "Coming Soon!" ] 0.75
            //]; ui_strut 1
            ui_strut 5
        ]
    ] [ // bar beneigh the list
        ui_strut 0.5

        ui_list [
            ui_checkbox (format "^fc%1" "Search LAN")  searchlan;                 ui_strut 1.5
            ui_button   (format "^fm%1" "LAN Connect") "savewarnchk lanconnect";  ui_strut 1.5
            ui_button   (format "^fo%1" "Disconnect")  "savewarnchk disconnect";  ui_spring 1

            if (hasauthkey) [
                ui_checkbox (format "Identify as ^fs^fc%1^fS on connect" $accountname) authconnect; ui_strut 1.5
                ui_center_z [ ui_font "little" [
                    ui_button "(^fs^frEdit Account^fS)" [show_ui profile 4]
                ] ]
            ]
        ]

    ]
    if (&& [! $guilayoutpass] [|| (> $serverinfo_timer 30000) (> $serverinfo_server_count (div (* $server_count 3) 4))]) [ if $serverinfo_pause [ if (! $pausesortservers) [ pausesortservers 1 ] ] [ if $pausesortservers [ pausesortservers 0 ] ] ]

    ui_tip (format "Press %1 to open this menu at any time" (dobindsearch "showservers"))
]

new_ui Servers [
    ui_header "Serverbrowser"
    server_menu_iterator
    ui_list [ server_menu ]
] [
    if (= $guipasses 0) [
        initialise_server_menu
    ]
]

new_ui guidelines [
    ui_editor doc/guidelines.txt -80 22
    ui_textinit doc/guidelines.txt doc/guidelines.txt
    ui_textmode 4
    ui_big_spacer
    ui_font "emphasis" [
        ui_list [
            ui_button "[ ^fs^foI disagree, stay offline^fS ]" [clear_ui 1]
            ui_spring 1
            ui_button "[ ^fs^fgI agree, play online^fS ]" [connectguidelines 1; @guidelinesaction; guidelinesaction ""]
            ui_strut 3
        ]
    ]
    ui_status "You must read and agree to these guidelines before playing online"
] [ if $connectguidelines [clear_ui 1] ]
showservers = [ guidelinesaction "show_ui Servers"; show_ui (? $connectguidelines Servers guidelines) ]
