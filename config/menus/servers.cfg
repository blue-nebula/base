shown_server_ui = 0
serverinfo_index = 0
serverinfo_wait = 0
serverinfo_retry = ""
serverinfo_password = ""
server_count = 0
serverinfo_disp = 0
serverinfo_players = 0
serverinfo_servers = 0
serverinfo_server_count = 0
serverinfo_ui_time = 0

show_advanced_serverlist_options = 0
show_sort_options = 0


close_extra_menus = [
    if (= $arg1 0) [ // clicked on the advanced options button
        show_sort_options = 0
    ] [ // clicked on the sort options button
        show_advanced_serverlist_options = 0
    ]
]


list_show_ip_and_port = 1
list_show_priority = 0
list_show_server_platform_and_branch = 0
list_show_server_is_modified = 0

spacing_between_serverinfos = 0.5

number_of_servers_shown_at_once = 5
number_of_servers_shown_at_once_tmp = $number_of_servers_shown_at_once

apply_layout_changes = [
    number_of_servers_shown_at_once = $number_of_servers_shown_at_once_tmp
]

serverinfo_types = [ "status" "name" "port" "qport" "desc" "mode" "muts" "map" "time" "players" "maxplayers" "ping" "priority" ]
serverinfo_modify = [
    serverinfo_type_list = $arg1
    serverinfo_tabsl = (? (> $serverinfo_type_list 0) $serverinfo_type_list (- 0 $serverinfo_type_list))

    loop i (listlen $serversort) [
        serverinfo_server_type = (at $serversort $i)
        if $serverinfo_server_type [
            serverinfo_sabsl = (? (> $serverinfo_server_type 0) $serverinfo_server_type (- 0 $serverinfo_server_type))

            if (!= $serverinfo_tabsl $serverinfo_sabsl) [
                append serverinfo_type_list $serverinfo_server_type
            ]
        ]
    ]
    serversort $serverinfo_type_list
]
initialise_server_menu = [
    serverinfo_index = 0
    serverinfo_wait = 0
    serverinfo_retry = ""
    serverinfo_password = ""
    serverinfo_players = 0
    serverinfo_servers = 0
    serverinfo_server_count = 0
    serverinfo_ui_time = (getmillis 1)
    shown_server_ui = 0
    update_server_ui = 0
    search_filter = 1
    search_str = ""
]
server_menu_iterator = [
    if (! $shown_server_ui) [
        map_list = ""
        map_path = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $map_list [=s $mcurmap $lcurmap]) 0) [
                append map_list $lcurmap
                append map_path [maps/@lcurmap]
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                append map_list $lcurmap
                append map_path [base/@lcurmap]
            ]
        ]
        shown_server_ui = 1
    ]
]
server_menu = [
    serverinfo_pause = 0
    serverinfo_timer = (- (getmillis 1) $serverinfo_ui_time)
    ui_big_macro serverinfo_ $number_of_servers_shown_at_once 96 4 [getserver] (getserver) [
        serverinfo_status = (getserver $i 0 0)
        serverinfo_name = (getserver $i 0 1)
        serverinfo_port = (getserver $i 0 2)
        serverinfo_name_port_id = [@[serverinfo_name]:[@@serverinfo_port]] // previous name was "sinfonpid"
        serverinfo_title = (getserver $i 0 3) // this is the server name
        serverinfo_map_name = (getserver $i 0 4)
        serverinfo_player_count = (getserver $i 0 5)
        serverinfo_players = (? $i (+ $serverinfo_players $serverinfo_player_count) $serverinfo_player_count)
        serverinfo_servers = (? $i (? $serverinfo_player_count (+ $serverinfo_servers 1) $serverinfo_servers) (? $serverinfo_player_count 1 0))
        serverinfo_ping = (getserver $i 0 6)
        serverinfo_last = (getserver $i 0 7)
        serverinfo_handle = (getserver $i 0 8)
        serverinfo_flags = (getserver $i 0 9)
        serverinfo_bran = (getserver $i 0 10)
        serverinfo_priority = (getserver $i 0 11)
        serverinfo_status_flag = 0
        loop fi (stringlen $serverinfo_flags) [
            if (=s (substring $serverinfo_flags $fi 1) "s") [ serverinfo_status_flag = 1 ]
        ]
        // attributes
        serverinfo_attribute = (getserver $i 1 -1)
        serverinfo_game_version = (getserver $i 1 0)
        serverinfo_mode = (getserver $i 1 1)
        serverinfo_mutators = (getserver $i 1 2)
        serverinfo_time = (getserver $i 1 3)
        serverinfo_max_players = (getserver $i 1 4)
        serverinfo_map_string = (getserver $i 1 5)
        serverinfo_variables = (getserver $i 1 6)
        serverinfo_modifications = (getserver $i 1 7)
        serverinfo_version_map = (getserver $i 1 8)
        serverinfo_version_n = (getserver $i 1 9)
        serverinfo_version_player = (getserver $i 1 10)
        serverinfo_version_server = (getserver $i 1 11)
        serverinfo_version_a = (getserver $i 1 12)
        serverinfo_game_st = (getserver $i 1 13)
        serverinfo_game_tl = (getserver $i 1 14)
        serverinfo_offline_time = (? (>= $serverinfo_last 0) (div (max (- (getmillis 1) (- $serverinfo_last (div $serverinfo_ping 2))) 0) 1000) 0)
        serverinfo_active = (? (< $serverinfo_ping $serverwaiting) 1 0)
        serverinfo_server_count = (? $i (+ $serverinfo_server_count $serverinfo_active) $serverinfo_active)
        search_buffer = (concat $serverinfo_title $serverinfo_name_port_id $serverinfo_map_name (gamename $serverinfo_mode $serverinfo_mutators 0) $serverinfo_handle)
        if (> $serverinfo_player_count 0 ) [
            loop j $serverinfo_player_count [
                append search_buffer (getserver $i 2 $j) // the player name
                append search_buffer (getserver $i 3 $j) // and handle
            ]
        ]
        if (=s $serverinfo_name_port_id $serverinfo_retry) [
            if (= $serverinfo_wait 1) [
                if (stringlen $serverinfo_password) [
                    savewarnchk (format "connect %1 %2 %3" $serverinfo_name $serverinfo_port $serverinfo_password)
                    serverinfo_password = ""
                    serverinfo_retry = ""
                    serverinfo_wait = 0
                ]
            ] [
                if (|| [hasauthkey 1] [!= $serverinfo_status 3]) [
                    savewarnchk (format "connect %1 %2" $serverinfo_name $serverinfo_port)
                    serverinfo_password = ""
                    serverinfo_retry = ""
                    serverinfo_wait = 0
                ]
            ]
        ]
    ] [(|| (= $search_filter 0)  (> (stringcasestr $search_buffer $search_str) -1))] [
        if $update_server_ui [
            ui_button "^fwThere are no servers to display, maybe ^fgupdate ^fwthe list?" updatefrommaster [] "info"
        ] [
            sleep 1 [ update_server_ui = 1; updatefrommaster; serverinfo_ui_time = (getmillis) ]
        ]
    ] [ 
        /// bar above the list
        if (getserver) [ sleep 0.1 [ updateservers ] ]
        
        ui_list [
            ui_center_z [
                ui_strut 0.5
                
                /// Reset List Button in red
                ui_button (format "^fr%1" "Reset") clearservers
                ui_strut 1.5
                
                /// Refresh List Button in green
                ui_button (format "^fg%1" "Refresh") updatefrommaster
                ui_strut 7.5

                /// Search Bar
                ui_text "Search: "
                ui_strut 0.5
                ui_textfield search_strval 40 [search_str = $search_strval; search_filter = 1; serverinfo_index = 0] -1 0 "" 0 "^fd <enter text>" 1
            ]
            ui_spring 1
            /// show how many players are online on how many servers currently
            ui_list [
                ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border
                ui_list [
                    ui_strut 1
                    ui_text (format "^fc%1 ^fwPlayers on ^fc%2 ^fwof ^fc%3^fa/^fc%4 ^fwServer%5" $serverinfo_players $serverinfo_servers $serverinfo_server_count $server_count (? (!= $server_count 1) "s"))
                    ui_strut 1
                ]
            ]

        ]
        
        // -- ADVANCED OPTIONS --
        
        ui_strut 0.25
        ui_list [
            ui_checkbox (format "%1" "Advanced Settings") show_advanced_serverlist_options 1 0 [ close_extra_menus 0 ] // "Advanced Settings" checkbox
        
            ui_strut 1.5
            
            ui_checkbox (format "%1" "Sort Settings") show_sort_options 1 0 [ close_extra_menus 1 ] // "Sort Settings" checkbox
        
            ui_strut 1.5
            
            ui_checkbox (? $pausesortservers "^fdAuto Sort" "^fbAuto Sort") autosortservers // auto sort checkbox
            
            ui_strut 1.5
            
            // if auto sort is off, show sort now button
            if $autosortservers [ ] [ 
                ui_button (format "^fy%1" "Sort Now") sortservers
            ]
            
            if $show_advanced_serverlist_options [
                ui_strut 1.5
                ui_checkbox (format "^fc%1" "Search LAN") searchlan // search lan checkbox
                ui_strut 1.5
                ui_button (format "^fm%1" "LAN Connect") "savewarnchk lanconnect" // lan connect button
                ui_strut 1.5
                ui_button (format "^fo%1" "Disconnect") "saverwarnchk disconnect" // disconnect button
            ]
        ]
        
        // -- Advanced Options -- //
        
        if $show_advanced_serverlist_options [
            ui_strut 0.4
        
            ui_bar
            
            ui_text "Infos:"
            ui_strut 0.2
            
            ui_list [
                ui_center_z [
                    ui_checkbox (format "^fw%1" "Show IP and Port") list_show_ip_and_port // show_ip_and_port checkbox
                    
                    ui_strut 1.5
                    
                    ui_checkbox (format "^fc%1" "Show Priority") list_show_priority // show_priority checkbox
                    
                    ui_strut 1.5
                    
                    ui_checkbox (format "^fc%1" "Show Server Platform and Branch") list_show_server_platform_and_branch // show_server_platform_and_branch checkbox
                    
                    ui_strut 1.5
                    
                    ui_checkbox (format "^fa%1" "Show Server Modified Status") list_show_server_is_modified
                ]
            ]

            ui_strut 0.4
            
            ui_text "Update Interval (in seconds):"
            ui_strut 0.2
            
            ui_list [
                ui_center_z [ 
                    ui_radiobutton "1" serverupdateinterval 1
                    
                    ui_strut 0.75
                    
                    ui_radiobutton "5" serverupdateinterval 5
                    
                    ui_strut 0.75
                    
                    ui_radiobutton "15" serverupdateinterval 15
                    
                    ui_strut 0.75
                    
                    ui_radiobutton "30" serverupdateinterval 30
                    
                    ui_strut 0.75
                    
                    ui_radiobutton "60" serverupdateinterval 60
                    
                    ui_strut 0.75
                ]
                
            ]
            
            ui_strut 0.4
            
            
            ui_bar
        ]
        
        // -- Sort Options -- //
        if $show_sort_options [
            ui_bar
        
            ui_text "Sort:"
            ui_strut 0.2
            
            
            ui_list [
                ui_center_z [ 
                    ui_button "Reset" [serversortreset] [serversortreset] "" 0xFF8888 
                ]
                
                ui_strut 1.5
            
                ui_center_z [
                    loop i (listlen $serversort) [
                        serverinfo_server_type = (at $serversort $i)
                        if $serverinfo_server_type [
                            serverinfo_sabsl = (? (> $serverinfo_server_type 0) $serverinfo_server_type (- 0 $serverinfo_server_type))
                            serverinfo_server_name = (at $serverinfo_types $serverinfo_sabsl)
                            ui_big_spacer
                            ui_button (? (> $serverinfo_server_type 0) $serverinfo_server_name [-@serverinfo_server_name]) [
                                serverinfo_modify @serverinfo_server_type
                            ] [
                                serverinfo_modify (- 0 @serverinfo_server_type)
                            ] "" 0x00FFFF
                        ]
                    ]
                ]
                ui_spring 1
                ui_center_z [
                    ui_font "little" [
                        loop i (listlen $serverinfo_types) [
                            if (&& $i [< (listfind serverinfo_client_type $serversort [= $i (? (> $serverinfo_client_type 0) $serverinfo_client_type (- 0 $serverinfo_client_type))]) 0]) [
                                serverinfo_server_name = (at $serverinfo_types $i)
                                ui_strut 1
                                ui_button $serverinfo_server_name [
                                    serverinfo_modify @i
                                ] [
                                    serverinfo_modify (- 0 @i)
                                ] "" 0x888888
                            ]
                        ]
                    ]
                ]
            ]
            
            ui_bar
        ]

        

        // if a new version is available, show a banner that holds a link where you can download it
        ui_small_spacer
        if (> (getversion 3) (getversion 1)) [
            ui_list [ ui_button "^fzoynew version released! ^fwget it now from: ^fcgithub.com/redeclipse-legacy/release/releases/" "" ]
            ui_small_spacer
        ]

    ] [ // the actual list
        ui_merge 88 [
            ui_center [
                ui_strut 8 1
                if $serverinfo_active [
                    ui_center [
                        ui_font "huge" [ ui_button (format "^fw%1" $serverinfo_player_count) ] // players playing
                        ui_center [ ui_button (format "^fd/^fa%1" $serverinfo_max_players) ] // max players
                    ]
                    ui_strut 0.125
                    ui_center [
                        ui_button (format "^f%1%2" (? (< $serverinfo_ping 200) "g" (? (< $serverinfo_ping 400) "y" "r")) $serverinfo_ping) // ping info
                        ui_font "little" [ ui_button " ^fams" ]
                    ]
                    ui_strut 0.125
                    ui_center [
                        serverinfo_seconds = (? (>= $serverinfo_last 0) (div (- (getmillis 1) $serverinfo_last) 1000) -1) // ??last ping info??
                        ui_font "little" [ ui_button (? (>= $serverinfo_seconds 0) (format "^fa%1 ^fds ago" $serverinfo_seconds) "^fdwaiting..") ]
                    ]
                ] [ ui_center [ ui_font "huge" [ ui_button "?" ] ] ]
            ]
            // START OF SERVER INFORMATION LIST
            serverinfo_image = "textures/emblem"
            if (&& [< $serverinfo_ping $serverwaiting] [= $serverinfo_game_version (getversion 1)]) [
                serverinfo_path = (listfind curmap $map_list [|| [=s $curmap $serverinfo_map_name] [=s [base/@curmap] $serverinfo_map_name] [
                    && [> $hasoctapaks 0] [=s [base/@curmap] $serverinfo_map_name]
                ]])
                if (> $serverinfo_path -1) [ serverinfo_image = (at $map_path $serverinfo_path) ]
            ]
            ui_image $serverinfo_image "" 2 1 "textures/emblem" // display the server emblem
            ui_strut 1
            ui_center [
                ui_list [
                    // server name

                    title = (format "^fw%1" $serverinfo_title)

                    if (= $serverinfo_game_version (getversion 1)) [
                        ui_no_hit_fx [ ui_font "default" [
                            case $serverinfo_status 0 [
                                title = (format "^%1" $serverinfo_title)
                            ] 1 [
                                title = (format "^fy%1" $serverinfo_title)
                            ] 2 [
                                title = (format "^fm%1" $serverinfo_title)
                            ] 3 [
                                title = (format "^fzao%1" $serverinfo_title)
                            ] () [
                                title = (format "^fa%1" $serverinfo_title)
                            ]
                        ] ]
                    ] [
                        title = (format "^fo%1" $serverinfo_title)
                    ]

                    ui_font "emphasis" [ ui_button $title ]
                    
                    // if enabled, show ip and port
                    if $list_show_ip_and_port [
                        ui_font "tiny" [ ui_center [ ui_button (format "^fd^fa%1^fd" $serverinfo_name_port_id) ] ] 
                    ]

                    if $serverinfo_handle [
                        ui_font "little" [ ui_center [ ui_button (format "^fd[^fc%1^fd]" $serverinfo_handle) ] ]
                    ]
                    if $serverinfo_status_flag [
                        ui_font "little" [ ui_center [ ui_button "^fd[^fcstatistics^fd]" ] ]
                    ]

                    // if enabled, show priority
                    if $list_show_priority [
                        ui_font "little" [ ui_center [ ui_button (format "^fd[^fcpriority %1^fd]" $serverinfo_priority) ] ]
                    ]
                ]
                ui_spring 1
                
                // information beneigh the server title
                if $serverinfo_active [
                    ui_list [
                        ui_small_spacer
                        if (= $serverinfo_game_version (getversion 1)) [

                            if (!= $serverinfo_mode $modeidxediting) [
                                ui_button (format " ^fd[^fc%1^fd:^fw%2^fd]" (at $gamestatename $serverinfo_game_st) (timestr (* (? $serverinfo_player_count (max (- $serverinfo_game_tl $serverinfo_offline_time) 0) $serverinfo_time) 1000) 3))
                            ]
                            gname = (gamename $serverinfo_mode $serverinfo_mutators 0 32)
                            ui_font "normal" [ ui_center [ ui_list [
                                // show map name
                                ui_button (format " ^fy%1 ^faon ^fo%2" $gname $serverinfo_map_name)

                                // show how modified the server is
                                if $list_show_server_is_modified [
                                    ui_button (format " ^fa(%1Modified^fa)" (? $serverinfo_modifications (format "^fc%1%% " (precf (*f (divf $serverinfo_modifications $serverinfo_variables) 100) 2)) "^fgun"))
                                ]

                                // show server platform and branch
                                if (&& ($list_show_server_platform_and_branch) (>= $serverinfo_attribute 13)) [ 
                                    ui_text (format " ^fa[^fc%1" $serverinfo_version_map)
                                    ui_text (format "^fc.%1" $serverinfo_version_n)
                                    ui_text (format "^fc.%1" $serverinfo_version_player)
                                    ui_text (format "^fc.%1" (platname $serverinfo_version_server) )
                                    ui_text (format "^fc-%1" $serverinfo_version_a)
                                    ui_text (format "^fc-%1^fa]" $serverinfo_bran)
                                ]
                            ] ] ]
                        ] [
                            ui_no_hit_fx [ ui_font "default" [ ui_button "^foIncompatible" [] [] "textures/servers/failed" ] ]
                            ui_button (concat " ^faServer is using" (? (> $serverinfo_game_version (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol")
                        ]
                    ]
                    ui_spring 1
                    // display password field
                    ui_list [
                        if (=s $serverinfo_name_port_id $serverinfo_retry) [
                            if (= $serverinfo_status 3) [ ui_button "^fd[ ^fwWaiting for slot ^fd] " ]
                            ui_button "^fwPassword ^fd= "
                            serverinfo_passwordval = $serverinfo_password
                            ui_textfield serverinfo_passwordval 20 [serverinfo_password = $serverinfo_passwordval]
                        ] [
                            if (> $serverinfo_player_count 0) [
                                ui_small_spacer
                                serverinfo_player_count = (getserver $i 2)
                                if (> $serverinfo_player_count 0) [
                                    ui_font "little" [
                                        pname = ""
                                        plist = ""
                                        pmore = 0
                                        loop j $serverinfo_player_count [
                                            if (|| $pmore (>= (ui_text_width $plist) 1400)) [ pmore = (+ $pmore 1) ] [
                                                append pname (format ["%1"] (getserver $i 2 $j))
                                                plist = (prettylist $pname)
                                            ]
                                        ]
                                        ui_button (concat $plist (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                        //[] [] "" -1 -1 1900
                                    ]
                                ] [ ui_button "^faPlayer info not available" ]
                            ] [ ui_button "^faNo players online" ]
                        ]
                    ]
                ] [
                    ui_list [
                        ui_small_spacer
                        ui_font "default" [ ui_button "^foUnresponsive" [] [] "textures/servers/failed" ]
                        ui_button " ^faServer is not replying to queries"
                    ]
                ]
            ]
        ] [
            serverinfo_password = ""
            serverinfo_retry = @(escape $serverinfo_name_port_id)
            serverinfo_wait = (! (|| [hasauthkey 1] [!= @@serverinfo_map_string 4]))
        ] [
            serverinfo_password = ""
            serverinfo_retry = @(escape $serverinfo_name_port_id)
            serverinfo_wait = (! (hasauthkey 1))
        ] [
            serverinfo_pause = 1
            if $serverinfo_active [
                serverinfo_player_count = (getserver $i 2)
                if (> $serverinfo_player_count 0) [
                    phover = ""
                    loop j $serverinfo_player_count [
                        if $j [ append phover "^n" ]
                        phandle = (getserver $i 3 $j)
                        if (stringlen $phandle) [
                            append phover (format "%1 (%2)" (getserver $i 2 $j) $phandle)
                        ] [
                            append phover (format "%1" (getserver $i 2 $j))
                        ]
                    ]
                    ui_tooltip $phover
                ] [ ui_tooltip "^faNo players online" ]
            ] [ ui_tooltip "^faNo information available" ]
        ]
        ui_strut $spacing_between_serverinfos
    ] [ // bar beneigh the list
        ui_big_spacer

        

        ui_big_spacer
        if (hasauthkey) [
            ui_list [
                ui_center_z [ ui_text "User Account:" ]
                ui_spring 1
                ui_center_z [ ui_checkbox (format "Identify as ^fs^fc%1^fS on connect" $accountname) authconnect ]
                ui_spring 1
                ui_center_z [ ui_font "little" [ ui_button "(^fs^frEdit Account^fS)" [show_ui profile 4] ] ]
            ]
        ]
    ]
    if (&& [! $guilayoutpass] [|| (> $serverinfo_timer 30000) (> $serverinfo_server_count (div (* $server_count 3) 4))]) [ if $serverinfo_pause [ if (! $pausesortservers) [ pausesortservers 1 ] ] [ if $pausesortservers [ pausesortservers 0 ] ] ]

    ui_tip (format "Press %1 to open this menu at any time" (dobindsearch "showservers"))
]

new_ui Servers [
    server_menu_iterator
    ui_list [ server_menu ]
] [
    if (= $guipasses 0) [
        initialise_server_menu
    ]
]

new_ui guidelines [
    ui_editor doc/guidelines.txt -80 22
    ui_textinit doc/guidelines.txt doc/guidelines.txt
    ui_textmode 4
    ui_big_spacer
    ui_font "emphasis" [
        ui_list [
            ui_button "[ ^fs^foI disagree, stay offline^fS ]" [clear_ui 1]
            ui_spring 1
            ui_button "[ ^fs^fgI agree, play online^fS ]" [connectguidelines 1; @guidelinesaction; guidelinesaction ""]
            ui_strut 3
        ]
    ]
    ui_status "You must read and agree to these guidelines before playing online"
] [ if $connectguidelines [clear_ui 1] ]
showservers = [ guidelinesaction "show_ui servers"; show_ui (? $connectguidelines Servers guidelines) ]
