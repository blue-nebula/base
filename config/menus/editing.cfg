
    // utilities
    entselbyindex = [entselect [(> (indexof $arg1 (entindex)) -1)]]
    setdesc "entselbyindex" "selects one or more entities using a list of indices, for example^n/entselbyindex $myentlist^n/entselbyindex [12 15 7]" "indexlist"
    setcomplete entselbyindex 1

    // main edit menu bound to F3
    new_ui edit [ ui_stay_open [
        ui_font emphasis [
            // ui_center [ui_edit_button2 [toggle edit mode] [edittoggle]]
            ui_center [ui_button "map info" [show_ui mapinfo]]
            ui_center [ui_button "geometry and texturing" [show_ui geometry]]
            ui_center [ui_button (format "texture list %1" (dobindsearch showtexgui edit)) [clear_ui ; showtexgui ]]
            ui_center [ui_button "editing options" [show_ui editoptions]]
            ui_center [ui_button "environment settings" [show_ui world]]
            ui_center [ui_button "texture variations" [show_ui textures]]
            ui_center [ui_button "find and edit entities" [show_ui ents]]
            ui_center [ui_button "configure key binds" [sleep 50 [curbindtype = 2]; show_ui options_menu_controls]]
        ]
        guistatus (format "Press %1 to show this menu as a compass" (dobindsearch [showcompass editing] edit))
    ] ]

    // this one could go to config/compass.cfg, but it is convenient to have it here for comparison
    newcompass editing $editingtex [
        //compass "^fs^fcQ^fSuit editing" Q [edittoggle]
        compass "m^fs^fca^fSp info" A [show_ui mapinfo]
        compass "^fs^fcg^fSeometry and texturing" G [show_ui geometry]
        compass "^fs^fct^fSexture list" T [showtexgui]
        compass "e^fs^fcd^fSiting options" D [show_ui editoptions]
        compass "^fs^fcw^fSorld variables" W [show_ui world]
        compass "texture ^fs^fcv^fSariations" V [show_ui textures]
        compass "^fs^fcf^fSind and edit entities" F [show_ui ents]
        compass "^fs^fcc^fSonfigure key binds" C [sleep 50 [curbindtype = 2]; show_ui options_controls]
        // shown below:
        compass (format "^fs^fce^fSscape (toggle this compass) ^n^n%1 show this as a menu" (dobindsearch [show_ui edit] edit)) E [clear_ui 1]
    ]

    new_ui mapinfo [ ui_stay_open [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        ui_list [
            ui_no_cursor_fx [ ui_center [ui_image $mapname [] 9 1 "textures/emblem"]]
            ui_strut 2
            ui_center [
                ui_list [
                    ui_textfield maptitle -20
                    ui_strut 2
                    ui_text  "map title"
                ]
                ui_list [
                    ui_textfield mapauthor -20
                    ui_strut 2
                    ui_text  "map author"
                ]
                ui_list [
                    ui_textfield mapname -20
                    ui_strut 2
                    ui_text  "file name"
                ]
                ui_list [
                    ui_textfield mapmusic -20
                    ui_strut 2
                    ui_text  "music"
                    ui_spring 1
                    ui_button "pick" [show_ui music]
                ]
                ui_list [
                    ui_textfield numplayers 4
                    ui_text " num"
                    ui_strut 2
                    ui_textfield maxplayers 4
                    ui_text " max"
                    ui_strut 2.2
                    ui_text  "players"
                ]
                ui_strut 0.5
                ui_list [
                    ui_text "switch teams at half-time:"
                    ui_spring 1
                    ui_radiobutton "off" mapbalance 0
                ]
                ui_list [
                    ui_spring 1
                    ui_radiobutton "in ctf, dnc and bb" mapbalance 1
                    ui_spring 1
                    ui_radiobutton "in all modes" mapbalance 2
                ]
                ui_strut 0.5
                ui_list [
                    ui_text (format "map revision: %1" $maprevision)
                    ui_spring 1
                    ui_edit_button2 "save the map" [savemap]
                ]
                ui_text (format "map file format version: %1" $mapversion)
                ui_strut 0.5
                ui_text (format "map size: 2^^%1 or %2 cubes = %3m" $mapsize (<< 1 $mapsize) (<< 1 (- $mapsize 3 )))
                ui_list [
                    ui_spring 1
                    ui_edit_button2 "double" [mapenlarge]
                    ui_spring 1
                    ui_edit_button2 "shrink to non-empty volume" [shrinkmap]
                ]
                ui_strut 0.5
                if (isonline) [ ui_edit_button "refresh map from server" getmap ]
                ui_button "edit the config file" [notepad @mapname.cfg] []
                ui_button "reset the texture list" [show_ui texreset] [] $warningtex
            ]
        ]

        ui_tab "waypoints and routes"
        if (= 0 $guipasses) [ idmax = 3 ; count = "" ]
        ui_strut 1
        ui_center [
            ui_list [
                ui_font "emphasis" [ui_text "waypoints"]
                ui_strut 1
                ui_text "serve as navigation meshes for bots" $privbottex
                ui_edit_checbox "show waypoints" showwaypoints
                ui_edit_checbox "drop waypoints as you move" dropwaypoints
                ui_strut 0.5
                ui_edit_button "^fyclear^fw all waypoints" clearwaypoints
                ui_edit_button "clear waypoints in selected volume" delselwaypoints
                ui_strut 0.5
                if (findfile (format "%1.wpt" $mapname)) [
                    ui_edit_button "reload waypoints from file" [loadwaypoints]
                    ui_edit_button "save and ^fyoverwrite^fw waypoints file" [savewaypoints]

                ] [
                    ui_text "^fano waypoint file found for this map"
                    ui_edit_button "save current waypoints" [savewaypoints]
                ]
                ui_strut 0.5
                ui_text "^faoptions for testing"
                ui_list [
                    ui_button "raise or " [gamespeed (+25 $gamespeed)]
                    ui_button "lower gamespeed:" [gamespeed (-25 $gamespeed)]
                    ui_spring 1
                    ui_textfield gamespeed 4
                    ui_text "%"
                ]
                ui_list [
                    ui_edit_button "add bot" addbot
                    ui_spring 1
                    ui_edit_button "remove bot" delbot
                ]

            ]
            ui_strut 1
            ui_bar 5 1 0xffffff
            ui_strut 1
            ui_list [
                ui_font "emphasis" [ui_text "routes"]
                ui_strut 20 1
                ui_strut 1
                ui_text "show players the way (optional)" $moderacetex
                ui_text "^fashow routes"
                ui_list [
                    ui_radiobutton "disabled" routeid -1
                    ui_spring 1
                    ui_button "deselect all" [entcancel]
                ]
                loop i $idmax [
                    ui_list [
                        ui_radiobutton (format "id %1" $i) routeid $i
                        ui_spring 1
                        ui_button "select" [entcancel; entfind route @i]
                    ]
                ]
                ui_list [
                    ui_text "^falook up more routes "
                    ui_spring 1
                    ui_textfield idmax 3
                ]
                ui_strut 0.5
                ui_edit_checbox "drop route ents as you move" droproute
                ui_strut 0.5
                ui_text (format "^faselected entities: %1" (enthavesel))
                ui_button "^fydelete^fw all selected ents" [delent]
                ui_strut 0.5
            ]
        ]

        ui_tab load
        local [maps whichmaps]
        if (= 0 $guipasses) [
            whichmaps = 0
            selmap = $mapname
            slider = 0
            maps = $mapname
            imax = 1
        ]
        ui_text "pick a map and click the preview image to start a new editing session"
        ui_strut 2
        ui_list [
            ui_list [
                ui_text "browse maps:"
                ui_radiobutton "current map" whichmaps 0 [slider = 0; maps = "" ; selmap = $mapname;]
                ui_radiobutton "custom" whichmaps 1 [
                    slider = 0
                    maps = ""
                    imax = 0
                    loopfiles i "maps" mpz [
                        append maps $i
                    ]
                    maps = (listdel $maps $mainmaps)
                    maps = (listdel $maps $racemaps)
                ]
                ui_radiobutton "downloaded" whichmaps 2 [
                    slider = 0
                    maps = ""
                    loopfiles i "maps" mpz [
                        if (< -1 (stringstr $i "_0x")) [append maps $i]
                    ]
                ]
                j = 3
                looplist i [small medium large capture defend bomber race duel multi hold king] [
                    ui_radiobutton $i  whichmaps $j [slider = 0; maps = $[@@[i]maps]]
                    j = (+ 1 $j)
                ]
            ]
            ui_spring 1
            imax = (listlen $maps)
            if (imax) [
                ui_list [
                ui_loop_scrollbar j $imax 15 30 [
                    p = (at $maps $j)
                    ui_button $p [selmap = @p]
                ]
                ]
            ] [
                ui_strut 33
            ]
            ui_spring 1
            ui_list [
                ui_center [ui_image [maps/@selmap] [edit @selmap] 5 1 $emblemtex]
                ui_center [ui_text $selmap]
                ui_strut 0.5
                ui_center [ui_text "^fyWARNING:"]
                ui_center [ui_text "unsaved changes are lost"]
            ]
        ]
    ] ]

    new_ui music [ ui_stay_open [
        loopfiles i "sounds/music" "ogg" [
            ui_list [
                ui_button $i [music music/@i] [] $arrowrighttex
                ui_spring 1
                if $editing [
                    ui_button "" [mapmusic sounds/music/@i; clear_ui 1] [] $editingtex
                ]
            ]
        ]
        ui_list [
        ui_button clear [music ""] [] $warningtex
            if $editing [
                ui_spring 1
                ui_button "" [mapmusic ""; clear_ui 1] [] $editingtex
            ]
        ]
    ] ]

    new_ui editoptions [ ui_stay_open [
        guistatus "some buttons have tooltips and alt-actions for console commands"
        ui_list [
            ui_list [
                ui_edit_checbox "show material volumes" showmat
                ui_edit_checbox "toggle allfaces texture mode" "allfaces"
                ui_strut 0.5
                ui_edit_checbox "enable selection and editing of entities" entediting
                ui_edit_checbox "snap ents to the grid as they are moved" entselsnap
                ui_strut 0.5
                ui_text "^faplace new entities:"
                i = (& $entdrop 2)
                ui_bitfield "on selected area" entdrop 2
                // next checkbox is just the opposite of this bitfield
                ui_checkbox "on camera position"  i 0 2 [entdrop (^ $entdrop 2)]
                ui_bitfield "drop to ground" entdrop 1
                ui_strut 0.5
                ui_list [
                    ui_text "threads to compute lightmaps in parallel "
                    ui_spring 1
                    ui_textfield lightthreads 2
                ]
                ui_list [
                    ui_text "precision for quicklight"
                    ui_spring 1
                    ui_textfield lightprecisionquick 4
                ]
                ui_list [
                    ui_text "adjust your float speed "
                    ui_spring 1
                    ui_textfield floatspeed 6 [ floatspeed $floatspeed ]
                ]
                ui_strut 0.5
                ui_text (format "grid size: %1 cubes =  %2m" (<< 1 $gridpower) (<< 1 (- $gridpower 3)))
                ui_slider gridpower 0 (min (- $mapsize 1) 12)
                ui_strut 0.5
                ui_text "undo cache size in MB"
                ui_slider undomegs 1 10
            ]
        ]

        ui_tab view
        ui_edit_checbox "show distance to selection" "crosshairdistance"
        ui_strut 0.5
        ui_edit_checbox "toggle outline" "outline"
        ui_button "change outline colour" [pick_color outlinecolour] [] $editingtex
        ui_strut 0.5
        ui_edit_checbox "toggle fullbright" "fullbright"
        ui_edit_checbox "toggle wireframe" "wireframe"
        ui_edit_checbox "toggle blank geometry" "blankgeom"
        ui_strut 0.5
        ui_edit_checbox "toggle paste grid" showpastegrid
        ui_edit_checbox "toggle cursor grid" showcursorgrid
        ui_edit_checbox "toggle selection grid" showselgrid
        ui_strut 0.5
        ui_edit_checbox "show edit radar:" showeditradar
        ui_radiobutton "sectional compass" editradarstyle 0
        ui_radiobutton "compass-distance " editradarstyle 1
        ui_radiobutton "full hud" editradarstyle 2
        ui_list [
            ui_radiobutton "minimap" editradarstyle 3
            ui_spring 1
            ui_button "refresh" [minimapsize @minimapsize]
            ui_spring 1
            ui_button "customize" [show_ui world 6]
        ]
        ui_strut 1
        ui_list [
            ui_text "radar distance (zoom out)"
            ui_spring 1
            ui_radiobutton "full map size" editradardist 0
        ]
        ui_slider editradardist 16 (<< 1 $mapsize)

        if (isonline) [
            ui_tab lock
            ui_text "admins can restrict editing by auth rank"
            ui_list [
                ui_text "when the server is locked:"
                ui_spring 1
                ui_edit_button open [mastermode 0]
                ui_spring 1
                ui_edit_button lock [mastermode 2]
                ui_spring 1
            ]
            ui_text (format "current master mode: %1" (getmastermode 1))
            ui_strut 0.5
            ui_list [
                ui_spring 1
                looplist v [spawn edit] [
                    ui_text $v
                    ui_spring 1
                ]
            ]
            i = 0
            looplist priv [none player supporter moderator operator administrator developer founder] [
                ui_list [
                    ui_spring 1
                    looplist v [spawneditlock editlock] [   // spawnlock connectlock varslock ... keep it simple
                        ui_body [
                        	ui_image [textures/privs/@priv] [] 0.8 1 "" [] (? (>= $i $$v) 0x00ff00 0xff0000)
                        ] [@v @i] [saycommand /@v @i] [ui_tooltip [/@@v @@i]]
                        ui_spring 1
                    ]
                ]
                i = (+ 1 $i)
            ]
        ]
    ] ]

    prefabcopy = 1

    new_ui geometry [ ui_stay_open [
        ui_strut 0.5
        guistatus "some buttons have tooltips and alt-actions for console commands"
        ui_edit_button "undo" [undo; passthroughsel 0]
        ui_edit_button "redo" redo
        ui_strut 0.5
        ui_edit_button "pull cube" [universaldelta -1]
        ui_edit_button "push cube" [universaldelta 1]
        ui_edit_button "push / pull faces " [domodifier 2]
        ui_edit_button "push / pull corners" [domodifier 3]
        ui_strut 0.5
        ui_edit_button "deselect" "cancelsel"
        ui_strut 0.5
        ui_edit_button "delete selection (ents first)" editdel
        ui_edit_button "copy" "editcopy"
        ui_edit_button "paste" "editpaste"
        ui_edit_button "^fdcut and paste on release" "editcut"
        ui_strut 0.5
        ui_edit_button "flip" "editflip"
        ui_edit_button "^fdrotate via mouse wheel" [domodifier 4]
        ui_strut 0.5
        ui_edit_button "toggle heightmap mode" [hmapedit (! $hmapedit); blendpaintmode 0]
        ui_edit_button "^fdcycle heightmap brushes via mouse wheel" [domodifier 9]
        ui_strut 0.5
        ui_text (format "copy/paste is scaled by the grid size: %1 cubes =  %2m" (<< 1 $gridpower) (<< 1 (- $gridpower 3)))
        ui_slider gridpower 0 (min (- $mapsize 1) 12)
        ui_edit_button "^fdchange via mouse wheel" [domodifier 1]

        ui_tab "prefabs"
        if (= 0 $guipasses) [
            prefabs = ""; slider = 0; pnew = "tmp"
            loopfiles i "prefab" "obr" [append prefabs $i]
        ]
        ui_strut 1
        ui_list [
            ui_edit_button "save the selected geometry as" [saveprefab @pnew]
            ui_strut 1
            ui_textfield pnew 10
        ]
        imax = (listlen $prefabs)
        if (imax) [
            ui_list [
                ui_text "^fapick a prefab to "
                ui_radiobutton (format "paste with %1 " (dobindsearch editpaste edit)) prefabcopy 1
                ui_radiobutton " insert on the map" prefabcopy 0
            ]
            ui_strut 1
            ui_list [
                ui_list [
                    ui_loop_scrollbar j $imax 15 15 [
                        p = (at $prefabs $j)
                        ui_button $p [
                            copyprefab @p
                            if $prefabcopy pastehilight paste
                            clear_ui
                        ]
                    ]
                ]
                ui_no_cursor_fx [ui_prefab_preview $guirollovername -1 [] 7.5 1 []]
            ]
        ] [
            ui_text "no obr files found in home/prefab"
        ]
        ui_strut 1
        ui_text (format "prefabs are scaled by the grid size: %1 cubes =  %2m" (<< 1 $gridpower) (<< 1 (- $gridpower 3)))
        ui_slider gridpower 0 (min (- $mapsize 1) 12)

        ui_tab texturing
        ui_strut 1
        ui_center [ui_text "apply textures to the selected geometry"]
        ui_strut 1
        ui_body [
                ui_button "view the texture list"
                ui_strut 1
                ui_spring 1
                ui_button (dobindsearch showtexgui edit)
        ] [clear_ui ; showtexgui ] [saycommand /showtexgui] [ui_tooltip showtexgui]
        ui_edit_button "^fdcycle textures via mouse wheel" [domodifier 6]
        ui_edit_button "grab texture from selected face" gettex
        ui_edit_button "repeat last texture change across the entire map" [allfaces 0; replace]
        ui_edit_button "repeat last texture change inside selection" [allfaces 0; replaceinsel]
        ui_edit_button "create texture variations" [show_ui textures]
        ui_edit_button "create a blendmap" [show_ui textures 6]
        ui_strut 1
        ui_center [ui_text "apply lighting to the textured geometry"]
        ui_strut 1
        ui_edit_button "create some light entities" [show_ui lights]
        ui_edit_button "optimize the geometry" remip
        ui_edit_button "patch existing lightmap" [fullbright 0; patchlight]
        ui_edit_button "quick lightmap calculation" [quicklight]
        ui_edit_button "full lightmap calculation" [fullbright 0; calclight 1]
        ui_strut 0.5
        ui_text "^falight precision - larger values imply less detail:"
        ui_center [
            ui_center [ui_button "finer" [lightprecision (div $lightprecision 2)]]
            ui_strut 1
            ui_textfield lightprecision 4 [] (case (div $lightprecision 32) 0 [
                result 0xff0000 ] 1 [ result 0xff6000 ] 2 [
                result 0xffa000 ] 3 [ result 0xffff00 ] 4 [
                result 0x80ff00 ] 5 [ result 0x00ff00 ] () [ result 0x008000 ])
            ui_strut 1
            ui_center [ui_button "coarser" [lightprecision (* $lightprecision 2)]]
        ]
        ui_strut 0.5
        ui_list [
            ui_text "see also:"
            ui_spring
            ui_button "lightmap variables" [show_ui world 2]
            ui_spring
            ui_button "editing options" [show_ui editoptions 1]
        ]
    ] ]

    new_ui world [ ui_stay_open [
        if (= 0 $guipasses) [coastval = 1.0]
        guistatus "some buttons have tooltips and alt-actions for console commands"
        ui_center [ui_text "world variables control the map specific environment"]
        ui_strut 1
        ui_edit_button "apply and customize material volumes" [show_ui materials]
        ui_button "advanced search for world ^fgvariables" [sleep 500 [varflags = 8; vartypes = 7; varsmore = 1]; show_ui vars]
        ui_button "advanced search for world ^fgaliases" [sleep 500 [varflags = 8; vartypes = 16; varsmore = 1]; show_ui vars]
        ui_edit_button "^fyRESET ALL^fw world variables" [show_ui resetworldvars]
        ui_strut 1
        ui_text "game physics:"
        ui_strut 0.5
        ui_list [
            ui_textfield gravity 15
            ui_strut 1
            ui_text "gravity"
        ]
        ui_list [
            ui_textfield floorcoast 15
            ui_strut 1
            ui_text "floor coast (slipperiness)"
        ]
        ui_list [
            ui_textfield coastval 15 [sleep 50 [vcoastscale @coastval]]
            ui_strut 1
            ui_text "scale coast on selected texture"
        ]
        ui_list [
            ui_textfield aircoast 15
            ui_strut 1
            ui_text "air coast"
        ]
        ui_list [
            ui_textfield liquidcoast 15
            ui_strut 1
            ui_text "liquid coast"
        ]
        ui_list [
            ui_textfield liquidspeed 15
            ui_strut 1
            ui_text "liquid speed"
            ui_spring 1
            ui_button "^fyreset^fa these physics settings" [
                looplist i [gravity floorcoast aircoast liquidcoast liquidspeed] [
                    do [@i (getfvardef @i)]
                ]
            ]
        ]
        ui_strut 0.5

        ui_tab lighting
        ui_center [ui_text "Lighting uses precomputed lightmaps"]
        ui_list [
            ui_center [ui_text "lightprecision: "]
            ui_strut 3
            ui_center [ui_button "finer" [lightprecision (div $lightprecision 2)]]
            ui_strut 1
            ui_textfield lightprecision 4 [] (case (div $lightprecision 32) 0 [
                result 0xff0000 ] 1 [ result 0xff6000 ] 2 [
                result 0xffa000 ] 3 [ result 0xffff00 ] 4 [
                result 0x80ff00 ] 5 [ result 0x00ff00 ] () [ result 0x008000 ])
            ui_strut 1
            ui_center [ui_button "coarser" [lightprecision (* $lightprecision 2)]]
        ]
        ui_text "^falarger values imply less detail, smaller mpz files, faster calculations"
        ui_strut 1.5
        ui_list [
            ui_list [
                ui_edit_button "create some light ents" [show_ui lights]
                ui_edit_button "patch lightmap " [fullbright 0; patchlight]
                ui_edit_button "quick calculation" [quicklight]
                ui_edit_button "full calculation" [fullbright 0; calclight 1]
                ui_strut 1
                ui_strut 30 1
                ui_text "colours (0 = disabled)"
                ui_list [
                    ui_text "^fascale lightmap"
                    ui_spring 1
                    ui_text "colour   " "" $lightmapcolour
                    ui_button "pick" [pick_color lightmapcolour] [] $editingtex
                ]
                ui_list [
                    ui_text "^faambient lighting"
                    ui_spring 1
                    ui_text "colour   " "" $ambient
                    ui_button "pick" [pick_color ambient] [] $editingtex
                ]
                ui_list [
                    ui_text "^faactor shadows"
                    ui_spring 1
                    ui_text "colour   " "" $shadowmapambient
                    ui_button "pick" [pick_color shadowmapambient] [] $editingtex
                ]
                ui_list [
                    ui_text "^faimplied sunlight"
                    ui_spring 1
                    ui_text "colour   " "" $skylight
                    ui_button "pick" [pick_color skylight] [] $editingtex
                ]
            ]
            ui_spring 1
            ui_list [
                ui_text "^faadvanced options:"
                ui_text "^fause with caution"
                ui_list [
                    ui_textfield lightlod 3
                    ui_strut 0.3
                    ui_text "^falevel of detail"
                ]
                ui_list [
                    ui_textfield lighterror 3
                    ui_strut 0.3
                    ui_text "^faerror"
                ]
                ui_list [
                    ui_textfield lightcompress 3
                    ui_strut 0.3
                    ui_text "^facompress"
                ]
                ui_list [
                    ui_textfield blurlms 3
                    ui_strut 0.3
                    ui_text "^fablurr"
                ]
                ui_strut 1
                ui_list [
                    ui_button "^fyreset all" [
                        looplist i [lightmapcolour ambient shadowmapambient skylight lighterror lightcompress blurlms lmaa] [
                            do [@i (getsvardef @i)]
                        ]
                    ]
                ]
            ]
            ui_strut 2
        ]

        ui_tab fog
        ui_center [ui_text "customize background and fog colours"]
        ui_strut 1
        ui_list [
            ui_text "fog: blends to full opacity at distance   "
            ui_textfield fog 8
            ui_spring 1
            ui_text "colour   " "" $fogcolour
            ui_button "pick" [pick_color fogcolour] [] $editingtex
        ]
        ui_strut 1
        ui_list [
            ui_text "sky background:"
            ui_spring 1
            ui_checkbox "glare" skybgglare
            ui_strut 6
            ui_text "colour   " "" $skybgcolour
            ui_button "pick" [pick_color skybgcolour] [] $editingtex
        ]
        ui_text "   shown when no skybox is present, useful for transparent skyboxes"
        ui_strut 1.5
        ui_list [
            ui_text "fog dome: a background colour gradient for the sky"
            ui_spring 1
            ui_text "colour   " "" $fogdomecolour
            ui_button "pick" [pick_color fogdomecolour] [] $editingtex
        ]
        ui_strut 0.5
        ui_list [
            ui_strut 8
            ui_textfield fogdomeheight 5
            ui_strut 1
            ui_text "lower sky height for a max blend value of"
            ui_spring 1
            ui_textfield fogdomemax 5
        ]
        ui_strut 0.5
        ui_list [
            ui_strut 8
            ui_textfield fogdomeclip 5
            ui_strut 1
            ui_text "upper sky height for a min blend value of"
            ui_spring 1
            ui_textfield fogdomemin 5
        ]
        ui_strut 0.5
        ui_list [
            ui_strut 8
            ui_edit_checbox "cap (close) the fog dome at the bottom" fogdomecap
        ]
        ui_strut 0.5
        ui_edit_button "edit fog and colour properties of water and lava materials" [show_ui materials]

        ui_tab sky
        ui_center [ui_text "create a background scenery using skyboxes and cloud layers"]
        ui_strut 1
        if (concatword $skybox $cloudbox $cloudlayer $envlayer) [
            ui_list [
                ui_spring 1
                looplist j [colour blend spin yaw glare] [
                    ui_text (tabify $j 2)
                ]
            ]
        ] [
            ui_right [ui_text "pick a skybox a or layer to show more options"]
        ]
        ui_strut 0.3
        looplist i [sky cloud cloudlayer envlayer] [
            b = "box"
            if (< -1 (stringstr $i layer)) [b = ""]
            ui_list [
                ui_button [@i@b] [show_ui [@@i@@b]] [saycommand /show_ui [@@i@@b]] $editingtex
                ui_spring 1
                if $[@i@b] [
                    ui_button "pick" [pick_color @@[i]colour] [] $editingtex $[@[i]colour]
                    ui_strut 2
                    ui_textfield [@[i]blend] 5
                    ui_strut 1
                    looplist j [spin yaw] [
                        // one exception: spin/yawclouds, not cloud
                        if (=s $i cloud) [
                            ui_textfield [@[j]clouds] 5
                        ] [
                            ui_textfield [@j@i] 5
                        ]
                        ui_strut 1
                    ]
                    ui_checkbox " " [@[i]glare]
                    ui_strut 3.5
                    ui_image $ui_exit_texture [[@@i@@b] ""] 0.5
                ] [
                    ui_strut 1.25 1
                ]
            ]
        ]
        ui_strut 0.7
        if (concatword $cloudlayer $envlayer) [
            ui_list [
                ui_spring 1
                looplist j [fade height scrollx scrolly scale subdiv] [ // skipped: offset
                    ui_text (tabify $j 2)
                ]
            ]
        ] [
            ui_right [ui_text "pick a cloud or env layer to show more options"]
        ]
        ui_strut 0.3
        looplist i [cloud env] [
            ui_list [
                ui_button $i [show_ui @[i]layer]  [saycommand /show_ui @[i]layer] $editingtex
                ui_spring 1
                if $[@[i]layer] [
                    looplist j [fade height scrollx scrolly scale subdiv] [
                        ui_textfield [@i@j] 5
                        ui_strut 1
                    ]
                ] [
                    ui_strut 1.25 1
                ]
            ]
        ]
        ui_strut 0.5
        ui_list [
            ui_text "examples with quick sky lighting:"
            ui_spring 1
            ui_button "night" [ambient 40 40 40; skylight 70 80 90; skybox skyboxes/stars; skycolour 255 255 255; cloudlayer skyboxes/clouds01; cloudlayercolour 50 60 100; cloudlayerblend 0.3; cloudscrollx 0.005; fogcolour 12 10 13; fog 2000; calclight -1]
            ui_spring 1
            ui_button "morning" [ambient 60 35 40; skylight 170 140 145; skybox freezurbern/harmony; skycolour 255 160 180; cloudlayer skyboxes/clouds03; cloudlayercolour 255 125 170; cloudlayerblend 0.2; cloudscrollx 0.01; fogcolour 129 79 59; fog 2000; calclight -1]
            ui_spring 1
            ui_button "evening" [ambient 50 30 10; skylight 180 100 70; skybox skyboxes/sunsetflat; skycolour 255 255 255; cloudlayer skyboxes/clouds01; cloudlayercolour 255 100 50; cloudlayerblend 0.2; cloudscrollx 0.005; fogcolour 40 20 5; fog 2000; calclight -1]
        ]
        ui_strut 0.5
        ui_button "^fyreset all^fw skybox and layer variables, but keep the textures" [
            looplist i [sky cloud envlayer cloudlayer] [
                looplist j [colour blend glare] [
                    do [@i@j (getsvardef @i@j)]
                ]
            ]
            looplist i [spin yaw] [
                looplist j [sky clouds cloudlayer envlayer] [
                    do [@i@j (getsvardef @i@j)]
                ]
            ]
            looplist i [cloud env] [
                looplist j [fade height scrollx scrolly scale subdiv] [
                    do [@i@j (getsvardef @i@j)]
                ]
            ]
        ]

        ui_tab grass
        ui_center [ui_text "pick a grass type to apply on all surfaces with"]
        ui_center [ui_text "the same texture as the current selection"]
        ui_strut 1
        ui_list [
            ui_list [
                ui_loop_list_split i 2 [
                    textures/grass
                    nobiax/grass01
                    nobiax/grass02
                    nobiax/grass03
                    nobiax/grassbush
                    luckystrike/grass_lucky
                    luckystrike/grass_lucky_alt
                    luckystrike/grass_lucky_bright
                    luckystrike/grass_lucky_green
                ] [
                    //ui_image $i ... non-square images are not aligned correctly atm
                    ui_font huge [ui_edit_button (format "^f(%1)" $i)  [setgrass @i] ]
                    ui_strut 0.2
                ]
                ui_edit_button "^fyclear^fa from texture" [setgrass ""]
            ]
            ui_spring 1
            ui_list [
                ui_strut 22 1
                ui_list [
                    ui_text "colour" "" $grasscolour
                    ui_spring 1
                    ui_button "pick" [pick_color grasscolour] [] $editingtex
                ]
                ui_list [
                    ui_textfield grassblend 8
                    ui_spring 1
                    ui_text "blend"
                ]
                ui_list [
                    ui_textfield grassheight 8
                    ui_spring 1
                    ui_text "height"
                ]
                ui_list [
                    ui_textfield grassscale 8
                    ui_spring 1
                    ui_text "scale down"
                ]
                ui_list [
                    ui_textfield grassanimmillis 8
                    ui_spring 1
                    ui_text "animmillis"
                ]
                ui_list [
                    ui_textfield grassanimscale 8
                    ui_spring 1
                    ui_text "animscale"
                ]
                ui_right [ ui_button "^fyreset" [
                    looplist i [colour blend height scale animmillis animscale] [
                        do [grass@i (getfvardef grass@i)]
                    ]
                ] ]
            ]
        ]
        ui_strut 1
        ui_center [ui_button "Tip: Looks better with texture blending" [show_ui textures 6]]

        ui_tab misc
        ui_center [ui_text "edit various world settings"]
        ui_strut 1
        ui_list [
            ui_list [
                ui_list [
                    ui_text "minimap settings: "
                    ui_spring 1
                    ui_edit_button2 "^fgrefresh" [minimapsize @minimapsize]
                ]
                ui_list [
                    ui_text "^fabackground"
                    ui_strut 1
                    ui_text "colour   " "" $minimapcolour
                    ui_button "pick" [pick_color minimapcolour] [] $editingtex
                ]
                ui_list [
                    ui_text "^fadrawn at height"
                    ui_strut 1
                    ui_textfield minimapheight 8
                    ui_spring 1
                ]
                ui_button "set height to current position" [minimapheight (at $getcampos 2)]
                ui_strut 0.2
                ui_checkbox "minimap clip" minimapclip
            ]
            ui_spring 1
            ui_list [
                ui_text "sky texture:" "textures/sky"
                ui_checkbox "apply sunlight" skytexturelight
                ui_checkbox "invisible (needs reload)" skytexture 0 1 [if $skytexture [
                    echo skytexture 1: skybox
                ] [
                    echo skytexture 0: invisible - takes affect after saving and loading]
                ]
                ui_strut 1
                ui_text "hurt material damage:"
                ui_list [
                    ui_textfield hurtdamage 2
                    ui_text " hp every "
                    ui_textfield hurtdelay 4
                    ui_text " ms"
                ]

            ]
            ui_spring 1
        ]
        ui_strut 1
        ui_list [
            ui_list [
                ui_text "point aliases:"
                ui_no_cursor_fx [ui_image $pointtex [] 2.5]
            ]
            ui_strut 2
            ui_list [
                ui_loop_list_split i 2 [1 2 3 4 5 6 7 8] [
                    do [ui_textfield point_@i -20]
                ]
            ]
        ]
    ] ]

    new_ui resetworldvars [
        ui_text "^fyCaution:^fw A reset of all map specific variables cannot be undone."
        ui_text "Therefore, it is recommended to save your map first."
        ui_text "Are you sure you wish to continue?"
        ui_strut 1
        ui_list [
            ui_edit_button "^fyYes, reset these vars" [resetworldvars]
            ui_spring 1
            ui_button "^fgNo, get me out of here" [clear_ui 1]
        ]
    ]

    cloudlist = [
        skyboxes/clouds01
        skyboxes/clouds02
        skyboxes/clouds03
        wicked/wickedcloudlayer1
        wicked/wickedcloudlayer2
    ]
    loopfiles i "torley/desat" png [
        if (= -1 (stringstr $i "_")) [
            append cloudlist [torley/desat/@i]
        ]
    ]

    new_ui cloudlayer [ ui_stay_open [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        ui_list [
            ui_list [
                ui_loop_scrollbar j (listlen $cloudlist) 10 40 [
                    p = (at $cloudlist $j)
                    if (=s $p $cloudlayer) [
                        ui_button $p [cloudlayer ""] [saycommand /cloudlayer ""] "" 0xffff00
                    ] [
                        ui_button $p [cloudlayer @p] [saycommand /cloudlayer @p]
                    ]
                ]
            ]
            ui_no_cursor_fx [ ui_image (? $guirollovername $guirollovername $cloudlayer) [] 5 1]
        ]
    ] ]
    // same thing for envlayer
    new_ui envlayer [ ui_stay_open [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        ui_list [
            ui_list [
                ui_loop_scrollbar j (listlen $cloudlist) 10 40 [
                    p = (at $cloudlist $j)
                    if (=s $p $envlayer) [
                        ui_button $p [envlayer ""] [saycommand /envlayer ""] "" 0xffff00
                    ] [
                        ui_button $p [envlayer @p] [saycommand /envlayer @p]
                    ]
                ]
            ]
            ui_no_cursor_fx [ ui_image (? $guirollovername $guirollovername $envlayer) [] 5 1]
        ]
    ] ]

    findskyboxes = [
        skyboxes = ""
        loopfiles dir data "" [
            looplist ext "png jpg" [
                loopfiles i $dir $ext [
                    if (> (stringstr $i "_up") -1) [
                        ibox = (format "%1/%2" $dir $i)
                        append skyboxes (substring $ibox 0 (stringstr $ibox "_"))
                    ]
                ]
            ]
        ]
    ]

    findskyboxes
    skyboxprev = 1
    cloudradio = 0

    new_ui skyboxes [ ui_stay_open [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        if (= 0 $guipasses) [
            slider = 0
            imax = (listlen $skyboxes)
        ]
        if (imax) [
            slidermax = (max (- $imax 10) 0)
            ui_list [
                ui_list [
                    ui_strut 1
                    ui_text "pick a texture set"
                    ui_strut 0.5
                    ui_radiobutton " as skybox" cloudradio 0
                    ui_radiobutton " as cloudbox" cloudradio 1
                    ui_strut 1
                    ui_checkbox "show a preview" skyboxprev
                    ui_strut 0.5
                    ui_text "cloudboxes are clipped"
                    ui_list [
                        ui_text "at height level "
                        ui_textfield cloudclip 4
                    ]
                ]
                ui_strut 10
                ui_list [
                    ui_strut 25 1
                    loop i (min 10 $imax) [
                        j = (+ $i $slider)
                        p = (at $skyboxes $j)
                        if $cloudradio [
                            ui_button $p [cloudbox @p] [] (format "../%1%2_%3" "<thumbnail>" $p "ft")
                        ] [
                            ui_button $p [skybox @p] [] (format "../%1%2_%3" "<thumbnail>" $p "ft")
                        ]
                    ]
                ]
                if (slidermax) [ui_slider slider 0 $slidermax [] 1 1]
                ui_spring
                p = $guirollovername
                if $p [] [
                    // no mouse-over preview: Show current sky or cloud box
                    if $cloudradio [p = $cloudbox] [p = $skybox]
                ]
                if $skyboxprev [ui_no_cursor_fx [ ui_image (format "%1%2_%3" "<thumbnail:128>" $p "up") [] 5 0 "textures/sky"]]
            ]
            if $skyboxprev [
                ui_list [
                    looplist k "ft lf bk rt" [
                        ui_no_cursor_fx [ ui_image (format "%1%2_%3" "<thumbnail:128>" $p $k) [] 5 0 "textures/sky"]
                    ]
                ]
                ui_list [
                    ui_list [
                        ui_strut 2
                        if $skybox [
                            ui_button (format "the current skybox is %1 ^fy(clear)" $skybox ) [skybox ""]
                        ] [
                            ui_text "no skybox is currently in use"
                        ]
                        ui_strut 2
                        if $cloudbox [
                            ui_button (format "the current cloudbox is %1 ^fy(clear)" $cloudbox ) [cloudbox ""]
                        ] [
                            ui_text "no cloudbox is currently in use"
                        ]
                        ui_strut 2
                        ui_text "Tip: A preview is shown as you move the mouse over the above list"
                    ]
                    ui_spring
                    ui_no_cursor_fx [ ui_image (format "%1%2_%3" "<thumbnail:128>" $p "dn") [] 5 0 "textures/sky"]
                ]
            ]
        ] [
            ui_text "no skyboxes found - this should not happen."
        ]
    ]  ]

    new_ui skybox [
        cloudradio = 0
        clear_ui 1
        show_ui skyboxes
    ]

    new_ui cloudbox [
        cloudradio = 1
        clear_ui 1
        show_ui skyboxes
    ]

    new_ui textures [ ui_stay_open [
        if (getseltex) [
            guistatus "some buttons have tooltips and alt-actions for console commands"
            if (= $guipasses 0) [varpix = 32 ; vara = 0.5; vara2 = 0.0]
            ui_strut 0.5
            ui_center [ui_text "v-commands create variations of the currently selected texture"]
            ui_center [
                ui_edit_button2 "reset variation on selected texture" [vreset]
                ui_spring 1
                ui_button "^fyclear^fw unused textures"  [show_ui texreset]
            ]
            ui_strut 0.5
            ui_list [
                ui_spring 1
                ui_box2 -1 0xffffff [
                    ui_text "rotation or flip:"
                    ui_right [ui_edit_button2 "90 degree"  [vrotate 1]]
                    ui_right [ui_edit_button2 "180 degree"  [vrotate 2]]
                    ui_right [ui_edit_button2 "270 degree"  [vrotate 3]]
                    ui_right [ui_edit_button2 "left to right" [vrotate 4]]
                    ui_right [ui_edit_button2 "top to bottom" [vrotate 5]]
                    ui_right [ui_edit_button2 "reset"  [vrotate 0]]
                ]
                ui_spring 1
                ui_box2 -1 0xffffff [
                    ui_text "texture offset:"
                    ui_edit_button "move up" [vdelta voffset 0 (* -1 @varpix)] [] $arrowtex
                    ui_edit_button "move down" [vdelta voffset 0  @varpix] [] $arrowdowntex
                    ui_edit_button "move left" [vdelta voffset (* -1 @varpix) 0] [] $arrowlefttex
                    ui_edit_button "move right" [vdelta voffset @varpix 0] [] $arrowrighttex
                    ui_list [
                        ui_text "^faby "
                        ui_textfield varpix 3
                        ui_text " ^fapixels"
                    ]
                    ui_edit_button "reset offset" [voffset 0 0] []
                ]
                ui_spring 1
                ui_box2 -1 0xffffff [
                    ui_text "scale:"
                    ui_edit_button "800.0%" [vscale 8]
                    ui_edit_button "400.0%" [vscale 4]
                    ui_edit_button "200.0%" [vscale 2]
                    ui_edit_button "100.0%" [vscale 1]
                    ui_edit_button " 50.0%" [vscale 0.5]
                    ui_edit_button " 25.0%" [vscale 0.25]
                    ui_edit_button " 12.5%" [vscale 0.125]
                ]
                ui_spring 1
                ui_box2 -1 0xffffff [
                    ui_text "scrolling:"
                    ui_edit_button "+horizontal" [vdelta vscroll 0.1 0]
                    ui_edit_button "-horizontal" [vdelta vscroll -0.1 0]
                    ui_edit_button "+vertical" [vdelta vscroll 0 0.1]
                    ui_edit_button "-vertical" [vdelta vscroll 0 -0.1]
                    ui_edit_button "stop moving" [vscroll 0 0]
                ]
                ui_spring 1
            ]
            ui_strut 1
            ui_center [
                ui_box2 -1 0xffffff [
                    ui_text "customize the opacity when inside alpha material:"
                    ui_list [
                        ui_textfield vara 3
                        ui_text " front side"
                        ui_strut 3
                        ui_textfield vara2 3
                        ui_text " back side"
                        ui_strut 3
                        ui_edit_button2 "apply" [valpha @vara @vara2]
                    ]
                ]
            ]

            ui_tab colour
            if (= $guipasses 0) [hex = 0xffffff; colr = 1.0; colg = 1.0; colb = 1.0]
            ui_list [
                ui_strut 2
                ui_image (gettexname $getseltex) [vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255)] 2.5 1 "" [] $hex
                ui_center [ui_text " = "]
                ui_image (gettexname $getseltex) [vcolour 1 1 1] 2.5 1
                ui_center [ui_text " * "]
                ui_hex_preview $hex multiplier
                ui_strut 2
                ui_list [
                    ui_rgb_sliders hex
                ]
            ]
            ui_strut 0.5
            ui_list [
                ui_edit_button2 "click the preview to apply the texture" [vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255)]
                ui_spring
                ui_edit_button2 "or click here to multiply the current colour" [vdelta vcolour @(divf $colr 255) @(divf $colg 255) @(divf $colb 255)]
            ]
            ui_strut 1.5
            ui_palette 0.6

            ui_tab palette
            if (= 0 $guipasses) [tb = 0; wb = 0]
            ui_strut 1
            ui_center [ui_text "palettes allow to colourize a texture dynamically"]
            ui_strut 1
    	    ui_list [
    	        ui_list [
                    ui_text "disabled"
    	            ui_image $warningtex [vpalette 0 0] 1.5 1
    	        ]
                ui_spring 1
    	        ui_list [
                    ui_text "flashing colours"
    	            ui_list [
    	                ui_image $burningtex [vpalette 0 1] 1.5 1
    	                ui_image $modekaboomtex [vpalette 0 2] 1.5 1
    	                ui_image $bombtex [vpalette 0 3] 1.5 1
    	                ui_image $shockingtex [vpalette 0 4] 1.5 1
    	            ]
                    ui_strut 1
    	        ]
                ui_spring 1
    	        ui_list [
                    ui_text "team colour variables"
    	            ui_list [
    	                ui_image $modeffatex [vpalette 1 0] 1.5 1
                        i = 1
                        looplist p [alpha omega kappa sigma ] [
    	                    ui_image $[team@[p]tex] [vpalette 1 (+ @i $tb)] 1.5 1 "" [] $[team@[p]colour]
                            i = (+ 1 $i)
    	                ]
    	            ]
                    ui_checkbox "only applied in team games" tb 0 5
    	        ]
    	    ]
            ui_strut 1
            ui_text "weapon colour variables"
    	    ui_list [
    	        loop i $weapidxnum [
                    w = (at $weapname $i)
    	            if (!=s $w "melee") [ui_image $[@[w]tex] [vpalette 2 (+ @i $wb)] 1.5 1 "" [] $[@[w]colour]]
    	        ]
    	    ]
            ui_checkbox "only applied if the weapon is allowed to spawn" wb 0 $weapidxnum

            ui_tab shaders
            if (= $guipasses 0) [
                par1 = 0.05
                par2 = 0.0
                pt = 1.0
            ]
            ui_strut 0.5
            ui_center [ ui_text "certain textures support additional shader settings"]
            ui_center [ ui_text "pick a colour and set the corresponding shader parameter"]
            ui_center [ ui_button "tip: Use black to disable a shader" [hex = 0x000000]]
            ui_strut 0.5
            ui_center [
                ui_box -1 0xffffff [
                    ui_strut 1
                    ui_hex_preview $hex multiplier
                    ui_strut 1
                    ui_center [
                        ui_rgb_sliders hex
                    ]
                    ui_strut 3
                    ui_list [
                        ui_edit_button "specular (gloss)" [vdelta vshaderparam specscale @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ]
                        ui_edit_button "glow effect (shine)" [vdelta vshaderparam glowcolor @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ]
                        ui_edit_button "pulsing glow effect" [vdelta vshaderparam pulseglowcolor @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ]
                        ui_edit_button "envmap (reflection)" [vdelta vshaderparam envscale @(divf $colr 255) @(divf $colg 255) @(divf $colb 255) ]
                    ]
                    ui_strut 1
                ]
            ]
            ui_strut 0.5
            ui_center [
                ui_box2 -1 0xffffff [
                    ui_list [
                        ui_textfield pt 4
                        ui_strut 1
                        ui_text "Hz"
                        ui_strut 3
                        ui_edit_button2 "set pulse speed" [vdelta vshaderparam pulsespeed @pt]
                        ui_strut 3
                        ui_edit_button2 "set pulseglow speed" [vdelta vshaderparam pulseglowspeed @pt]
                    ]
                ]
            ]
            ui_strut 0.5
            ui_center [
                ui_box2 -1 0xffffff [
                    ui_text "parallax deforms the texture for a pseudo-3d-effect based on the normal map:"
                    ui_strut 0.5
                    ui_list [
                        ui_textfield par1 12
                        ui_strut 1
                        ui_text "scaling factor "
                        ui_spring 1
                        ui_textfield par2 12
                        ui_strut 1
                        ui_text "offset"
                        ui_spring 1
                        ui_edit_button2 "^fgapply" [vdelta vshaderparam parallaxscale @par1 @par2]
                        ui_spring 1
                    ]
                    ui_list [
                        ui_text "some examples:"
                        ui_spring 1
                        ui_button "moderate"   [par1 = 0.02; par2 = 0.0]
                        ui_spring 1
                        ui_button "strong"     [par1 = 0.05; par2 = 0.0]
                        ui_spring 1
                        ui_button "inverted"   [par1 = -0.02; par2 = 0.0]
                        ui_spring 1
                        ui_button "soft blob"  [par1 = 0.00; par2 = -0.02]
                        ui_spring 1
                        ui_button "fish eye"   [par1 = 0.00; par2 = 2.0]
                        ui_spring 1
                    ]
                ]
            ]

            ui_tab blend
            if (= 0 $guipasses) [slotnum = 1]
            ui_strut 1
            ui_center [ui_text "a blendmap creates smooth transitions on two-layer textures"]
            ui_strut 1
            ui_list [
            ui_button "first look up a texture index in the texture list:  #" [clear_ui; showtexgui]
                ui_strut 1
                ui_textfield slotnum 4
            ]
            ui_edit_button "add the index as texture layer to the current selection" [vlayer @slotnum]
            ui_strut 1
            ui_edit_button "reset texture layer on selection" [vlayer ""]
            ui_strut 1
            ui_bar
            ui_list [
                ui_list [
                    ui_text "cycle blend paint mode:"
                    ui_radiobutton "off" blendpaintmode 0
                    ui_radiobutton "overwrite" blendpaintmode 1
                    ui_radiobutton "merge" blendpaintmode 2
                    ui_radiobutton "max opacity" blendpaintmode 3
                    ui_radiobutton "inverted merge" blendpaintmode 4
                    ui_radiobutton "erase" blendpaintmode 5
                ]
                ui_spring 1
                ui_button (dobindsearch [hmapedit 0;
    blendpaintmode (? (= $blendpaintmode (getvarmax blendpaintmode)) 0 (+ $blendpaintmode 1))
    echo (concatword "^fgblend mode:^fw " (at $paintmodes $blendpaintmode) " (^fc" $blendpaintmode "^fw)")] edit)
                ui_spring 1
                ui_list [
                    ui_strut 30 1
                    ui_text "blendmap paint brush:"
                    ui_text (getblendbrushname (curblendbrush))
                    if ($blendpaintmode) [
                        ui_edit_button "cycle" [universaldelta -1]
                        ui_edit_button "quit blendmap paint mode" [hmapedit 0; blendpaintmode 0]
                    ] [
                        ui_text "enable blendmap mode first"
                        ui_strut 1
                    ]
                    ui_strut 1
                    ui_edit_button "generate preview" showblendmap
                    ui_edit_button "^foclear^fw the entire blendmap" clearblendmap
                ]
            ]

            ui_tab "new"
            ui_strut 0.5
            if (= $guipasses 0) [
                texs = (loopconcat i 5 [result (gettexname $getseltex $i)])
                tn = (listlen $texs)
                loop i $tn [
                    t = (at $texs $i)
                    do [s@i = (substring $t (+ (stringstr $t "_") 1) 1)]
                ]
                s0 = "c"
                looplist i [bump env specmap parallax pulse glow] [
                    do [s@i = 0]
                ]
            ]
            ui_center [ui_text "this tab is an experimental feature"]
            ui_strut 1
            ui_center [ui_text "it allows to create a new texture slot using the chosen shader settings"]
            ui_center [ui_text "and the components of the currently selected texture"]
            ui_center [ui_text "for example, this allows to add pulseglow to a texture with glow"]
            ui_strut 1
            ui_center [
                ui_box2 -1 0xffffff [
                    texs = (loopconcat i 5 [result (gettexname $getseltex $i)])
                    if (stringlen $texs) [
                        ui_list [
                            ui_button "add slot:" [
                                t = ""
                                looplist i [bump env specmap parallax pulse glow] [
                                    if $[s@i] [t = (concatword $t $i)]
                                ]
                                setshader (concatword $t "world")
                                echo setshader (concatword $t "world")
                                loop i $tn [
                                    texture $[s@i] (at $texs $i) 0 0 0 0.5
                                    echo texture $[s@i] (at $texs $i) 0 0 0 0.5
                                ]
                            ]
                            ui_strut 2
                            ui_list [
                                looplist i [bump env specmap parallax pulse glow] [
                                    ui_checkbox $i [s@i] 1 0
                                ]
                            ]
                            ui_strut 2
                            ui_list [
                                loop i $tn [
                                    ui_list [
                                        ui_textfield [s@i] 1
                                        ui_strut 1
                                        t = (at $texs $i)
                                        ui_text $t [../@t]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ] [
            ui_font "emphasis" [ui_center [ui_text "no current selection"]]
            ui_strut 0.5
            ui_center [ui_text "select some texture before opening this menu"]
        ]
    ] ]

    slider = 0
    efuitype = 0
    efuitypename = "*"

    efuireset = [
        efuiattrs = "*"
        loop i (? $efuitype (getentattr $efuitype) 12) [
            [efuiattr@i] = "*"
        ]
    ]
    efuireset

    groupedit = 0
    new_ui ents [
        guistatus "Please note the selection info at the right side of the screen"
        ui_stay_open [
            ui_strut 0.5
            ui_center [
                ui_edit_button "create lights" [show_ui lights]
                ui_spring 1
                ui_edit_button "create sound" [show_ui sounds]
                ui_spring 1
                ui_edit_button "create mapmodel" [show_ui mapmodels]
            ]
            ui_center [ui_text "quick creation of basic entities:"]
            ui_strut 0.5
            ui_list [
                loop i 5 [
                    wp = (at $weapname (+ 2 $i))
                    ui_image $[@[wp]tex] [newent weapon (+ 2 @i)] 2 1 [] "" $[@[wp]colour]
                ]
            ]
            ui_list [
                loop i 5 [
                    wp = (at $weapname (+ 7 $i))
                    ui_image $[@[wp]tex] [newent weapon (+ 7 @i)] 2 1 [] "" $[@[wp]colour]
                ]
            ]
            ui_list [
                ui_image $flagtex [newent affinity ] 2 1 [] ""
                ui_image $flagtex [newent affinity 1] 2 1 [] "" $teamalphacolour
                ui_image $flagtex [newent affinity 2] 2 1 [] "" $teamomegacolour
                ui_image $flagtex [newent affinity 3] 2 1 [] "" $teamkappacolour
                ui_image $flagtex [newent affinity 4] 2 1 [] "" $teamsigmacolour
            ]
            ui_list [
                ui_image $playertex [newent playerstart ] 2 1 [] ""
                ui_image $playertex [newent playerstart 1] 2 1 [] "" $teamalphacolour
                ui_image $playertex [newent playerstart 2] 2 1 [] "" $teamomegacolour
                ui_image $playertex [newent playerstart 3] 2 1 [] "" $teamkappacolour
                ui_image $playertex [newent playerstart 4] 2 1 [] "" $teamsigmacolour
            ]
            ui_list [
                ui_image $arrowtex [newent pusher] 2 1
                ui_image $shockingtex [newent teleport] 2 1
                ui_image $burningtex [newent particles] 2 1
                ui_image $modeonslaughttex [newent actor] 2 1
                ui_image $moderacetex [newent checkpoint] 2 1
            ]

            ui_tab edit
            if (= 0 (enthavesel)) [
                groupedit = 0
                ui_center [
                    slider = 0
                    ui_list [
                        ui_strut 2
                        ui_text "no entities are currently selected"
                        ui_strut 4
                        ui_text "Use the other tabs to create a new entity"
                        ui_text "or to find entities with specific attributes."
                        ui_strut 1
                        ui_text "You can also use below buttons to select:"
                        ui_strut 1
                        ui_edit_button " the previous group of ents," [entselbyindex $entindexlist]
                        ui_strut 0.5
                        ui_edit_button " all entities on this map" [entfind]
                        ui_strut 0.5
                        ui_edit_button " or all in the selected volume" [entfindinsel]
                        ui_strut 1
                    ]
                ]
            ]
            if (> (enthavesel) 1) [
                if $groupedit [
                    ui_strut 1
                ] [
                    ui_text (format "selected entities: %1" $enthavesel)
                    ui_list [
                        ui_text "deselect:"
                        ui_spring 1
                        ui_edit_button2 "ents" [entcancel] []
                        ui_spring 1
                        ui_edit_button2 "all" [cancelsel] []
                    ]
                    ui_edit_button "cycle entity links  ->  <-  <-->  -" [entlink]
                    ui_list [
                        ui_text "^faview:"
                        ui_spring 1
                        ui_edit_button "next ent" [entautoview 1]
                        ui_spring 1
                        ui_edit_button "previous ent" [entautoview -1]
                        ui_spring 1
                        ui_text (dobindsearch [domodifier 10; onrelease entautoview] edit)
                    ]
                    ui_strut 0.5
                    ui_text "click below to select and edit an entity"
                    ui_strut 0.5
                ]
                entgetlist = ""
                entindexlist = ""
                imax = 0
                entloop [
                    append entindexlist (entindex)
                    append entgetlist (format "[(%1)^t %2]" (entindex) (entget))
                    imax = (+ $imax 1)
                ]
                ui_loop_scrollbar j $imax 15 30 [
                    ui_button (at $entgetlist $j) [
                        entcancel
                        entselect [(= (at $entindexlist @@j) (entindex))]
                        slider = 0
                    ]
                ]
                ui_strut 0.5
                ui_checkbox (format "^f%1 edit multiple ents: use with caution!" (? $groupedit zyw d)) groupedit
                ui_strut 0.5
                ui_button "  save the list for use with /entselbyindex" [saycommand "/myentlist = $entindexlist; setpersist myentlist 1"]
            ]
            if (|| $groupedit (= 1 (enthavesel))) [
                if (> $enthavesel 1) [
                    ui_text "Warning: entity attributes may differ" $waitingtex
                ] [
                    //ui_button "reselect matching ents (find)" [entfind @efuitypename @efuiattrs] []
                    entl = (getentity $entindex 2)
                    ui_list [
                        ui_text (format "linked entities: %1 " $entl)
                        if $entl [
                            ui_button "(select)" [
                                id = $entindex
                                loop i $entl [ entselbyindex (getentity $id 2 $i)]
                            ]
                        ]
                    ]
                    ui_list [
                        ui_text "deselect:"
                        ui_spring 1
                        ui_edit_button2 "ents" [entcancel] []
                        ui_spring 1
                        ui_edit_button2 "all" [cancelsel] []
                    ]
                ]
                ui_strut 1
                itype = (indexof $enttypelist (enttype))
                ui_text (format "selected: %1 (index %2)" (enttype) (entindex))
                loop iattr (getentattr $itype) [
                    a = (getentattr $itype $iattr (entattr 0))
                    if (|| (stringlen $a) (= $iattr 0)) [
                        ui_list [
                            ui_font emphasis [
                            ui_button "+" [entprop @iattr 1]
                            ui_strut 1
                            ui_button "-" [entprop @iattr -1]
                            ui_strut 1
                            ]
                            field@iattr = (entattr $iattr)
                            ui_textfield field@iattr -10 [entattr @iattr (field@iattr)]
                            ui_strut 1
                            ui_text $a
                            ui_spring 1
                            if (=s $a flags) [
                                ui_button pick [pickattr = @iattr; show_ui entflags] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a yaw) [
                                ui_button pick [pickattr = @iattr; showcompass entyaw] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a pitch) [
                                ui_button pick [pickattr = @iattr; showcompass entpitch] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a modes) [
                                ui_button pick [pickattr = @iattr; show_ui entmodes] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a muts) [
                                ui_button pick [pickattr = @iattr; show_ui entmuts] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a colour) [
                                ui_button pick [pickattr = @iattr; show_ui entcolour] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a green) [
                                ui_button pick [pickattr = @iattr; show_ui entcolours] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a palette) [
                                ui_button pick [pickattr = @iattr; show_ui entpalette] -1 $editingtex
                                ui_strut 1
                            ]
                            if (=s $a dir) [
                                ui_button pick [show_ui particledir] -1 $editingtex
                                ui_strut 1
                            ]
                            if (< $iattr 12) [
                                ui_text (dobindsearch (concat domodifier (+ 11 $iattr)) edit)
                            ]
                        ]
                    ]
                ]
                ui_strut 1
                ui_edit_button "edit selected ents via console" selentedit
                ui_list [
                    ui_text "quick-edit: mousewheel and "
                    ui_text (dobindsearch [domodifier 11] edit)
                    ui_text (dobindsearch [domodifier 12] edit)
                    ui_text (dobindsearch [domodifier 13] edit)
                    ui_text " etc."
                ]
            ]

            ui_tab "find"
            ui_center [ui_text "advanced search and replacement"]
            ui_strut 0.5
            ui_list [
                ui_list [
                    ui_radiobutton (tabify "any type" 3) efuitype 0 [efuitypename = "*"; efuireset]
                    loop i (- (getentinfo) 2) [
                        type = (getentinfo (+ $i 1))
                        ui_radiobutton (tabify $type 3) efuitype (+ $i 1) [efuitypename = @type; efuireset]
                    ]
                ]
                ui_strut 1
                ui_list [
                    if (efuitype) [
                        if (=s $efuitypename particles) [ui_text "attributes differ per type:"]
                        loop i (getentattr $efuitype) [
                            a = (getentattr $efuitype $i $efuiattr0)
                            if (|| (stringlen $a) (= $i 0)) [
                                ui_list [
                                    ui_textfield [efuiattr@i] 11
                                    ui_strut 1
                                    ui_text $a
                                ]
                            ]
                        ]
                    ] [
                        loop i 12 [
                            ui_list [
                                ui_textfield [efuiattr@i] 11
                                ui_strut 1
                                ui_text (format "attribute %1" (+ $i 1))
                            ]
                        ]
                    ]
                    ui_strut 25 1
                    if (< 0 $enthavesel) [
                        ui_center [ ui_button "pick from selected entity" [
                            efuitypename = $enttype
                            efuitype = (indexof $enttypelist $enttype)
                            efuireset
                            loop i 12 [
                                [efuiattr@i] = (entattr $i)
                            ]
                        ] ]
                    ]
                ]
            ]
            ui_strut 0.5
            ui_center [
                ui_list [
                    if (efuitype) [
                        efuiattrs = (loopconcat i (getentattr $efuitype) [result $[efuiattr@i]])
                    ] [efuiattrs = "*"]
                    ui_edit_button "find and select all matching entities" [entcancel; entfind @efuitypename @efuiattrs] []
                    ui_edit_button "add them to the current selection" [entfind @efuitypename @efuiattrs] []
                    ui_edit_button "search matches in the selected volume" [entcancel; entfindinsel @efuitypename @efuiattrs] []
                    if (efuitype) [
                        ui_edit_button "create a new ent with these attributes" [newent @efuitypename @efuiattrs]
                    ] [
                        ui_strut 1
                    ]
                    if (< 0 $enthavesel) [
                        ui_button "^fyreplace all^fw selected entities" [
                            i = 0
                            if (!=s $efuitypename "any") [enttype $efuitypename]
                            looplist j $efuiattrs [
                                if (!=s $j "*") [entattr @i @j]
                                i = (+ $i 1)
                            ]
                        ]
                    ] [ui_strut 1]
                ]
            ]
        ]
    ]

    new_ui entflags [ ui_stay_open [
        ui_status "press ^fcEsc^fw when done to return to the previous menu"
        ui_text (format "pick flags for attribute %1 of the selected %2" $pickattr (enttype)) "" -1 0 500
        ui_strut 0.5
        if (= 0 $guipasses) [
            flags = (entattr $pickattr)
        ]
        if (=s $enttype "mapmodel") [
            i = 1
            looplist a [hide no-clip no-shadow no-dyn-shadow] [
                ui_bitfield $a flags $i [sleep 50 [entattr @pickattr @flags]]
                i = (<< $i 1)
            ]
            ui_strut 1
            ui_text "Tip: Link the mapmodel with a trigger to make the flags mode-specific or switchable" "" -1 0 500
        ]
        if (=s $enttype "sound") [
            i = 1
            looplist a [no-atten no-delay no-cull no-pan no-dist no-quiet clamped] [
                ui_bitfield $a flags $i [sleep 50 [entattr @pickattr @flags]]
                i = (<< $i 1)
            ]
        ]
        if (=s $enttype "lightfx") [
            i = 1
            looplist a [rnd-min rnd-max +dynlight +flicker +pulse +glow] [
                ui_bitfield $a flags $i [sleep 50 [entattr @pickattr @flags]]
                i = (<< $i 1)
            ]
        ]
        if (=s $enttype "weapon") [
            ui_bitfield "forced" flags 1 [sleep 50 [entattr @pickattr @flags]]
        ]

    ]]

    newcompass entyaw $editingtex [
        i = 0
        loop i 8 [
            a = (format "%1 degree" (* 45 $i))
            compass $a [entattr @pickattr @a]
        ]
    ]

    newcompass entpitch $editingtex [
        i = 0
        loop i 5 [
            a = (format "%1 degree" (- 90 (* 45 $i)))
            compass $a [entattr @pickattr @a]
        ]
    ]

    new_ui entmodes [ ui_stay_open [
        if (= 0 $guipasses) [
            modes = (entattr $pickattr)
            if (< $modes 0) [s = -1; modes = (abs $modes)] [s = 1]
        ]
        ui_checkbox "not-" s -1 1 [sleep 50 [entattr @pickattr (* @s @modes)]]
        i = 1
        loop j (- $modeidxnum 2) [
            m = (at $modename (+ $j 2))
            ui_bitfield $m modes $i [sleep 50 [entattr @pickattr (* @s @modes)]]
            i = (<< $i 1)
        ]
        ui_list [
            ui_button "^fyclear" [entattr @pickattr 0; modes = 0]
            ui_spring 1
            ui_button "^fgdone" [clear_ui 1]
        ]
    ] ]

    new_ui entmuts [ ui_stay_open [
        ui_list [
            ui_list [
                if (= 0 $guipasses) [
                    muts = (entattr $pickattr)
                    if (< $muts 0) [s = -1; muts = (abs $muts)] [s = 1]
                ]
                ui_checkbox "not-" s -1 1 [sleep 50 [entattr @pickattr (* @s @muts)]]
                i = 1
                looplist m (concat $mutsname gsp1 gsp2 gsp3) [
                    ui_bitfield $m muts $i [sleep 50 [entattr @pickattr (* @s @muts)]]
                    i = (<< $i 1)
                ]
            ]
            ui_strut 2
            ui_list [
                loop i 3 [
                    ui_strut 1
                    ui_text (format "gsp%1:" (+ 1 $i))
                    loop j $modeidxnum [
                        m = (gspmutname $j $i)
                        if (stringlen $m) [ui_text (format "%1-%2" $m  (at $modename $j))]
                    ]
                ]
            ]
        ]
        ui_strut 0.5
        ui_list [
            ui_button "^fyclear" [entattr @pickattr 0; muts = 0]
            ui_spring 1
            ui_button "^fgdone" [clear_ui 1]
        ]
    ] ]

    setdir = [entattr 1 (+ $offset (* 32 $i) (* 3 $shape) $axis)]
    new_ui particledir [ ui_stay_open [
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        if (= 0 $guipasses) [
            t = (entattr 0)
            hex = (entattr 3)
            dir = (mod (entattr 1) 64)
            if (> $dir 32) [
                i = 1
                dir = (- $dir 32)

            ] [
                i = 0
            ]
            axis = (mod $dir 3)
            shape = (div $dir 3)
            offset = 256
        ]
        ui_list [
            ui_list [
                ui_text "type"
                ui_strut 0.5
                looplist j [4 7 8 9 10 11 12 13] [
                    ui_radiobutton (getentattr 5 0 $j) t $j [entattr 0 @j]
                ]
            ]
            ui_strut 2
            ui_list [
                ui_text "shape"
                ui_strut 0.5
                ui_radiobutton circle shape 0 setdir
                ui_radiobutton cylinder shape 1 setdir
                ui_radiobutton cone-shell shape 2 setdir
                ui_radiobutton cone-shell-2 shape 3 setdir
                ui_radiobutton plane-volume shape 4 setdir
                ui_radiobutton line-volume shape 5 setdir
                ui_radiobutton line-volume-2 shape 6 setdir
                ui_radiobutton sphere shape 7 setdir
                ui_radiobutton plane-flat shape 8 setdir
            ]
            ui_strut 2
            ui_list [
                ui_text "direction"
                ui_strut 0.5
                ui_radiobutton x-axis axis 0 setdir
                ui_radiobutton y-axis axis 1 setdir
                ui_radiobutton z-axis axis 2 setdir
                ui_strut 1
                ui_checkbox inverted i 1 0 setdir
            ]
            //ui_strut 2
            //ui_list [
            //    ui_text "test offset"
            //    loop j 8 [
            //        ui_radiobutton (* 64 $j) offset (* 64 $j) setdir
            //    ]
            //]
        ]
    ] ]

    new_ui entcolour [ ui_stay_open [
        if (= $guipasses 0) [hex = (entattr $pickattr)]
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        ui_text (format "pick a colour for attribute %1 of the selected %2" $pickattr (enttype))
        ui_strut 1
        ui_list [
            ui_hex_preview $hex hex-value
            ui_strut 1
            ui_list [ui_rgb_sliders hex]
            ui_strut 2
            ui_button "apply this colour" [entattr @pickattr @hex] -1 $editingtex
        ]
        ui_palette 0.6
    ] ]

    new_ui entcolours [ ui_stay_open [
        if (= $guipasses 0) [
            hex = (+ (<< (entattr (- $pickattr 1)) 16) ( << (entattr $pickattr) 8) (entattr (+ $pickattr 1)))
        ]
        guistatus "press ^fcEsc^fw when done to return to the previous menu"
        ui_text (format "pick a colour for attributes %1, %2 and %3 of the selected %4" (- $pickattr 1) $pickattr (+ $pickattr 1) (enttype))
        ui_strut 1
        ui_list [
            ui_hex_preview $hex hex-value
            ui_strut 1
            ui_list [ui_rgb_sliders hex]
            ui_strut 2
            ui_button "apply this colour" [
                entattr (- $pickattr 1) @colr
                entattr $pickattr  @colg
                entattr (+ $pickattr 1) @colb
            ] -1 $editingtex
        ]
        ui_palette 0.6
    ] ]


    new_ui entpalette [
        if (= 0 $guipasses) [tb = 0; wb = 0; p = (+ $pickattr 1)]
        ui_strut 1
        ui_text (format "pick a palette for attributes %1 and %2 of the selected %3" $pickattr (+ $pickattr 1) (enttype))
        ui_strut 1
    	ui_list [
    	    ui_list [
                ui_text "disabled"
    	        ui_image $warningtex [entattr $pickattr 0; entattr $p 0] 1.5 1
    	    ]
            ui_spring 1
    	    ui_list [
                ui_text "flashing colours"
    	        ui_list [
    	            ui_image $burningtex [entattr $pickattr 0; entattr $p  1] 1.5 1
    	            ui_image $modekaboomtex [entattr $pickattr 0; entattr $p 2] 1.5 1
    	            ui_image $bombtex [entattr $pickattr 0; entattr $p 3] 1.5 1
    	            ui_image $shockingtex [entattr $pickattr 0; entattr $p 4] 1.5 1
    	        ]
                ui_strut 1
    	    ]
            ui_spring 1
    	    ui_list [
                ui_text "team colour variables"
    	        ui_list [
    	            ui_image $modeffatex [entattr $pickattr 1; entattr $p 0] 1.5 1
                    i = 1
                    looplist p [alpha omega kappa sigma ] [
    	                ui_image $[team@[p]tex] [entattr $pickattr 1; entattr $p (+ @i $tb)] 1.5 1 "" [] $[team@[p]colour]
                        i = (+ 1 $i)
    	            ]
    	        ]
                ui_checkbox "only applied in team games" tb 0 5
    	    ]
    	]
        ui_strut 1
        ui_text "weapon colour variables"
    	ui_list [
    	    loop i $weapidxnum [
                w = (at $weapname $i)
    	        ui_image $[@[w]tex] [entattr $pickattr 2 ; entattr $p (+ @i $wb)] 1.5 1 "" [] $[@[w]colour]
    	    ]
    	]
        ui_checkbox "only applied if the weapon is allowed to spawn" wb 0 $weapidxnum
    ]

    new_ui sounds [ ui_stay_open [
        if (= 0 $guipasses) [
            mapsounds = ""
            imax = (getsound 1)
            loop i $imax [
                append mapsounds (getsound 1 $i 3)
            ]
        ]
        if (imax) [
            ui_text "^fapick a sound entity to create"
            ui_strut 1
            ui_loop_scrollbar j $imax 15 40 [
                p = (at $mapsounds $j)
                ui_edit_button $p [newent sound @j]
            ]
        ] [
            ui_text "no sounds found for this map configuration"
        ]
    ] ]

    new_ui soundtest [ ui_stay_open [
        if (= 0 $guipasses) [
            sounds = ""
            imax = (getsound 0)
            loop i $imax [
                append sounds (getsound 0 $i 3)
            ]
        ]
        if (imax) [
            ui_button "test the /sound command"
            ui_button "show sound debugger" [show_ui cursounds]
            ui_strut 1
            ui_loop_scrollbar j $imax 15 30 [
                p = (format "%1^t%2" $j (at $sounds $j))
                ui_button $p [sound @j] [] $arrowrighttex
            ]
        ] [
            ui_text "could not load sound data"
        ]
    ] ]

    mapmodelprevs = 1
    new_ui mapmodels [ ui_stay_open [
        if (= 0 $guipasses) [
            slider = 0
            imax = (mapmodelindex)
        ]
        if (imax) [
            ui_text "^fapick a mapmodel entity to create"
            ui_strut 0.5
            ui_checkbox "show a preview" mapmodelprevs
            ui_strut 0.5
            ui_list [
                ui_list [
                    ui_loop_scrollbar j $imax 15 25 [
                        p = (mapmodelindex $j)
                        ui_button $p [newent mapmodel @j]
                    ]
                ]
                if $mapmodelprevs [ui_no_cursor_fx [ui_model_view $guirollovername "mapmodel" [] 7.5 1]]
            ]
        ] [
            ui_text "no mapmodels found for this map configuration"
        ]
    ] ]

    new_ui lights [ ui_stay_open [
        if (= 0 $guipasses) [ hex = 0xffffff; rad = 64 ]
        ui_strut 1
        ui_center [ui_text "pick a colour for creating light or sunlight presets"]
        ui_strut 1
        ui_list [
            ui_spring 1
            ui_list [
                ui_list [
                    ui_hex_preview $hex colour
                ]
                ui_strut 1
                ui_edit_button "create lightfx" [newent lightfx]
                ui_edit_button "link ents" [entlink]
            ]
            ui_strut 1
            ui_list [
                ui_rgb_sliders hex
                ui_strut 1
                ui_edit_button "^tquick lightmap calculation" [quicklight]
                ui_edit_button "^tfull lightmap calculation" [fullbright 0; calclight 1]
            ]
            ui_strut 1
            ui_list [
                ui_text "create entitites:"
                ui_edit_button "tiny light" [newent light 32 @colr @colg @colb]
                ui_edit_button "small light" [newent light 64 @colr @colg @colb]
                ui_edit_button "medium light" [newent light 128 @colr @colg @colb]
                ui_edit_button "large light" [newent light 256 @colr @colg @colb]
                ui_edit_button "huge light" [newent light 512 @colr @colg @colb]
            ]
            ui_strut 2
            ui_list [
                ui_strut 1
                ui_edit_button "sunlight top" [newent sunlight 0 0 @colr @colg @colb 45]
                ui_edit_button "sunlight north" [newent sunlight 0 30 @colr @colg @colb 45]
                ui_edit_button "sunlight east" [newent sunlight 90 30 @colr @colg @colb 45]
                ui_edit_button "sunlight south" [newent sunlight 180 30 @colr @colg @colb 45]
                ui_edit_button "sunlight west" [newent sunlight 270 30 @colr @colg @colb 45]
            ]
        ]
        ui_strut 1
        ui_palette 0.6
        guistatus [Tip: Create lightfx first and then lights to link them quickly]
    ] ]

    new_ui serverpriv [
        ui_stay_open [
            ui_radiobutton   "veto server"               openservertype 0   [serveropen 3]
            ui_radiobutton   "open server"               openservertype 1   [serveropen 1]
            ui_radiobutton   "coop server"               openservertype 2   [serveropen 2]
            ui_text " "
            ui_button   "claim privileges"            "setpriv 1"
            ui_button   "relinquish privileges"         "setpriv 0"
            ui_text " "
            ui_button   "administrator password"      "saycommand [/adminpass ]"
        ]
    ]

    new_ui texreset [
        ui_list [
            ui_list [
            ui_edit_button "^fyCLEAR ^fwall unused textures from map cfg" texturecull
            ui_edit_button "^fyCLEAR ^fwunused textures and load the default packages" texturerehash
            ui_edit_button "^frCLEAR ^fwall textures from map cfg" texturereset
            ]
        ]
    ]

    matmenu = [
        ui_edit_checbox "show material volumes" showmat
        ui_strut 1
        looplist mat [air alpha water lava clip noclip aiclip death hurt ladder] [
            ui_edit_button $mat [editmat @mat]
        ]
        ui_strut 1
        ui_bar
        ui_strut 0.5
        ui_center [ui_text "Note: Use of glass is not recommended"]
        ui_center [ui_text "create transparent geometry with alpha"]
    ]

    new_ui materials [
        @matmenu
        ui_tab "^f(textures/editing)"
        ui_center [ui_text "customize colour, fog and specular"]
        ui_strut 0.5
        local var
        looplist i "water lava glass" [
            looplist j ["" 2 3 4] [
                var = [@[i]@[j]]
                ui_list [
                    ui_strut 1.2 1
                    ui_button (tabify $var 2) $var
                    ui_button "colour" [ pick_color @[var]colour ] [
                        saycommand /pick_color @[var]colour
                    ] $editingtex $[@[var]colour]
                    if (!=s "glass" $i) [
                        ui_strut 1
                        ui_textfield @[var]fog 4
                    ]
                    if (=s "water" $i) [
                        ui_strut 1
                        ui_textfield @[var]spec 4
                    ]
                ]
            ]
        ]
        ui_tab "^f(textures/dead)"
        ui_center [ui_text "customize obituaries"]
        ui_strut 1
        looplist i [ fall death hurt water water2 water3 water4 lava lava2 lava3 lava4] [
            ui_list [
                ui_text $i
                ui_spring 1
                    ui_textfield obit@i -22
            ]
        ]
    ]

