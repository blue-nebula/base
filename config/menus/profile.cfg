profilebuttons = [
    local text_ok

    if (&& (= $numargs 1 ) (=s $arg1 "yes_no")) [
        text_ok = "^fgYes"
        text_cancel = "^foNo"
    ] [
        text_ok = "^fgOk"
        text_cancel = "^foCancel"
    ]

    ui_list [ ui_font "emphasis" [
        ui_strut 2
        ui_button $text_ok [
            setinfo $player_name_new $player_colour_new $player_model_new $player_vanity_new $player_loadweap_new $player_randweap_new
            clear_ui 1
        ] []
        ui_strut 3
        if (needname) [
            ui_button "^foMain Menu" [show_ui main]
        ] [
            ui_button $text_cancel [clear_ui 1]
        ]
    ] ]
]

changes_names = [ name colour model vanity loadweap randweap ]
changes_name_replacements = [ "Nickname" "Charactercolor" "Charactermodel" "Character Customization" "Loadout" "Random Weapon Filter" ]

// this menu has to be opened before "profile", that way it lies in the stack beneigh it and when
// you close "profile" this menu can check for differences, if there are none, it closes itself
new_ui profilediff [
    ui_header "Profile"
    ui_text "Do you want to save"
    ui_text "the following changes?"
    ui_strut 1

    loop i (listlen $changes_names) [
        change_name = (at $changes_names $i)
        change_name_replacement = (at $changes_name_replacements $i)

        if (!=s $[player@change_name] $[player_@[change_name]_new]) [
            ui_center [
                ui_text $change_name_replacement
            ]
            skip = 0
        ]
    ]
    if $skip [ clear_ui 1 ]
    ui_strut 1
    profilebuttons "yes_no"
] [
    skip = 1
]


profilepreview = [
    ui_player_preview $player_model_new $player_colour_new $player_team_new $player_weap_new $player_vanity_new [player_inherit_new = 1; show_ui playerprev] 7.6 1 1
]

profiletab = [ui_two [profilepreview] [@arg1] [profilebuttons] ] // left, right, bottom right

player_inherit_new = 0
player_disinherit_new = 0

new_ui playerprev [
    ui_header "Player Preview"
    ui_center [ ui_player_preview $player_model_new $player_colour_new $player_team_new $player_weap_new $player_vanity_new [clear_ui 1] 15 1 1 ]
    ui_strut 0.25
    ui_center [
        ui_text "Team: "
        teamneutraltex = $teamtex
        loop t 5 [
            push t (at "neutral alpha omega kappa sigma" $t) [
                ui_stay_open [ ui_font "default" [ ui_button (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [player_team_new = @@@@t] ] ]
            ]
        ]
        ui_strut 1
        ui_text "Weapon: "
        loop w (- $weapidxnum 1) [ // exclude last id: melee
            push w (at $weapname $w) [
                ui_stay_open [ ui_font "emphasis" [ ui_button (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [player_weap_new = @@@@w] ] ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $player_inherit_new [ player_inherit_new = 0; player_disinherit_new = 1 ] [
            player_team_new =   (getplayerteam 1)
            player_weap_new =   (weapselect)
            player_name_new =   (getplayername)
            player_colour_new = (getplayercolour -1)
            player_model_new =  (getplayermodel)
            player_vanity_new = (getplayervanity)
        ]
    ]
]

loadweapall = 0
// arg1 = int: weapon slot
// arg2 = int: weapon idx
loadweaps = [
    lw_all = ""
    lw_len = (listlen $player_loadweap_new)
    lw_o = (? (> (> $lw_len $arg1) (at $player_loadweap_new $arg1) -1))

    loop lw_slot $weapidxloadout [
        if (= $lw_slot $arg1) [
            lw_all = (? $lw_slot (concat $lw_all $arg2) $arg2)
        ] [
            lw3 = (? (> $lw_len $lw_slot) (at $player_loadweap_new $lw_slot) -1)
            if (&& $lw3 (= $arg2 $lw3)) [
                lw3 = $lw_o
            ]
            lw_all = (? $lw_slot (concat $lw_all $lw3) $lw3)
        ]
    ]

    if (listlen $lw_all) [ player_loadweap_new = $lw_all ]
    player_weap_new = $arg2
]
// arg1 = int:  weapon_idx
// arg2 = bool: append?
randweaps = [
    rwa = ""
    rwl = (listlen $player_randweap_new)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $player_randweap_new $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ player_randweap_new = $rwa ]
]

cur_select_weapon_slot = -1
new_ui select_weapon [
    ui_header (format "%1. Weapon" (+ $cur_select_weapon_slot 1))

    local cur_selected_weapon
    cur_selected_weapon = (at $player_loadweap_new $cur_select_weapon_slot)
    if (<= $cur_selected_weapon 0) [
        cur_selected_weapon = 0
    ] [
        cur_selected_weapon = (- $cur_selected_weapon 1)
    ]

    local selected_weapon
    selected_weapon = (at $player_loadweap_new $cur_select_weapon_slot)

    loop w_idx (+ $weapidxloadout 1) [
        if (= $w_idx 0) [
            w_idx_offset = 0
            weap_name =    "Random"
            weap_color =   0xFFFFFF
            weap_icon =    textures/question
            button_label = "Random"
        ] [
            w_idx_offset = (+ $w_idx (- $weapidxoffset 1))
            weap_name =    (at $weapname $w_idx_offset)
            weap_color =   $[@[weap_name]colour]
            weap_icon =    $[@[weap_name]tex]
            button_label = (format "^f[%1]%2" $weap_color $[@[weap_name]longname])
        ]

        ui_merge 20 [
            if (= $w_idx $cur_selected_weapon) [
                ui_background 0x505050
            ] [
                ui_background 0x303030
            ]; ui_strut 0.5

            ui_image $weap_icon [] 0.75 0 [] [] $weap_color
            ui_center [ ui_center_z [
                ui_button $button_label
            ] ]
            ui_spring 1
        ] [
            // Left Click Action
            loadweaps $cur_select_weapon_slot @w_idx_offset
            clear_ui 1
        ] [
            // Right Click Action (same as left click)
            loadweaps $cur_select_weapon_slot @w_idx_offset
            clear_ui 1
        ] [
            // Hover action
            if (= $w_idx 0) [
                ui_tooltip "Chooses a random weapon, based on the weapon filter"
            ] [
                ui_tooltip (format "^f[%1]Primary:^n^fw%2^n^f[%1]Secondary:^n^fw%3" $weap_color $[@[weap_name]desc1] $[@[weap_name]desc2]) 1500
            ]
        ]

    ]
] [
    if (= $cur_select_weapon_slot -1) [
        clear_ui 1
    ]
]
// these names should match the types in config/vanities.cfg
vantypename = [Chest Eyes Scalp Forehead Ears Face Back [Right Foot] [Left Foot] [Right Shoulder] [Left Shoulder]]
vantypesel = -1
vansel = -1

new_ui profile [
    if (needname) [
        ui_strut 0.5
        ui_header welcome
        ui_center [ui_font huge [ui_text "Welcome to Blue Nebula" $emblemtex]]
        ui_strut 0.5
        ui_center [ui_text "To get ready for action, please set up your player profile:"] 
        ui_center [ui_text "Use the other tabs of this menu to choose a name, a profile colour and a pair of loadout weapons."]
        ui_strut 0.5
        ui_center [ui_text "To learn more about the weapons and many other aspects of the game, you can consult the help menus."]
        ui_center [ui_text (format "You can press %1 any time to open these menus, and %2 to return to the previous menu." (dobindsearch [show_ui help]) (dobindsearch [if (clear_ui 1) [] [show_ui main]]))]
        ui_center [ui_text "During a match, this allows you to quickly look up rules and tips for the current game mode."]
        ui_center [ui_text "Most of this information is also available on the Blue Nebula docs, ^fchttps://go.blue-nebula.org/docs"]
        ui_strut 0.5
        ui_center [ui_text "Once you have set up your profile with the ^fgok^fw ui_button, you can start playing offline or online."]
        ui_center [ui_text "Learning by doing or watching experienced players, both are great ways to get you started."]
        ui_center [ui_text (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))]
        ui_center [ui_text "Use the mouse wheel to follow the view of different players while spectating."]
        ui_strut 0.5
        ui_center [ui_text (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))]
        ui_tab Profile
    ] [ui_header Profile]
    profiletab [
        ui_list [
            ui_strut 3
            ui_list [
                ui_center [ ui_font "emphasis" [ ui_text "Nickname" ] ]
                ui_center [ ui_textfield player_name_new 24 [] -1 0 "" 0 "^faEnter Nickname" ]
                ui_strut 1
                ui_center [ ui_font "emphasis" [ ui_text "Charactercolor" ] ]
                ui_center [
                    if $player_colour_new [ui_hex_preview $player_colour_new "hex-value"] [
                        w = (at $weapname $player_weap_new)
                        ui_stay_open [ ui_image $[@[w]tex] [
                            player_weap_new = (+ 1 $player_weap_new)
                            if (= $player_weap_new (- $weapidxnum 1)) [player_weap_new = 0 ]
                        ] 1.9 1 "" [] $[@[w]colour] ]
                        ui_strut 4.45
                    ]
                    ui_strut 1
                    ui_list [
                        ui_rgb_sliders player_colour_new
                    ]
                ]
                ui_strut 0.2
                ui_right [ui_radiobutton "Use the current weapon's colour  " player_colour_new 0]
                ui_strut 0.5
                ui_center [ ui_font "emphasis" [ ui_text "Charactermodel" ] ]
                ui_strut 0.25
                ui_center [
                    ui_radiobutton "Thicc" player_model_new 0
                    ui_strut 1.5
                    ui_radiobutton "Thin" player_model_new 1
                ]
            ]
        ]
    ]
    ui_tab "Loadout"
    profiletab [

        ui_list [
            ui_strut 3
            // Loadout selector
            ui_list [
                ui_center [ ui_text "Loadout" ]; ui_strut 0.5

                shown_loadout_weapons = $playermaxcarry
                // if we enabled "Show All weapon slots" then increase
                // shown_loadout_weapons
                if $loadweapall [
                    shown_loadout_weapons = $weapidxloadout
                ]

                loop i $shown_loadout_weapons [
                    inventory_idx = (+ $i 1)
                    // idx of the weapon at loadout slot i
                    weap_idx = (at $player_loadweap_new $i)

                    // weap_idx = 0 means Random
                    if (<= $weap_idx 0) [
                        weap_name = "Random"
                        weap_color = 0xFFFFFF
                        button_label = "Random"
                        weap_icon = textures/question
                    ] [
                        weap_name = (at $weapname $weap_idx)
                        weap_color = $[@[weap_name]colour]
                        button_label = (format "^f[%1]%2" $weap_color $[@[weap_name]longname])
                        weap_icon = $[@[weap_name]tex]
                    ]

                    ui_list [
                        // 1. 2. 3. etc
                        ui_text (format "%1. " $inventory_idx)

                        ui_list [ ui_merge 20 [
                            ui_background 0x303030; ui_strut 0.5

                            ui_image $weap_icon [] 0.75 0 [] [] $weap_color
                            ui_center [ ui_center_z [
                            // I have to use external var cur_select_weapon_slot since show_ui doesn't take any additional arguments
                            ui_button $button_label [ cur_select_weapon_slot = @i; show_ui select_weapon ]
                            ] ]
                            ui_spring 1
                        ] [
                            // Left Click Action
                            cur_select_weapon_slot = @i
                            show_ui select_weapon
                        ] [
                            // Right Click Action (same as Left Click
                            // Action)
                            cur_select_weapon_slot = @i
                            show_ui select_weapon
                        ] [
                            // Hover Action
                            ui_tooltip "Click to choose a different weapon"
                        ] ]
                    ]
                ]; ui_strut 0.25
                ui_center [ ui_checkbox " Show All Weapon Slots" loadweapall ]
            ]; ui_strut 3
            // Random Weapon Filter and additional checkboxes
            ui_list [
                ui_center [ ui_text "Random Weapon Filter" ]
                ui_strut 0.5
                ui_center [
                    ui_center [
                        ui_stay_open [
                            num_weaps = (listlen $player_randweap_new)
                            ui_list [
                                ui_background 0x303030; ui_spring 1

                                loop w_idx $weapidxloadout [
                                    // getting the vars
                                    w_idx_offset = (+ $w_idx $weapidxoffset)
                                    w_name =       (at $weapname $w_idx_offset)
                                    is_enabled =   (? (> $num_weaps $w_idx) (at $player_randweap_new $w_idx) 1)
                                    w_icon =       $[@[w_name]tex]
                                    // if the weapon is not enabled, then it'll be
                                    // grey
                                    w_color =      (? $is_enabled $[@[w_name]colour] 0x808080)

                                    // GUI
                                    ui_list [
                                        // draw border around icon if the weapon is
                                        // enabled
                                        if $is_enabled [
                                            ui_background 0xFFFFFF 0.01 $w_color 1 1
                                        ]; ui_strut 0.125

                                        ui_image $w_icon [
                                            remove = (? (> (listlen $player_randweap_new) @w_idx) (at $player_randweap_new @w_idx) 1)
                                            randweaps @w_idx (! $remove)
                                        ] 0.75 0 [ textures/blank ] [] $w_color; ui_strut 0.125
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]

                ui_strut 1
                ui_font "little" [
                    ui_strut 0.5
                    ui_center [ ui_text "(If a weapon is not available, the" ]
                    ui_center [ ui_text "next one in the list will be used)" ]
                    ui_strut 0.5
                ]; ui_strut 0.5

                ui_center [ ui_text (format "You can carry ^fs^fc%1^fS %2" $playermaxcarry (? (= $playermaxcarry 1) "weapon" "weapons")) ]
                ui_font "little" [
                    if (= $getplayerstate 0) [
                        ui_center [ ui_text "^fs^frWeapon changes will not take^fS" ]
                        ui_center [ ui_text "^fs^freffect until after respawn!"     ]
                    ] [
                        ui_center [ ui_text "You will spawn with these weapons"     ]
                        ui_strut 0.25
                    ]
                ]; ui_strut 0.5

                ui_center [ ui_checkbox " Pick a Loadout Every New Game" showloadoutmenu ]
                ui_strut 3
            ]
        ]

        ui_visible [ ui_tip (format "Press %1 To Open This Weapon Menu At Any Time" (dobindsearch "show_ui profile 2")) ]
    ]
    ui_tab "Character Customization"
    profiletab [
        ui_list [
            ui_strut 3
            ui_list [
                ui_font "emphasis" [
                    ui_center [ ui_text "Customize your Character" ]
                ]
                ui_strut 0.5
                ui_font "little" [
                    ui_center [ ui_text "These decorative items do not influence movement or abilities in any way" ]
                    ui_strut 0.5
                    ui_strut 70 1
                    ui_list [
                        ui_list [
                            ui_strut 32 1
                            ui_text "Select a body part:"
                            ui_strut 0.5
                            loop i (listlen $vantypename) [
                                t = (format "^fd%1: Nothing" (at $vantypename $i))
                                loop j (getvanity) [
                                    if (= (getvanity $j 0) $i) [
                                        if (< -1 (indexof  $player_vanity_new (getvanity $j 1))) [
                                            t = (format "%1:^fy %2" (at $vantypename $i) (getvanity $j 2))
                                        ]
                                    ]
                                ]
                                ui_radiobutton $t vantypesel $i [
                                    vansel = -1
                                    loop j (getvanity) [
                                        if (= (getvanity $j 0) @i) [
                                            if (< -1 (indexof  $player_vanity_new (getvanity $j 1))) [vansel = $j]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                        ui_strut 2
                        ui_list [
                            if (> $vantypesel -1) [
                                ui_text (format "Options For The %1:" (at $vantypename $vantypesel))
                                ui_strut 0.5
                                ui_radiobutton "Nothing" vansel -1 [
                                    loop j (getvanity) [
                                        if (= (getvanity $j 0) $vantypesel) [
                                            player_vanity_new = (listdel $player_vanity_new (getvanity $j 1))
                                        ]
                                    ]
                                ]
                            ]
                            loop i (getvanity) [
                                if (= (getvanity $i 0) $vantypesel) [
                                    ui_radiobutton (getvanity $i 2) vansel $i [
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $vantypesel) [
                                                player_vanity_new = (listdel $player_vanity_new (getvanity $j 1))
                                            ]
                                        ]
                                        append player_vanity_new (getvanity @i 1)
                                    ]
                                ]
                            ]
                        ]
                        //ui_spring 1
                        //ui_list [
                        //    ui_model_view (getvanity $vansel 6) "" [] 5 1
                        //]
                    ]
                ]
            ]
        ]
    ]

    ui_tab "Account"
    profiletab [
        ui_list [
            ui_strut 2
            ui_center [
                ui_strut 0.5
                ui_font "emphasis" [ ui_center [ ui_text "Blue Nebula User Account" ] ]
                ui_strut 0.5

                ui_list [
                    ui_list [
                        ui_text "Player Handle:"; ui_strut 0.5
                        ui_right [ ui_text "Private Key:" ]
                    ]; ui_strut 0.5
                    ui_list [
                        ui_textfield accountname 48 []; ui_strut 0.25
                        ui_textfield accountpass 48 []
                    ]
                ]; ui_strut 1.5

                ui_center [ ui_text "Unique user accounts provide you with an established identity in the" ]
                ui_center [ ui_text "community, and allow server owners to add you as a local moderator." ]

                ui_center [
                    ui_text "The icon before your name will also change to player status: "
                    ui_no_hit_fx [ ui_image "textures/privs/player" [] 0.5 ]
                ]

                ui_strut 1
                ui_center [ ui_text "New Account Applications And Replacement Accounts Are Available At:" ]
                ui_center [ ui_text "^fghttps://go.blue-nebula.org/accounts" ]
                ui_strut 1
            ]
            ui_strut 2
        ]
    ]

] [
    if (= $guipasses 0) [
        if $player_disinherit_new [ player_disinherit_new = 0 ] [
            player_team_new = (getplayerteam 1)
            player_weap_new = (weapselect)
            player_name_new = (getplayername)
            player_colour_new = (getplayercolour -1)
            player_model_new = (getplayermodel)
            player_vanity_new = (getplayervanity)
        ]
        player_loadweap_new = ""
        loop i $weapidxloadout [
            q = (getloadweap $i)
            player_loadweap_new = (? $i (concat $player_loadweap_new $q) $q)
        ]

        player_randweap_new = ""
        break = 0
        loopwhile i $weapidxloadout [= $break 0] [
            q = (getrandweap $i)
            player_randweap_new = (? $i (concat $player_randweap_new $q) $q)
        ]
    ]
]
