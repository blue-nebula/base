profilebuttons = [
    ui_list [ ui_font "emphasis" [
        ui_strut 2
        ui_button "^fgok" [
            setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevloadweap $playerprevrandweap
            clear_ui 1
        ] []
        ui_strut 3
        if (needname) [
            ui_button "^fomain menu" [show_ui main]
        ] [
            ui_button "^focancel" [clear_ui 1]
        ]
    ] ]
]

new_ui profilediff [
    ui_header "profile"
    ui_text "Do you want to save"
    ui_text "the following changes?"
    ui_strut 1
    looplist i [name colour model vanity loadweap randweap] [
        if (!=s $[player@i] $[playerprev@i]) [
            ui_center [
                ui_text @i
            ]
            skip = 0
	]
    ]
    if $skip [clear_ui 1]
    ui_strut 1
    profilebuttons
] [
    skip = 1
]


profilepreview = [
    ui_player_preview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; show_ui playerprev] 7.6 1 1
]

profiletab = [ui_two [profilepreview] [@arg1] [profilebuttons] ] // left, right, bottom right

playerprevinherit = 0
playerprevdisinherit = 0

new_ui playerprev [
    ui_header "player preview"
    ui_center [ ui_player_preview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [clear_ui 1] 15 1 1 ]
    ui_strut 0.25
    ui_center [
        ui_text "team: "
        teamneutraltex = $teamtex
        loop t 5 [
            push t (at "neutral alpha omega kappa sigma" $t) [
                ui_stay_open [ ui_font "default" [ ui_button (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
            ]
        ]
        ui_strut 1
        ui_text "weapon: "
        loop w (- $weapidxnum 1) [ // exclude last id: melee
            push w (at $weapname $w) [
                ui_stay_open [ ui_font "emphasis" [ ui_button (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevinherit [ playerprevinherit = 0; playerprevdisinherit = 1 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]

loadweapall = 0
loadweaps = [
    lwa = ""
    lwl = (listlen $playerprevloadweap)
    lwo = (? (> $lwl $arg1) (at $playerprevloadweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $playerprevloadweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [ lw3 = $lwo ]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ playerprevloadweap = $lwa ]
    playerprevweap = $arg2
]

randweaps = [
    rwa = ""
    rwl = (listlen $playerprevrandweap)
    loop rw2 $weapidxloadout [
        rw3 = (> $rwl $rw2)
        rwv = (? $rw3 (at $playerprevrandweap $rw2) 1)
        if (= $rw2 $arg1) [
            rwv = $arg2
        ]
        rwa = (? $rw2 (concat $rwa $rwv) $rwv)
    ]
    if (listlen $rwa) [ playerprevrandweap = $rwa ]
]

// these names should match the types in config/vanities.cfg
vantypename = [chest eyes scalp forehead ears face back [right foot] [left foot] [right shoulder] [left shoulder]]
vantypesel = -1
vansel = -1

new_ui profile [
    if (needname) [
        ui_strut 0.5
        ui_header welcome
        ui_center [ui_font huge [ui_text "Welcome to Red Eclipse Legacy" $emblemtex]]
        ui_strut 0.5
        ui_center [ui_text "To get ready for action, please set up your player profile:"] 
        ui_center [ui_text "Use the other tabs of this menu to choose a name, a profile colour and a pair of loadout weapons."]
        ui_strut 0.5
	ui_center [ui_text "To learn more about the weapons and many other aspects of the game, you can consult the help menus."]
        ui_center [ui_text (format "You can press %1 any time to open these menus, and %2 to return to the previous menu." (dobindsearch [show_ui help]) (dobindsearch [if (clear_ui 1) [] [show_ui main]]))]
	ui_center [ui_text "During a match, this allows you to quickly look up rules and tips for the current game mode."]
        //ui_center [ui_text "Most of this information is also available on the Red Eclipse Legacy wiki, ^fchttp://redeclipse.net/wiki/"]
        ui_strut 0.5
        ui_center [ui_text "Once you have set up your profile with the ^fgok^fw ui_button, you can start playing offline or online."]
        ui_center [ui_text "Learning by doing or watching experienced players, both are great ways to get you started."]
        ui_center [ui_text (format "You can press %1 to enter spectator mode and %2 to toggle first or third person view." (dobindsearch [spectate 1]) (dobindsearch thirdpersonswitch))]
        ui_center [ui_text "Use the mouse wheel to follow the view of different players while spectating."]
        ui_strut 0.5
        ui_center [ui_text (format "One last tip: You can perform wall-runs and kicks with the %1 key. Have fun!" (dobindsearch special))]
        ui_tab profile
    ] [ui_header profile] 
    profiletab [
        ui_list [
            ui_strut 3
            ui_list [
                ui_center [ ui_font "emphasis" [ ui_text "player name:" ] ]
                ui_center [ ui_textfield playerprevname 24 [] -1 0 "" 0 "^fzad<enter name>" ]
                ui_strut 1
                ui_center [ ui_font "emphasis" [ ui_text "player color:" ] ]
                ui_center [
                    if $playerprevcolour [ui_hex_preview $playerprevcolour "hex-value"] [
                        w = (at $weapname $playerprevweap)
                        ui_stay_open [ ui_image $[@[w]tex] [
                            playerprevweap = (+ 1 $playerprevweap)
                            if (= $playerprevweap (- $weapidxnum 1)) [playerprevweap = 0 ]
                        ] 1.9 1 "" [] $[@[w]colour] ]
                        ui_strut 4.45
                    ]
                    ui_strut 1
                    ui_list [
                        ui_rgb_sliders playerprevcolour
                    ]
                ]
                ui_strut 0.2
                ui_right [ui_radiobutton "use the current weapon's colour  " playerprevcolour 0]
                ui_strut 0.5
                ui_center [ ui_font "emphasis" [ ui_text "player model:" ] ]
                ui_strut 0.25
                ui_center [
                    ui_radiobutton "male" playerprevmodel 0
                    ui_strut 1.5
                    ui_radiobutton "female" playerprevmodel 1
                ]
            ]
        ]
    ]
    ui_tab "loadout"
    profiletab [
        ui_list [
            ui_strut 3
            ui_list [
                ui_strut 0.75
                ui_font "emphasis" [
                    ui_center [ ui_text "CHOOSE YOUR WEAPONS" ]
                ]
                ui_strut 0.5
                ui_font "little" [
                    if (= $getplayerstate 0) [
                        ui_center [ ui_text "^fs^frWeapon changes will not take^fS"]
                        ui_center [ ui_text "^fs^freffect until after respawn!"]
                    ] [
                        ui_center [ ui_text "you will spawn with these weapons" ]
                        ui_center [ ui_text (format "you can carry ^fs^fc%1^fS %2" $playermaxcarry (? (= $playermaxcarry 1) "weapon" "weapons")) ]
                    ]
                ]
                ui_strut 0.5
                ui_list [
                    ui_strut 3
                    ui_list [
                        ui_center [ ui_text "random weapon filter:" ]
                        ui_stay_open [
                            gdr = (listlen $playerprevrandweap)
                            ui_list [
                                ui_background 0x303030
                                ui_spring 1
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    w3 = (? (> $gdr $w1) (at $playerprevrandweap $w1) 1)
                                    ui_list [
                                        if $w3 [ ui_background 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
                                        ui_strut 0.125
                                        ui_image textures/weapons/@w5 [
                                            rwv = (? (> (listlen $playerprevrandweap) @w1) (at $playerprevrandweap @w1) 1)
                                            randweaps @w1 (! $rwv)
                                        ] 0.75 0 [textures/blank] [] (? $w3 $[@[w5]colour] 0x808080)
                                        ui_strut 0.125
                                    ]
                                ]
                            ]
                        ]
                        ui_strut 1
                        ui_center [ ui_checkbox " show all weapon slots" loadweapall ]
                        ui_font "little" [
                            ui_strut 0.5
                            ui_center [ ui_text "(if a weapon is not available, the" ]
                            ui_center [ ui_text "next one in the list will be used)" ]
                            ui_strut 0.5
                        ]
                    ]
                    ui_strut 3
                ]
                ui_strut 0.5
                ui_center [ ui_checkbox " pick a loadout every new game" showloadoutmenu ]
            ]
            ui_strut 2
            ui_list [
                ui_background
                ui_center [
                    ui_stay_open [
                        gdl = (listlen $playerprevloadweap)
                        gdw = 0
                        loop w2 (? $loadweapall $weapidxloadout $playermaxcarry) [
                            w3 = (? (> $gdl $w2) (at $playerprevloadweap $w2) -1)
                            hi = (mod $w2 2)
                            al = (|| (= $w3 0) (allowedweap $w3))
                            ui_list [
                                ui_background (? (&& $al (< $gdw $playermaxcarry)) 0x303030 0x000000)
                                ui_strut 0.25
                                ui_center [ ui_text (format "%1%2" (? $hi "^fd" "^fw") (+ $w2 1)) ]
                                ui_strut 0.5
                                ui_list [
                                    if (= $w3 0) [ ui_background 0xFFFFFF 0.01 0xFFFFFF 1 1 ]
                                    ui_strut 0.125
                                    ui_image [textures/question] [loadweaps @w2 0] 0.75 0 [textures/blank] [] (? (= $w3 0) 0xFFFFFF 0x808080)
                                    ui_strut 0.125
                                ]
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    ui_list [
                                        if (= $w3 $w4) [ ui_background 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
                                        ui_strut 0.125
                                        ui_image (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [loadweaps @w2 @w4] 0.75 0 [textures/blank] [] (? (= $w3 $w4) $[@[w5]colour] 0x808080)
                                        ui_strut 0.125
                                    ]
                                ]
                            ]
                            if $al [ gdw = (+ $gdw 1) ]
                        ]
                        if (! $loadweapall) [
                            ui_strut 1.8
                            ui_list [
                                ui_strut 1.5
                                ui_spring 1
                                ui_center [ui_button "random" [loadweaps 0 0] [loadweaps 1 0]]
                                ui_spring 1
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    ui_button (format "%1" $[@[w5]longname]) [loadweaps 0 (+ $weapidxoffset @w1)] [loadweaps 1 (+ $weapidxoffset @w1)] "" $[@[w5]colour]
                                    cases $guirollovername "random" [
                                        ui_tooltip "You will get a random weapon each time you spawn" 1500
                                    ] (format "%1" $[@[w5]longname]) [
                                      ui_tooltip (format "^f[%1]primary mode:^n^fw%2^n^f[%1]secondary mode:^n^fw%3" $[@[w5]colour] $[@[w5]desc1] $[@[w5]desc2]) 1500
                                    ]
                                    ui_spring 1
                                ]
                            ]
                        ui_strut 1.8
                        ]
                    ]
                ]
            ]
        ]
    ui_visible [ ui_tip (format "press %1 to open this weapon menu at any time" (dobindsearch "show_ui profile 2")) ]
    ] 
    ui_tab "vanity items"
    profiletab [
        ui_list [
            ui_strut 3
            ui_list [
                ui_font "emphasis" [
                    ui_center [ ui_text "CHOOSE YOUR VANITIES" ]
                ]
                ui_strut 0.5
                ui_font "little" [
                    ui_center [ ui_text "these decorative items do not influence movement or abilities in any way" ]
                    ui_strut 0.5
                    ui_strut 70 1
                    ui_list [
                        ui_list [
                            ui_strut 32 1
                            ui_text "select a body part:"
                            ui_strut 0.5
                            loop i (listlen $vantypename) [
                                t = (format "^fd%1: nothing" (at $vantypename $i))
                                loop j (getvanity) [
                                    if (= (getvanity $j 0) $i) [
                                        if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [
                                            t = (format "%1:^fy %2" (at $vantypename $i) (getvanity $j 2))
                                        ]
                                    ]
                                ]
                                ui_radiobutton $t vantypesel $i [
                                    vansel = -1
                                    loop j (getvanity) [
                                        if (= (getvanity $j 0) @i) [
                                            if (< -1 (indexof  $playerprevvanity (getvanity $j 1))) [vansel = $j]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                        ui_strut 2
                        ui_list [
                            if (> $vantypesel -1) [
                                ui_text (format "options for the %1:" (at $vantypename $vantypesel))
                                ui_strut 0.5
                                ui_radiobutton "nothing" vansel -1 [
                                    loop j (getvanity) [
                                        if (= (getvanity $j 0) $vantypesel) [
                                            playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                                        ]
                                    ]
                                ]
                            ]
                            loop i (getvanity) [
                                if (= (getvanity $i 0) $vantypesel) [
                                    ui_radiobutton (getvanity $i 2) vansel $i [
                                        loop j (getvanity) [
                                            if (= (getvanity $j 0) $vantypesel) [
                                                playerprevvanity = (listdel $playerprevvanity (getvanity $j 1))
                                            ]
                                        ]
                                        append playerprevvanity (getvanity @i 1)
                                    ]
                                ]
                            ]
                        ]
                        //ui_spring
                        //ui_list [
                        //    ui_model_view (getvanity $vansel 6) "" [] 5 1
                        //]
                    ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevdisinherit [ playerprevdisinherit = 0 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
        playerprevloadweap = ""
        break = 0
        loopwhile i $weapidxloadout [= $break 0] [
            q = (getloadweap $i)
            if (< $q 0) [ break = 1 ] [ playerprevloadweap = (? $i (concat $playerprevloadweap $q) $q) ]
        ]
        playerprevrandweap = ""
        break = 0
        loopwhile i $weapidxloadout [= $break 0] [
            q = (getrandweap $i)
            playerprevrandweap = (? $i (concat $playerprevrandweap $q) $q)
        ]
    ]
]

new_ui useraccount [
    ui_header "user account"
    ui_list [
        ui_strut 2
        ui_list [
            ui_strut 0.5
            ui_font "emphasis" [ ui_center [ ui_text "RED ECLIPSE USER ACCOUNT" ] ]
            ui_strut 0.5
            ui_center [ ui_text "unique user accounts provide you with an established identity in the" ]
            ui_center [ ui_text "community, and allow server owners to add you as a local moderator." ]
            ui_center [
                ui_text "the icon before your name will also change to player status: "
                ui_no_hit_fx [ ui_image "textures/privs/player" [] 0.5 ]
            ]
            ui_strut 0.5
            ui_center [ ui_text "enter the account login information received in your email below:" ]
            ui_strut 0.5
            ui_center [ ui_text "player handle:" ]
            ui_center [ ui_textfield accountname 48 [] ]
            ui_strut 0.5
            ui_center [ ui_text "private key:" ]
            ui_center [ ui_textfield accountpass 48 [] ]
            ui_strut 1
            ui_center [ ui_checkbox "use my account online" authconnect ]
            ui_center [ ui_checkbox "use my account offline" quickauthchecks ]
            ui_strut 1
            ui_center [ ui_text "new account applications and replacement accounts are available at:" ]
            ui_center [ ui_text "^fghttp://redeclipse.net/apply" ]
            ui_strut 1
        ]
        ui_strut 2
    ]
]
