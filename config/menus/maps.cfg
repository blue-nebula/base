// CC BY-SA 4-0 https://creativecommons.org/licenses/by-sa/4.0/legalcode
// Iceflower iceflower@gmx.de
// Modified by MoonPadUSer moonpaduser@mailbox.org


truncate_string = [
    if (!=s (limitstring $arg1 $arg2) $arg1) [
        result (concatword (limitstring $arg1 (- $arg2 3)) "...")
    ] [
        result (limitstring $arg1 $arg2)
    ]
]

hidemappreviews = 0
setpersist hidemappreviews 1
setcomplete hidemappreviews 1
setdesc hidemappreviews "If set to 1, will replace all map icons with default emblem, potential performance improvement" "none"


chosenmode = -1
chosenmuts = 0
chosenmap = ""
chosendemo = -1
chosendemoimage = ""
chosendemofilename = ""

mapfavs = ""
setpersist mapfavs 1
setcomplete mapfavs 1

demofavs = ""
setpersist demofavs 1
setcomplete demofavs 1

custommapname = ""
mapsearch = ""
mapsearchon = 0
mapresults = ""
mapocta = 0

mapindex = 0
slideindex_list = "0 0 0"

mapcategory = 0
maplist = ""

voteindex = 0


//PLAYERSONSERV returns the number of player which are on the server
//hiw: subtract the sum of bots with the sum of all
playersonserv = [
    result (- (listlen (listclients 1 0)) (listlen (listclients 0 2)))
]

privnumbertotext = [
    result (sublist "None Player Supporter Moderator Operator Administrator Developer Founder" $arg1 1)
]

saveslideindexlist = [    //saves the actual guislide index in the list
    case $mapcategory 0 [
        slideindex_list = (format "%1 %2" $mapindex (sublist $slideindex_list 1 2))
    ] 1 [
        slideindex_list = (format "%1 %2 %3" (sublist $slideindex_list 0 1) $mapindex (sublist $slideindex_list 2 1))
    ] 2 [
        slideindex_list = (format "%1 %2" (sublist $slideindex_list 0 2) $mapindex)
    ]
]

modevar = [
    local g m s
    ui_stay_open [ ui_list [ ui_font "default" [
        g = $arg4
        m = $$arg3
        s = $$arg5
        if (ismodelocked $g $m 0 $s) [ ui_no_hit_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] [
            ui_button $arg1 [
            @arg2 = @arg4
            if (= $chosenmode 0) [mapcategory = -1] [
                chosendemoimage = ""
                if (= $mapcategory -1) [mapcategory = 0]
            ]
            slideindex_list = "0 0 0"    //reset slideindex because a new list will be generated with a different mode
            mapindex = 0
            fillmaparray
            if (< (indexof $maplist $chosenmap) 0) [chosenmap = ""]
            ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (= $$arg2 $arg4) "checkboxon" "") $ui_color_checkbox
        ]
    ] ] ]
]

mutsvar = [
    local g m s t
    ui_stay_open [ ui_list [
        g = $$arg2
        m = $$arg3
        s = $$arg5
        if (< $g 0) [ ui_no_hit_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] [
            if (|| (& (mutsimplied $g $m) $arg4) (& $mutslockforce $arg4)) [
                ui_button $arg1 [ implied = @arg4 ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 "checkboxon" $ui_color_checkbox_two
            ] [
                if (ismodelocked $g (| $m $arg4) $arg4 $s) [ ui_no_hit_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] [
                    t = (& $m $arg4)
                    ui_button $arg1 [
                        mutate = @arg4
                        if @t [@@arg3 = @@(mutscheck $g (- $m $arg4))] [
                            @@arg3 = @@(mutscheck $g (| $m $arg4) $arg4)
                        ]
                        slideindex_list = "0 0 0"    //reset slideindex because a new list will be generated with a different mode
                        mapindex = 0
                        fillmaparray
                    ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $t "checkboxon" "") $ui_color_checkbox
                ]
            ]
        ]
    ] ]
]

mapselection = [
    ui_list [
        ui_list [
            local j
            j = 0
            looplist i (sublist $maplist $mapindex 8) [
                isfav = (!= (listfind j $mapfavs [result (=s $j $i)]) -1)
                ui_strut 0.2
                ui_list [
                    if (=s $chosenmap $i) [ui_background 0x444444] [ui_background 0x222222]
                    ui_list [
                        ui_merge 30 [
                            if (! ($hidemappreviews)) [
                                ui_image (? (= $mapcategory 2) $i (concatword "maps/" $i)) [] 1.5 1 "textures/emblem"
                            ] [
                                ui_image "textures/emblem" [] 1.5 1
                            ]
                            ui_strut 1
                            ui_list [
                                if (!=s $i "<random>") [
                                    // The base name of the map.
                                    map_base_name = (? (= $mapcategory 2) (substring $i 5) $i)

                                    // Compile a list of the map's sizes, running through small, medium, and large variables.
                                    map_display_sizes = ""
                                    // Loop through descriptors of each size: <list variable> <display string>
                                    looplist map_size_desc [ "smallmaps ^f[0x008000]Small" "mediummaps ^f[0x00C000]Medium" "largemaps ^f[0x00FF00]Large"] [
                                        // If this map is in the list variable.
                                        if (!= (indexof $(at $map_size_desc 0) $map_base_name) -1) [
                                            // The display string. Unescaped to enable the formatting colors.
                                            map_display_size = (unescape (at $map_size_desc 1))

                                            // Add the unescaped display string to the sizes.
                                            // Simply replace the list if it does not exist, otherwise concat.
                                            if (=s $map_display_sizes "") [
                                                map_display_sizes = $map_display_size
                                            ] [
                                                map_display_sizes = (concat $map_display_sizes $map_display_size)
                                            ]
                                        ]
                                    ]

                                    // Set the strut offset depending on if any sizes must be displayed.
                                    if (=s $map_display_sizes "") [
                                        map_display_size_offset = 0
                                    ] [
                                        map_display_size_offset = 0.25
                                    ]
                                    ui_strut (-f 0.5 $map_display_size_offset)
                                    ui_button (concatword (? ($isfav) "^fb" "") (truncate_string $map_base_name 29)) []
                                    ui_font "little" [
                                        if (!= $mapcategory 2) [ui_button (getmapmodemuts $i) []] [ui_button "^f[0x808080]sauerbraten" []]
                                    ]
                                    // Display sizes if available.
                                    if (!=s $map_display_sizes "") [
                                        ui_font "little" [
                                            ui_button $map_display_sizes []
                                        ]
                                    ]
                                    ui_strut 25 (+f 1 $map_display_size_offset)
                                ] [
                                    ui_strut 0.5
                                    ui_button "Random"
                                    ui_font "little" [
                                        ui_button "^f[0x808080]Any" []
                                    ]
                                    ui_strut 28 1
                                ]
                            ]
                        ] [
                            if (= $chosenmode -1) [fillmaparray]
                            chosenmap = @i
                        ] [] []
                    ]
                    j = (+ $j 1)
                    ui_list [ if (!=s $i "<random>") [
                        ui_spring 1
                        if ($isfav) [
                            ui_image "textures/icons/fontawesome/graphics" [mapfavs = (listdel $mapfavs @@i); fillmaparray] 0.75 0 "" [] 0x6666ff
                        ] [
                            ui_image "textures/icons/fontawesome/favourite_off" [mapfavs = (concat $mapfavs @@i); fillmaparray] 0.75 0 "" []
                        ]
                        if (&& (> $mapcategory 0) (> $chosenmode 1)) [
                            curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
                            isprev = (!= (listfind j $curprev [result (=s $j $i)]) -1)
                            islocked = (= (listfind j (getmaplist $chosenmode $chosenmuts $allowmaps) [result (=s $j $i)]) -1)
                            if ($isprev) [
                                ui_no_hit_fx [ui_image "textures/waiting" [] 0.75 0 "" [] 0x999999 ]
                            ] [
                                if (= isonline 1) [    //only if online
                                    if ($islocked) [
                                        ui_no_hit_fx [
                                            ui_image "textures/warning" [] 0.75 0 "" [] 0x999999
                                        ]
                                    ]
                                ]
                            ]
                            if (= isonline 1) [    //only if online
                                if (=s $guirollovername textures/warning) [
                                    ui_tooltip (format "This map is not in the rotation^n%1s or higher can still vote for this map" (privnumbertotext $modelock))
                                ]
                            ]
                            if (=s $guirollovername textures/waiting) [
                                ui_tooltip (format "Recently played maps: %1" (prettylist $curprev and)) 400
                            ]
                        ]

                        if (=s $guirollovername textures/icons/fontawesome/favourite_off) [
                            ui_tooltip "Add to favorites and move to the top" 400
                        ]
                        if (=s $guirollovername textures/icons/fontawesome/graphics) [
                            ui_tooltip "Remove from favorites"
                        ]
                        ui_spring 1
                    ] ]
                ]
            ]
            if (=s $maplist "") [
                j = 1
                ui_strut 0.2
                ui_merge 35 [
                    ui_background 0x444444
                    ui_no_hit_fx [
                        ui_image "textures/info" [] 1.5 1
                    ]
                    ui_strut 1
                    ui_list [
                        ui_strut 0.5
                        ui_text "no matching maps"
                        ui_font "little" [ ui_text "^fdTry another search filter"]
                    ]
                    ui_spring 1
                ]
            ]
            if (< $j 8) [
                loop i (- 8 $j) [
                    ui_strut 0.2
                    ui_merge 30 [
                        ui_background 0x111111
                        ui_strut 3 1
                        ui_strut 35
                    ] []
                ]
            ]
        ]
        ui_slider mapindex 0 (max 0 (- (listlen $maplist) 8)) [] 1 1
    ]
]

parsedemo = [
    local y mn d h m months
    months = "January February March April May June July August September October November December"
    y = (substring $arg1 0 4)
    mn = (substring $arg1 4 2)
    d = (substring $arg1 6 2)
    h = (substring $arg1 8 2)
    m = (substring $arg1 10 2)
    result (concatword (at $months (- $mn 1)) " " $d ", " $y ", " $h ":" $m)
]

demoselection = [
    ui_list [
        ui_list [
            local j
            j = 0
            looplist i (sublist $maplist $mapindex 8) [
                isfav = (!= (listfind j $demofavs [result (=s $j $i)]) -1)
                ui_strut 0.2
                ui_list [
                    if (=s $chosenmap $i) [
                        ui_background 0x444444
                    ] [
                        ui_background 0x222222
                    ]
                    ui_list [
                        ui_merge 30 [
                            t_chosendemo = (demoscan (concatword "demos/" $i ".dmo"))
                            ui_image (concatword "maps/" (demoinfo $t_chosendemo 1)) [] 1.5 1 "textures/emblem"
                            ui_strut 1
                            ui_list [
                                ui_strut 0.5
                                ui_button (concatword (? ($isfav) "^fb" "") (? (= (stringlen $i) 14) (parsedemo $i) $i)) []
                                ui_font "little" [
                                    ui_button (concatword "^f[0x808080]" (gamename (demoinfo $t_chosendemo 2) (demoinfo $t_chosendemo 3) 0 32) " on " (demoinfo $t_chosendemo 1)) []
                                ]
                                ui_strut 25 1
                            ]
                        ] [
                            chosenmap = @i
                            chosendemo = (demoscan (concatword "demos/" @i ".dmo"))
                            chosendemoimage = (concatword "maps/" (demoinfo $chosendemo 2))
                        ] [] []
                    ]
                    j = (+ $j 1)
                    ui_list [
                        ui_spring 1
                        if ($isfav) [
                            ui_image "textures/icons/fontawesome/graphics" [mapfavs = (listdel $mapfavs @@i); fillmaparray] 0.75 0 "" [] 0x6666ff
                        ] [
                            ui_image "textures/icons/fontawesome/favourite_off" [mapfavs = (concat $mapfavs @@i); fillmaparray] 0.75 0 "" []
                        ]
                        if (=s $guirollovername textures/icons/fontawesome/favourite_off) [
                            ui_tooltip "Add to favorites and move to the top" 400
                        ]
                        if (=s $guirollovername textures/icons/fontawesome/graphics) [
                            ui_tooltip "Remove from favorites"
                            ]
                        ui_spring 1
                    ]
                ]
            ]
            if (=s $maplist "") [
                j = 1
                ui_strut 0.2
                ui_merge 35 [
                    ui_background 0x444444
                    ui_no_hit_fx [
                        ui_image "textures/info" [] 1.5 1
                    ]
                    ui_strut 1
                    ui_list [
                        ui_strut 0.5
                        ui_text "No matching maps"
                        ui_font "little" [ ui_text "^fdTry another search filter"]
                    ]
                    ui_spring 1
                ]
            ]
            if (< $j 8) [
                loop i (- 8 $j) [
                    ui_strut 0.2
                    ui_merge 30 [
                        ui_background 0x111111
                        ui_strut 3 1
                        ui_strut 35
                    ] []
                ]
            ]
        ]
        ui_slider mapindex 0 (max 0 (- (listlen $maplist) 8)) [] 1 1
    ]
]

voteselection = [
    ui_list [
        ui_list [
            ui_checkbox "^fcDynamic sort" sortvotes; ui_strut 1.5
            ui_checkbox "^foCleanup list" cleanvotes; ui_strut 1.5
            ui_button   "^fyClear vote" clearvote

            ui_spring
            ui_list [
                ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border
                ui_list [
                    ui_strut 1
                    votesum = 0    //prepare loop
                    loop j (getvote) [votesum = (+ $votesum (getvote $j 0))]    //loop for counting the sum of all votes
                    ui_text (format "^fc%1^fw/^fc%2 ^fwVote%3" $votesum (playersonserv) (? (!= $votesum 1) "s"))
                    ui_strut 1
                ]
            ]
        ]
        ui_strut 1
        if (getvote) [
            ui_list [
                ui_list [
                    loop j (if (< (getvote) 8) [result (getvote)] [result 8]) [
                        i = (+ $j $voteindex)
                        ui_strut 0.2
                        voteplayers = (getvote $i 0)
                        votemode = (getvote $i 1)
                        votemuts = (getvote $i 2)
                        votemap = (getvote $i 3)
                        voteself = 0
                        ui_list [
                            loop j $voteplayers [if (= (getclientnum) (getvote $i 0 $j)) [voteself = 1]]
                            if (= $voteself 1) [
                                ui_background 0x444444
                            ] [
                                ui_background 0x222222
                            ]
                            ui_list [
                                ui_merge 74 [
                                    ui_strut 1
                                    ui_center [
                                        ui_strut 4 1
                                        votecolour = (? $voteself "^fy" "^fw")
                                        ui_list [
                                            ui_center [ui_font "huge" [ui_button (concatword $votecolour $voteplayers) ]]
                                            ui_strut 0.3
                                            ui_list [
                                                ui_center [ui_font "little" [ui_button (format "%1 vote%2" $votecolour (? (!= $voteplayers 1) "s") )]]
                                                ui_center [ui_font "default" [ui_button (format "%1 %2%%" $votecolour (precf (*f (divf 100 (playersonserv)) $voteplayers) 0)) ]]    //can set the precision if its possible i think no decimal looks best
                                            ]
                                        ]
                                    ]
                                    ui_strut 1
                                    if (!= (stringstr $votemap "base/") -1) [
                                        ui_image $votemap "" 1.5 1 "textures/emblem"
                                        echo $votemap
                                    ] [
                                        ui_image (concatword "maps/" $votemap) "" 1.5 1 "textures/emblem"
                                    ]
                                    ui_strut 1
                                    ui_center [
                                        gname = (gamename $votemode $votemuts 0 32)
                                        ui_font "reduced" [ui_button (format "^fy%1 ^faon ^fo%2" $gname (? (!= (stringstr $votemap "base/") -1) (substring $votemap 5) $votemap))]
                                        mutlist = ""
                                        if (> $votemuts 0) [mutlist = (stringreplace (sublist (gamename $votemode $votemuts) 0 1) "-" "^n")]
                                        cases $guirollovername (format "^fy%1 ^faon ^fo%2" $gname (? (!= (stringstr $votemap "base/") -1) (substring $votemap 5) $votemap)) [ui_tooltip $mutlist]    //guirollovername from the button above
                                        ui_list [
                                            if (> $voteplayers 0) [
                                                ui_font "reduced" [ui_button "^fwby "]
                                                ui_font "little" [
                                                    pname = ""
                                                    pmore = 0
                                                    loop j $voteplayers [
                                                        if (|| $pmore (> (ui_text_width $pname) 1220)) [ pmore = (+ $pmore 1) ] [
                                                            append pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                                        ]
                                                    ]
                                                    ui_button (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                                ]
                                            ] [
                                                ui_font "reduced" [ui_button "^faNo current votes"]
                                            ]
                                        ]
                                    ]
                                ] [if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]] [] []
                            ]
                        ]
                    ]
                ]
                ui_slider voteindex 0 (max 0 (- (getvote) 8)) [] 1 1
                ui_strut 24.2 1
            ]
        ] [
            ui_list [
                ui_strut 76
                ui_strut 12 1
            ]
            ui_list [
                ui_spring 1
                ui_button " There are no votes currently pending, ^fgSubmit^fw one yourself" [show_ui maps 1]
                ui_spring 1
            ]
        ]
    ] [
        ui_visible [ ui_tip (format "Press %1 to open this menu at any time" (dobindsearch "show_ui maps 2")) ]
    ]
]

initmapsel = [
    chosendemofilename = ""
    chosendemoserver = ""
    mapcategory = 0
    mapindex = 0
    slideindex_list = "0 0 0"
    chosenmap = ""
    chosendemo = -1
    chosenmode = -1
    chosenmuts = 0
    maplist = ""
    fillmaparray
]

extraoptions = [
    if (= $chosenmode 1) [
        ui_text "^fbMap name"
        ui_strut 0.5
        ui_textfield chosenmap 34
    ]
    if (gspmutname $chosenmode 0) [
        ui_text "^fbExtra Mutators"
        ui_strut 0.5
        ui_font little [
            ui_loop_split n 3 $mutsidxgsn [
                mut = (gspmutname $chosenmode $n)
                if (stringlen $mut) [
                    mutsvar $mut chosenmode chosenmuts (<< 1 (+ $mutsidxgsp $n)) chosenmap
                ]
            ] [ ui_strut 3 ]
        ]
    ]

    ui_strut 1

    if (! (isonline)) [
        ui_list [
            ui_text "Server Type:"; ui_strut 0.5
            ui_radiobutton "Offline" servertype 0; ui_strut 0.5
            ui_radiobutton "Private" servertype 1; ui_strut 0.5
            ui_radiobutton "Public" servertype 2
        ]
    ]
]

fillmaparray = [ octapaks [
    maplist = ""
    case $mapcategory 0 [
        // Construct the "available maps" list from only maps that work with the chosen game.
        // The <random> special value will select from these maps as well, so it is included in this category.
        t_maplist = (concat "<random>" (getmaplist $chosenmode $chosenmuts $allowmaps))
        maplist = (listdel $t_maplist $mapfavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr $i $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ] 1 [
        t_maplist = ""
        loopfiles i maps mpz [
            t_maplist = (concat $t_maplist $i)
        ]
        t_maplist = (sortlist $t_maplist a b [<s $a $b])
        maplist = (listdel $t_maplist $allowmaps)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = $maplist
        maplist = (listdel $t_maplist $mapfavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr $i $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ] 2 [
        t_maplist = ""
        loopfiles i base ogz [
            t_maplist = (concat $t_maplist (concatword "base/" $i))
        ]
        t_maplist = (sortlist $t_maplist a b [<s $a $b])
        maplist = (listdel $t_maplist $mapfavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr (substring $i 5) $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ] () [
        t_maplist = ""
        loopfiles i demos dmo [
            t_maplist = (concat $t_maplist $i)
            t_maplist = (sortlist $t_maplist a b [>s $a $b])
        ]
        maplist = (listdel $t_maplist $demofavs)
        maplist = (concat (listdel $t_maplist $maplist) $maplist)
        t_maplist = ""
        if ($mapsearchon) [
            looplist i $maplist [
                if (> (stringstr (demoinfo (demoscan (concatword "demos/" $i ".dmo")) 1) $mapsearch) -1) [
                    t_maplist = (concat $t_maplist $i)
                ]
            ]
            maplist = $t_maplist
        ]
    ]
    mapindex = (? (< (+ $mapindex 8) (listlen $maplist)) $mapindex (? (< (- (listlen $maplist) 8) 0) 0 (- (listlen $maplist) 8)))
]]

getmapmodemuts = [
    local a
    a = "^f[0x808080]"
    if (!= (listfind i $capturemaps [=s $i $arg1]) -1) [
        a = (concatword $a "CTF ")
    ]
    if (!= (listfind i $defendmaps [=s $i $arg1]) -1) [
        a = (concatword $a "DTF ")
    ]
    if (!= (listfind i $bombermaps [=s $i $arg1]) -1) [
        a = (concatword $a "BB ")
    ]
    if (!= (listfind i $holdmaps [=s $i $arg1]) -1) [
        a = (concatword $a "Hold ")
    ]
    if (!= (listfind i $racemaps [=s $i $arg1]) -1) [
        a = (concatword $a "Race ")
    ]
    if (!= (listfind i $multimaps [=s $i $arg1]) -1) [
        a = (concatword $a "Multi ")
    ]
    if (!= (listfind i $duelmaps [=s $i $arg1]) -1) [
        a = (concatword $a "Duel ")
    ]
    if (&& (!= (listfind i $allowmaps [=s $i $arg1]) -1) (=s $a "^f[0x808080]")) [
        a = (concatword $a "Deathmatch ")
    ]
    if (=s $a "^f[0x808080]") [
        a = (concatword $a "Edit ")
    ]
    result $a
]

octapaks [
    fillmaparray
]

new_ui maps [ ui_stay_open [ octapaks [
    ui_header "Maps"
    ui_list [
        ui_list [
            ui_list [
                ui_spring 1
                ui_text (? (= $chosenmode -1) "Game selection" (gamename $chosenmode (? (= $chosenmode 0) 0 $chosenmuts) 0 32))
                ui_spring 1
            ]
            ui_list [
                ui_font little [
                    ui_spring 1
                    case $chosenmode -1 [
                        ui_text "^fySelect a mode and a map to continue"
                    ] 0 [
                        if (> (stringlen $chosenmap) 0) [
                            ui_text (concatword "^f[0x808080]" (gamename (demoinfo $chosendemo 2) (demoinfo $chosendemo 3) 0 32) " on " (demoinfo $chosendemo 1))
                        ] [
                            ui_text "Select a demo"
                        ]
                    ] () [
                        ui_text (? (=s $chosenmap "") "^fySelect a map to continue" (concat "on" (? (= $mapcategory 2) (substring $chosenmap 29) $chosenmap)))
                    ]
                    ui_spring 1
                ]
            ]
            ui_list [
                ui_font console [
                    ui_spring 1
                    ui_button "^f[0x9030F0]Random" [
                        mapcategory = 0
                        chosenmap = (at $allowmaps (rnd (listlen $allowmaps)))
                        chosenmode = (+ (rnd (- $modeidxnum $modeidxplay)) $modeidxplay)
                        chosenmuts = (mutscheck $chosenmode (rnd $mutsbitall))
                        fillmaparray
                    ]
                    ui_spring 1
                    ui_button "^f[0x9030F0]Reset" [
                        mapcategory = 0
                        chosenmap = ""
                        chosenmode = -1
                        chosenmuts = 0
                        chosendemoimage = ""
                        fillmaparray
                    ]
                    ui_spring 1
                    ui_button "^f[0x9030F0]Copy" [
                        mapcategory = 1
                        chosenmode = (gamemode)
                        if (< (mutators) 0) [chosenmuts = 0] [chosenmuts = (mutscheck $chosenmode (mutators))]
                        chosendemoimage = ""
                        fillmaparray
                    ]
                    ui_spring 1
                ]
            ]
            ui_list [
                ui_list [
                    ui_text "^fbMode"
                    ui_strut 1
                    ui_font "default" [
                        loop z $modeidxnum [
                            if (&& (!= $servertype 1) (= $z 0) (isonline)) [
                                if (= $chosenmode 0) [
                                    initmapsel
                                ]
                            ] [
                                modevar (at $modename $z) chosenmode chosenmuts $z chosenmap
                            ]
                        ]
                    ]
                ]
                ui_strut 2
                ui_list [
                    ui_no_hit_fx [
                        if (= $mapcategory -1) [
                            ui_image $chosendemoimage [] 5 1 "textures/emblem"
                        ] [
                            if (=s $chosenmap "") [// if no map is choosen the emblem will shown
                                ui_image "textures/emblem" [] 5 1
                            ] [
                                if (|| (= (findfile (concatword "maps/" $chosenmap ".png")) 1) (= (findfile (concatword "maps/" $chosenmap ".jpg")) 1)) [// looks if a image of the chosenmap exist    in RE NOT Sauer
                                    ui_image (concatword "maps/" $chosenmap) [] 5 1 "textures/emblem"
                                ] [
                                    ui_image $chosenmap [] 5 1 "textures/emblem"//show Sauer map image
                                ]
                            ]
                        ]
                    ]
                ]
                ui_strut 1
            ]
            ui_strut 1
            if (!= $chosenmode 0) [
                ui_list [
                    ui_list [
                        ui_text "^fbMutators"
                        ui_strut 0.5
                        ui_font "default" [
                            count = (- $mutsidxnum $mutsidxgsn)
                            ui_loop_split n 3 $count [
                                mutsvar (at $mutsname $n) chosenmode chosenmuts (<< 1 $n) chosenmap
                            ] [ ui_strut 3 ]
                        ]
                    ]
                ]
            ]
            ui_strut 1
            extraoptions
        ]
        ui_spring 1
        ui_list [
            if (&& (getclientpriv (getclientnum) $mapslock) (!= $chosenmode 0)) [
                ui_list [
                    ui_button (? (= $mapcategory 0) "Available" "^f[0x808080]Available") [saveslideindexlist; mapcategory = 0; fillmaparray; mapindex = (sublist $slideindex_list $mapcategory 1); if (< (indexof $maplist $chosenmap) 0) [chosenmap = ""]]
                    ui_strut 1
                    ui_button (? (= $mapcategory 1) "All Maps" "^f[0x808080]All Maps") [saveslideindexlist; mapcategory = 1; fillmaparray;  mapindex = (sublist $slideindex_list $mapcategory 1)]
                    if (= $hasoctapaks 1) [
                        ui_strut 1
                        ui_button (? (= $mapcategory 2) "Sauerbraten" "^f[0x808080]Sauerbraten") [saveslideindexlist; mapcategory = 2; fillmaparray;  mapindex = (sublist $slideindex_list $mapcategory 1)]
                    ]
                ]
            ] [
                ui_strut 1
            ]
            ui_list [
                ui_textfield mapsearch 25 [
                    if (stringlen $mapsearch) [mapsearchon = 1] [mapsearchon = 0]
                    fillmaparray
                ] -1 0 "" 0 "^fd Search" 1
                ui_strut 0.7
                if (= $mapsearchon 1) [//show the button for disable filter only if something is searched
                    ui_center [
                        ui_button "Filter" [ mapsearch = "" ; mapsearchon = 0; fillmaparray] [] "textures/guiexit"
                    ]
                ]
            ]
            if (= $chosenmode 0) [
                demoselection
            ] [
                mapselection
            ]

            ui_strut 0.5

            ui_checkbox "Hide Map Previews" hidemappreviews 1 0
        ]
    ]
    ui_strut 0.5
    ui_font super [
        ui_center [
            if (&& (>= $chosenmode 0) (stringlen $chosenmap)) [
                ui_button (? (isonline) "^fgVote" "^fgPlay")  [if (isonline) [show_ui maps 2]; (? (= (gamemode) 1) (savewarnchk [start $chosenmap $chosenmode (chosenmuts)]) (start $chosenmap $chosenmode (chosenmuts))); mapsearch = ""]
            ] [
                ui_text (? (isonline) "^fdVote" "^fdPlay")
            ]
        ]
    ]
    ui_visible [
        cases (at $guirolloveraction 0
        ) "chosenmode" [
            ui_tooltip (modedesc (at $guirolloveraction 2) $chosenmuts 3)
        ] "mutate" [
            ui_tooltip (mutsdesc $chosenmode (at $guirolloveraction 2) 3)
        ] "implied" [
            ui_tooltip "This is forced on by the current configuration"
        ] "disabled" [
            ui_tooltip "This is disabled by the current configuration"
        ]
        ui_tip (format "Press %1 to open this menu at any time" (dobindsearch "show_ui maps 1"))
    ]
    if (= (isonline) 1) [ //show tab votes only if you are online
        ui_tab (format "Votes (%1)" (getvote))    //ui_tab "votes"
        ui_list [
            voteselection
            ui_visible [
                ui_tip (format "Press %1 to open this menu at any time" (dobindsearch "show_ui maps 2"))
            ]
        ]
    ]
]]] [
     if (= $guipasses 0) [
            initmapsel
            voteindex = 0
     ]
]
