new_ui team [
    ui_one [
        ui_font "default" [ ui_center [ ui_text "choose team" ] ]
        ui_strut 1
        if (isspectator (getclientnum)) [
            ui_center [ ui_button (format "^fw^f(%1) unspectate" $playertex) [spectate 0] ]
        ] [
            ui_center [ ui_button (format "^fw^f(%1) spectate" $spectatortex) [spectate 1] ]
        ]
        if (&& [>= (gamemode) $modeidxstart] [! (& (mutators) $mutsbitcoop)] (|| [! (& (mutators) $mutsbitffa)] [& (mutators) $mutsbitmulti])) [
            ui_strut 1
            loop t (? (& (mutators) $mutsbitmulti) 4 2) [
                push t (at "alpha omega kappa sigma" $t) [
                    ui_center [ ui_button (format "^f[%1]^f(%2) %3" $[team@[t]colour] $[team@[t]tex] $[team@[t]name]) [team @t] ]
                ]
            ]
        ]
        ui_strut 1
        ui_font "default" [ ui_center [ ui_button "^focancel" [clear_ui 1] [] "action" ] ]
    ]
    ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui team"))
]

trimstring = [
    // trims arg1 to textwidth $arg2
    // a proper function may be more efficient
    if (> (textwidth $arg1) $arg2) [
        local str w l
        str = $arg1
        l = (stringlen $str)
        w = (- $arg2 (textwidth "..."))
        loopwhile i 1000 [ > (textwidth $str) $w] [
            l = (- $l 1)
            str = (substring $str 0 $l)
        ]
        result  (concatword $str "^fa...")
    ] [
        result $arg1
    ]
]

// client info menu
showbots = 0
reason = ""
client_tab = [
    clempty = (! (getclientpresence $cnsel))
    clname = (? $clempty "empty" (getclientname $cnsel))
    clcname = (? $clempty "empty" (getclientname $cnsel 1))
    clmodel = (? $clempty 0 (getclientmodel $cnsel))
    clcolour = (? $clempty 0 (getclientcolour $cnsel))
    clteam = (? $clempty 0 (getclientteam $cnsel))
    clweap = (? $clempty 0 (getclientweapselect $cnsel))
    clvanity = (? $clempty "" (getclientvanity $cnsel))
    //cltab = (format "\f[%1]%2" (? $clempty 0x111111 $clcolour) $cnsel)
    ui_list [
        ui_font play [ui_text (format "selected: cn %1 %2" $cnsel (getclienthandle $cnsel))]
        ui_spring 1
        ui_checkbox "include bots" showbots
    ]
    ui_font play [ui_text $clcname]
    ui_list [
        // preview with profile tooltip
        ui_body [
            ui_no_cursor_fx [ ui_player_preview $clmodel $clcolour $clteam $clweap $clvanity [] 7.5 1 1]
        ] [] [] [
            ui_tooltip (concat "colour:" (hexcolour $clcolour) "^nmodel:" (? $clmodel "female" "male" ) "^nvanities:^n" $clvanity) 900
        ]
        ui_strut 1
        // full loadout weaps with border around the current weapon
        ui_list [
            ui_strut 0.5
            loop i $weapidxloadout [
                wi = (getclientloadweap $cnsel $i)
                if (> $wi 0) [
                    wn = (at $weapname $wi)
                    ui_no_cursor_fx [ui_image textures/weapons/@[wn] [] 1 (= $wi (getclientweapselect $cnsel)) "" [] $[@[wn]colour]]
                ] [
                    ui_no_cursor_fx [ui_image textures/question [] 1 0 "" [] 0xaaaaaa] // random / no preference
                ]
            ]
            ui_strut -0.5
        ]
        ui_strut 2
        ui_list [
            ui_strut 0.2
            ui_background $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
            ui_strut 0.5
            ui_list [
                ui_strut 1.5
                ui_list [
                    ui_strut 12 1
                    ui_text "Chat" [] [0x9999DD]
                    ui_strut 0.5
                    ui_button "paste name" [saycommand @(getsaycolour)@clname, ""] [] $chattex
                    ui_strut 0.2
                    ui_button "whisper   " [saycommand /whisper $cnsel ""] [] $playertex
                    ui_strut 1.2
                    ui_text "Client info" "" 0xDD9999
                    ui_strut 0.5
                    ui_text (format "RE %1" (getclientversion $cnsel 13))
                    ui_strut 0.2
                    ui_text (format "protocol %1" (getclientversion $cnsel 3))
                    ui_strut 0.2
                    content = (getclienthost $cnsel)
                    if $content [if (=s $content "unknown") [content = "localhost"] []] [content = "no information available"]
                    ui_body [ ui_text "IP/host"] [] [] [ui_tooltip $content]
                    ui_strut 0.2
                    content = (concat "version^t" (getclientversion $cnsel 6) "^nshader^t" (getclientversion $cnsel 7) )
                    ui_body [ ui_text "OpenGL"] [] [] [ui_tooltip $content]
                    ui_strut 0.2
                    content = (getclientversion $cnsel 8)
                    ui_body [ ui_text "CRC checksum"] [] [] [ui_tooltip $content]
                    ui_strut 0.2
                    content = (concatword (getclientversion $cnsel 9) "^n" (getclientversion $cnsel 10) "^nver " (getclientversion $cnsel 11))
                    ui_body [ ui_text "GPU info"] [] [] [ui_tooltip $content]
                ]
                ui_strut 1.5
            ]
            ui_strut 0.5
        ]
        ui_strut 2
        ui_list [
            ui_strut 0.2
            ui_background $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend 1
            ui_strut 0.5
            ui_list [
                ui_strut 1.5
                ui_list [
                    ui_text "Moderation" [] 0xFFFFAA
                    ui_strut 0.5
                    if (ismoderator $getclientnum) [
                        ui_textfield reason -10 [] -1 0 "" 0 "^fdenter reason"
                    	ui_strut 0.5
                        ui_button "warn" [saycommand "^fzyw"@[clname], $reason] [] $alerttex
                    	ui_strut 0.2
                    ] [
                        ui_text "no moderator"
                        ui_text "privileges"
                        ui_strut 1
                        ui_button "claim privs" [setpriv 1] [] $privlocalmoderatortex
                        ui_strut 0.2
                        ui_button "admin pw" [saycommand /setpriv ""] [] $privlocaladministratortex
                        if (=s "" (getclienthandle $getclientnum)) [
                            ui_strut 0.2
                            ui_button "auth" [auth] [] $privmoderatortex
                            ]
                    ]
                    if (getclientpriv $getclientnum $mutelock) [
                        ui_button "mute" [saycommand /mute $cnsel $reason] [] $chattex
                    	ui_strut 0.2
                    ]
                    if (getclientpriv $getclientnum $speclock) [
                        if (isquarantine $cnsel) [
                            ui_button "^fzgwrelease" [saycommand /spectator $cnsel 0] [] $spectatortex
                    	    ui_strut 0.2
                        ] [
                            ui_button "quarantine" [saycommand /spectator $cnsel 2] [] $spectatortex
                    	    ui_strut 0.2
                        ]
                    ]
                    if (getclientpriv $getclientnum $kicklock) [
                        ui_button "kick" [saycommand /kick $cnsel $reason] [] $clawtex
                    	ui_strut 0.2
                    ]
                    if (getclientpriv $getclientnum $banlock) [
                        ui_button "ban!" [saycommand /ban $cnsel $reason] [] $warningtex
                    	ui_strut 0.2
                    ]
                    if (getclientpriv $getclientnum $teamlock) [
                        if (! (& (mutators) $mutsbitffa)) [
                            ui_text "team / limit" []
                            ui_list [
                                ui_button " " [saycommand /setteam $cnsel alpha] [] $teamalphatex -1 $teamalphacolour
                                ui_button " " [saycommand /setteam $cnsel omega] [] $teamomegatex -1 $teamomegacolour
                                if (& (mutators) $mutsbitmulti )[
                                    ui_button " " [saycommand /setteam $cnsel kappa] [] $teamkappatex -1 $teamkappacolour
                                    ui_button " " [saycommand /setteam $cnsel sigma] [] $teamsigmatex -1 $teamsigmacolour
                                ]
                                if (getclientpriv $getclientnum $limitlock) [
                                    ui_button  " " [saycommand /limit $cnsel $reason] [] $waitingtex 0x666666
                                ]
                            ]
                        ]
                    ]
                    //if (ismoderator $getclientnum) [
                    //    ui_strut 1
                    //    ui_text "Clear" [] 0x33ff33
                    //    ui_strut 0.5
                    //    ui_list [
                    //        ui_button " " [saycommand /clearmutes] [] $chattex
                    //        ui_button " " [saycommand /clearbans] [] $warningtex
                    //        ui_button " " [saycommand /clearlimits] [] $waitingtex
                    //    ]
                    //]
                ]
                ui_strut 1.5
            ]
            ui_strut 0.5
        ]
    ]
    ui_strut 1
    // client selection
    if $showbots [
        cnlist = (listclients 1)
    ] [
        cnlist = (listdel (listclients 1) (listclients 0 2))
    ]
    if (& (mutators) $mutsbitffa) [
        // FFA layout, save space with three cols
        ui_loop_list_split cn 3 $cnlist [
            ui_strut 25 1
            content = (getclientname $cn 1)
            ui_radiobutton (trimstring $content 1250) cnsel $cn
        ]
    ] [
        i = 0 // team index
        teamneutraltex = $spectatortex
        if (& (mutators) $mutsbitmulti) [ // 4 teams
            ui_list [
                looplist t [neutral alpha omega kappa sigma] [
                    ui_list [
                        ui_strut 15 1
                        ui_text $[team@[t]name] $[team@[t]tex] $[team@[t]colour] $[team@[t]colour]
                        looplist cn ($cnlist)  [
                            if (= (getclientteam $cn) $i) [
                                content = (getclientname $cn 1)
                                ui_radiobutton (trimstring $content 750) cnsel $cn
                            ]
                        ]
                    ]
                    i = (+ $i 1)
                ]
            ]
        ] [ // 2 teams
            ui_list [
                looplist t [neutral alpha omega] [
                    ui_list [
                        ui_strut 25 1
                        ui_text $[team@[t]name] $[team@[t]tex] $[team@[t]colour] $[team@[t]colour]
                        looplist cn ($cnlist)  [
                            if (= (getclientteam $cn) $i) [
                                content = (getclientname $cn 1)
                                ui_radiobutton (trimstring $content 1250) cnsel $cn
                            ]
                        ]
                    ]
                    i = (+ $i 1)
                ]
            ]
        ]
    ]
]

overview_tab = [
ui_center [ ui_list [
    ui_strut 0.4
    ui_background $guifieldbgcolour $guifieldbgblend $guifieldbgcolour $guifieldbgblend 1
    ui_strut 0.4
        ui_list [
            ui_strut 0.4
            loop icolumn 6 [
                ui_list [
                    switch = 0
                    loop i 5 [
                        looplist cn (listdel (listclients 1) (listclients 0 2))  [
                            if (= (getclientteam $cn) $i) [
                                if (= $switch 0) [rowcolour = "0x303030"; switch = 1] [rowcolour = "0x252525"; switch = 0]
                                case ($icolumn
                                    ) 1 [
                                        ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                            content = (getclientname $cn 1)
                                            ui_strut 0.1; if $content [ui_text $content] [ui_text "no information avaible" [] [0x444444]]; ui_strut 0.1
                                        ] ]
                                    ] 2 [
                                        ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                            content = (getclienthandle $cn)
                                            ui_strut 0.1; if $content [ui_text $content] [ui_text "no handle" [] [0x444444]]; ui_strut 0.1
                                        ] ]
                                    ] 3 [
                                        ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                            content = (getclienthost $cn)
                                            ui_strut 0.1; if $content [if (=s $content "unknown") [ui_text "localhost"] [ui_text $content]] [ui_text "no information available" [] [0x444444]]; ui_strut 0.1
                                        ] ]
                                    ] 4 [
                                        ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                            content = (stringreplace (loopconcat i 3 [getclientversion $cn $i]) " " ".")
                                            ui_strut 0.1; if $content [ui_text $content] [ui_text "no information avaible" [] [0x444444]]; ui_strut 0.1
                                        ] ]
                                    ] 5 [
                                        ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                            content = (
                                                case (getclientversion $cn 4
                                                    ) 0 [ result "Windows"
                                                    ] 1 [ result "OS X"
                                                    ] 2 [ result "Unix-like"
                                                ]
                                            )
                                            content = (concat $content (concatword (getclientversion $cn 5) "bit"))
                                            ui_strut 0.1; if $content [ui_text $content] [ui_text "no information avaible" [] [0x444444]]; ui_strut 0.1
                                        ] ]
                                    ] 6 [
                                    loop i $playermaxcarry [
                                        wi = (getclientloadweap $cn $i)
                                        if (> $wi 0) [
                                            ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                                ui_strut 0.1;
                                                wi = (at $weapname $wi)
                                                ui_no_cursor_fx [ui_image textures/weapons/@[wi] [] 0.6 0 "" [] $[@[wi]colour] ]
                                                ui_strut 0.1
                                            ] ]
                                        ] [
                                            ui_list [ ui_strut 0.4; ui_background $rowcolour 1 $guifieldbordercolour $guifieldborderblend 1; ui_strut 0.4; ui_list [
                                                ui_strut 0.1;
                                                ui_image textures/question [] 0.6 0 "" [] 0xAAAAAA
                                                ui_strut 0.1
                                            ] ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
            ui_strut 1.4
        ]
    ] ]
]

new_ui clients [ ui_stay_open [
    if (= $guipasses 0) [cnsel = $getclientnum]
    ui_header "Clients"
        [client_tab]
    ui_tab "Overview"
        [overview_tab]
] ]
