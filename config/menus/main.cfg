bind ESCAPE [if (clear_ui 1) [] [show_ui main]]    // it all starts here

defboolvar showmainplayerpreview 1 $idfbitpersist
setdesc showmainplayerpreview "Enables the display of the player preview in the main menu" "value"

new_ui main [
    ui_header "Main"
    ui_two [
        if $showmainplayerpreview [ ui_center [
            ui_center [ ui_player_preview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) [show_ui profilediff; show_ui profile] 7.5 1 1]
            ui_center [ ui_text (getplayername) ]
        ] ]
    ] [
        ui_list [ ui_strut 30 ]
        ui_list [ ui_list [ ui_font "emphasis" [

            ui_font "huge" [ ui_button "^fh PLAY ONLINE" "showservers" "" "textures/icons/fontawesome/play_online.png" 0xFFFFFF 0x32b842 ]

            ui_small_spacer
            ui_small_spacer

            if (isconnected) [
                ui_button " Vote Map/Mode" "show_ui maps 1" "" "textures/icons/fontawesome/vote_map__mode.png"

                ui_small_spacer

                // Show Votes button
                if (> (getvote) 0) [
                    ui_button "^fy Show Votes" "show_ui maps 2" "" "textures/icons/fontawesome/show_votes.png" // "Show Votes" button
                ]
            ] [
                ui_button "^fi Offline Practice" "show_ui maps 1" "" "textures/icons/fontawesome/offline_practice.png" 0xFFFFFF 0x196db6
            ]

            ui_big_spacer
            if (&& (isconnected) (= (gamemode) $modeidxediting)) [
                ui_button " Editing" "show_ui edit" "" "textures/icons/fontawesome/edit.png" // "Editing" button
                ui_small_spacer
                ui_button " Variables" "show_ui variables_gui" "" "textures/icons/fontawesome/advanced.png" // "Variables" button
                ui_big_spacer
            ]
            ui_button "^fS Profile" "show_ui profilediff; show_ui profile" "" "textures/icons/fontawesome/profile.png" // "Profile" button
            ui_small_spacer
            ui_button " Options" "show_ui Options" "" "textures/icons/fontawesome/options.png" // "Options" button
            ui_small_spacer
            ui_button " Help" "show_ui help" "" "textures/icons/fontawesome/help.png" // "Help" button
            ui_big_spacer
            if (isconnected) [
                ui_button " Disconnect" "savewarnchk disconnect" "" "textures/icons/fontawesome/disconnect.png" // "Disconnect" button
                ui_small_spacer
            ]
            ui_button "^fj Quit Game" "savewarnchk quit" "" "textures/icons/fontawesome/quit.png" 0xFFFFFF 0xff321b// "Quit" button
        ] ] ]
    ]
    cases $guirolloveraction "showservers" [
        ui_tooltip "display the server browser for online matches"
    ] "show_ui profilediff; show_ui profile" [
        ui_tooltip "edit your name, colour, loadout and model type"
    ] "show_ui edit" [
        ui_tooltip "access commands to use while editing a map"
    ] "show_ui lobby" [
        ui_tooltip "connect to our irc channel or show open irc tabs"
    ]  "savewarnchk disconnect" [
        ui_tooltip "disconnect your current connection"
    ] "show_ui maps 2" [
        ui_tooltip "display the current votes"
    ] "show_ui maps 1" [
        ui_tooltip (format "select a map/mode combination to %1" (? (isonline) "vote for" "practice on"))
    ] "show_ui Options" [
        ui_tooltip "edit your configuration options"
    ] "show_ui vars" [
        ui_tooltip "search for variables, commands or aliases and edit a plethora of game and client settings."
    ] "show_ui help" [
        ui_tooltip "learn more about many aspects of the game"
    ] "savewarnchk quit" [
        ui_tooltip "close Blue Nebula"
    ]
    ui_tip
]

guimodes = [
    ui_center [ ui_text (modedesc @arg1 @arg2 5) ]
    if (> $arg2 0) [
        loop i $mutsidxnum [
            mut = (<< 1 $i)
            if (&& [& $arg2 $mut] [! (& (mutsimplied $arg1) $mut)]) [
                muttxt = (mutsdesc $arg1 $mut 4)
                if (stringlen $muttxt) [ ui_center [ ui_text (concatword "^fa" $muttxt) ] ]
            ]
        ]
    ]
]

new_ui loading [
    ui_stay_open [ ui_no_hit_fx [
        ui_one [
            ui_font "huge" [ ui_center [ ui_text (? (&& (isloadingmap) (stringlen $maptitle)) $maptitle "loading") ] ]
            ui_font "default" [ ui_center [ ui_text (? (&& (isloadingmap) (stringlen $maptitle) (stringlen $mapauthor)) (concat "by" $mapauthor) "please wait..") ] ]
            if (&& (isloadingmap) (isconnected)) [
                if (= (isloadingmap) 2) [
                    ui_center [ ui_text "^fs^fylightmapping^fS" ]
                ] [
                    gname = (gamename (gamemode) (mutators) 0 32)
                    ui_center [ ui_text (format "^fs^fy%1^fS" $gname) ]
                ]
            ]
            case (isloadingmap) 2 [
                ui_big_spacer
                ui_center [ ui_image "" [] 7 0 ]
            ] 1 [
                if (isconnected) [
                    ui_big_spacer
                    ui_font "little" [
                        ui_center [
                            modetexs = (modetexlist (gamemode) (mutators))
                            looplist modetex $modetexs [ ui_image $modetex [] 1 0 "textures/blank" ]
                        ]
                        guimodes (gamemode) (mutators)
                    ]
                    ui_big_spacer
                    if (stringlen $connectname) [
                        ui_center [ ui_text (format "^faon: ^fw%1:[%2]" $connectname $connectport) ]
                        if (&& (isconnected) (stringlen $serverdesc)) [ ui_font "little" [ ui_center [ ui_text (format "^"^fs%1^fS^"" $serverdesc) ] ] ]
                    ]
                ]
            ]
        ]
        ui_tip
    ] ]
]
