variable_search_text = ""
variable_count = 30
variable_index = 0
variable_number = -1

variable_default_filters = [
    variable_types = 7
    variable_no_types = 0
    variable_flags = 16
    variable_no_flags = 128
]
variable_default_filters
variables_more = 0
variable_type_names = "integer float string command alias local"
variable_flag_names = "persist readonly rewrite world complete texture client server hex admin unknown args preload gamepreload"

menu_variables =  [
    numvars = (getvarinfo -1 $variable_types $variable_no_types $variable_flags $variable_no_flags $variable_search_text)
    if (>= $variable_number $numvars) [ variable_number = -1 ]
    ui_strut 100 1 // width
    ui_list [
        ui_strut 1
        ui_textfield variable_search_textval 32 [variable_search_text = $variable_search_textval; variable_number = -1; variable_index = 0] -1 0 "" 0 "^fd<enter search terms>" 1
        ui_strut 2
        ui_list [
            ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border 1
            ui_strut 0.15
            ui_list [
                ui_strut 1
                ui_text (format "^fg%1 ^fa%2 found" $numvars (? (= $numvars 1) "match" "matches"))
                ui_strut 1
            ]
            ui_strut 0.15
        ]
        ui_strut 1
        ui_list [
            ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border 1
            ui_strut 0.15
            ui_list [
                ui_strut 1
                ui_stay_open [ui_button (format "%1" "Export") [saycommand /writevars ""]]
                ui_strut 1
            ]
            ui_strut 0.15
        ]
        ui_spring 1
        if (= $variables_more 0) [
            ui_list [
                ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border 1
                ui_strut 0.15
                ui_list [
                    ui_strut 0.5
                    ui_bitfield "include commands" variable_types (<< 1 3) [variable_number = -1]
                    ui_strut 0.5
                ]
                ui_strut 0.15
            ]
        ]
        ui_spring 1
        ui_list [
            ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border 1
            ui_strut 0.15
            ui_list [
                ui_strut 0.5
                ui_checkbox " advanced filters" variables_more 1 0 [variable_default_filters]
                ui_strut 0.5
            ]
            ui_strut 0.15
        ]
    ]
    ui_strut 0.5
    ui_list [
        ui_font "little" [
            ui_list [
                ui_list [
                    //ui_strut $variable_count 1
                    variable_index = (min (max 0 (- $numvars $variable_count)) $variable_index) //safeguard
                    variable_number = (min $variable_number $numvars)
                    ui_list [
                        ui_strut 30 1 // expected max length of names
                        break = 0
                        loopwhile i $variable_count [= $break 0] [ // loop through all variables
                            q = (+ $variable_index $i)
                            if (< $q $numvars) [
                                curvar = (getvarinfo $q $variable_types $variable_no_types $variable_flags $variable_no_flags $variable_search_text)
                                hilvar = (string_highlight_list $curvar $variable_search_text "^fs^fy" "^fS")
                                ui_list [ ui_radiobutton $hilvar variable_number $q ]
                            ] [ ui_strut 1 ]
                        ]
                    ]
                ]
            ]
            ui_slider variable_index 0 (max (- $numvars $variable_count) 0) [] 1 1
        ]
        ui_strut 1
        ui_list [
            ui_strut ($variables_more 39 59) 1
            if (&& (>= $variable_number 0) (< $variable_number $numvars) (> $numvars 0)) [
                scurvar = (getvarinfo $variable_number $variable_types $variable_no_types $variable_flags $variable_no_flags $variable_search_text)
                scurvartype = (getvartype $scurvar)
                ui_font "emphasis" [ ui_text $scurvar ]
                ui_list [
                    case $scurvartype 0 [
                        ui_text "^fainteger"
                        ui_spring 1
                	if (& 256 (getvarflags $scurvar)) [
                            ui_text "^fahex colour"
                            ui_spring 1
                            ui_text "^famin: 0x000000"
                            ui_spring 1
                            ui_text "^famax: 0xFFFFFF"
                        ] [    
                            if (& (= (getvarmin $scurvar) 0) (= (getvarmax $scurvar) 1)) [
                                ui_checkbox "toggle" $scurvar
                                ui_spring 
                            ]
                            ui_text (format "^fadefault: ^fw%1" (getvardef $scurvar 1))
                            if (> (getvarmin $scurvar) (<< -1 30)) [
                                ui_spring 1
                                ui_text (format "^famin: ^fw%1" (getvarmin $scurvar))
                            ]
                            if (< (getvarmax $scurvar) (<< 1 30)) [
                                ui_spring 1
                                ui_text (format "^famax: ^fw%1" (getvarmax $scurvar))
                            ]
                            ui_spring 1
                            ui_text (format "^fadefault: ^fw%1" (getvardef $scurvar 1))
                        ]
                    ] 1 [
                        ui_text "^fafloat"
                        ui_spring 1
                        ui_text (format "^famin: ^fw%1" (getfvarmin $scurvar))
                        ui_spring 1
                        ui_text (format "^famax: ^fw%1" (getfvarmax $scurvar))
                        ui_spring 1
                        ui_text (format "^fadefault: ^fw%1" (getfvardef $scurvar 1))
                    ] 2 [
                        ui_text "^fastring"
                    ] 3 [
                        ui_text "^facommand"
                    ] 4 [
                        ui_text "^faalias"
                    ] 5 [
                        ui_text "^falocal"
                    ] () [
                        ui_text "^fa???"
                    ]
                ]
                if (|| (= $scurvartype 2) (= $scurvartype 4))  [
                    ui_strut 0.25
                    ui_list [
                        ui_text (tabify "^fadefault:" 2)
                        ui_editor [@[scurvar]_vardef] (? $variables_more -43 -58) 0 4 -1 0 "" (getsvardef $scurvar 1)
                    ]
                ]
                ui_strut 0.25
                if (> $scurvartype 2) [
                    ui_strut (? $variables_more 52 67) 1
                    ui_list [
                        ui_text (tabify "^faconsole" 2)
                        ui_list [
                            ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border 1
                            ui_strut  0.2
                            ui_stay_open [ ui_button (format "/%1 ...   " $scurvar) [saycommand (format "/%1 " $scurvar)] [] "textures/menu" ]
                            ui_strut  0.2
                        ]
                        ui_strut 1
                        ui_list [
                            ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border 1
                            ui_strut  0.2
                            ui_stay_open [ ui_button (format "/echo (%1 ...   " $scurvar) [saycommand (format "/echo (%1 " $scurvar)] [] "textures/menu" ]
                            ui_strut  0.2
                        ]
                    ]
                ] [    
                    ui_strut (? $variables_more 50 65) 1
                    if (& 256 (getvarflags $scurvar)) [
                        ui_list [
                            ui_text (format "^fadefault: ^fw%1" (hexcolour (getvardef $scurvar 1)))
                            ui_spring 1
                            ui_list [
                                ui_background (getvardef $scurvar) 1.0  0xffffff 1.0 1
                                ui_strut 8 1
                                ui_strut 1.25
                            ]
                        ]    
                        ui_list [
                            ui_text "value:"
                            ui_strut 1
                            [@[scurvar]_varval] = (hexcolour $$scurvar)
                            ui_textfield [@[scurvar]_varval] 10 [@scurvar $[@@[scurvar]_varval]] -1 0 ""
                            ui_spring 1
                            ui_list [
                                ui_background $$scurvar 1.0  0xffffff 1.0 1
                                ui_strut 8 1
                                ui_strut 1.25
                            ]
                        ]
                        ui_strut 0.5
                        ui_button "pick a colour" [show_ui pick_color] [] $editingtex
                    ] [ 
                        if (&& (= $scurvartype 2) (> (stringlen $$scurvar) 500)) [
                            ui_list [
                                ui_text (tabify "value:" 2)
                                ui_stay_open [ ui_button "set via console" [saycommand (format "/%1 %2" $scurvar $$scurvar)] [] "textures/menu" ]
                            ]
                        ] [
                            ui_list [
                                ui_text (tabify "value:" 2 )
                                [@[scurvar]_varval] = $$scurvar
                                ui_textfield [@[scurvar]_varval] (? $variables_more -43 -58) [@scurvar $[@@[scurvar]_varval]] -1 0 ""
                            ]
                        ]
                    ]    
                ]
                if (> (getvartype $scurvar) 2) [
                    scurusage = (loopconcat i (getvarfields $scurvar) [result (getvarfields $scurvar $i)])
                    if (stringlen $scurusage) [
                        ui_strut 0.25
                        ui_list [
                            ui_text (tabify "^fausage:" 2)
                            ui_text (concat $scurvar $scurusage) "" -1 -1 (? $variables_more 1100 1500)
                        ]
                    ]
                ]
                scurdesc = (getvardesc $scurvar)
                if (stringlen $scurdesc) [
                    ui_strut 0.25
                    ui_list [
                        ui_text (tabify "^fainfo:" 2)
                        ui_text $scurdesc "" -1 -1 (? $variables_more 1100 1500)
                    ]
                ]
                scurfields = (getvarfields $scurvar)
                scurfmax = (- (<< 1 $scurfields) 1)
                if (& (= (getvartype $scurvar) 0) (> $scurfields 1)) [
                    if (!= $scurfmax (getvarmax $scurvar)) [
                        ui_text (format "^fyusage description of %1 implies a max value of %2, but it should be %3." $scurvar $scurfmax (getvarmax $scurvar))
                    ]
                    ui_list [
                        ui_strut 15
                        ui_list [
                        loop i $scurfields [
                                ui_bitfield  (getvarfields $scurvar $i) $scurvar (<< 1 $i)
                            ]
                        ]
                    ]
                ]
            ] [
                ui_strut (? $variables_more 52 67) 1
                ui_font "emphasis" [ ui_text "no variable or command selected" ]
                ui_font "little" [
                    ui_text "^fause the list on the left to pick a variable" point
                    ui_text "^fause the search field at the top to narrow results" point
                ]
            ]
        ]
        ui_spring
        if ($variables_more) [
            ui_list [
                ui_strut 15 1 
                ui_center [ ui_text "^fatypes" ]
                loop i (listlen $variable_type_names) [
                    ui_list [
                        ui_bitfield "" variable_types (<< 1 $i) [variable_no_types = 0; variable_number = -1] 
                        ui_spring 1
                        ui_text (at $variable_type_names $i)
                        ui_spring 1
                        //ui_bitfield "" variable_no_types (<< 1 $i) [variable_types = 0; variable_number = -1] 
                        ui_stay_open [ ui_image "textures/warning" [
                            variable_no_types = (^ $variable_no_types (<< 1 @i)) ; variable_number = -1; variable_types = 0
                        ] 0.5 0 "" [] (? (& $variable_no_types (<< 1 $i)) 0xff0000 0x999999)]
                    ] 
                    if (=s $guirolloveraction variable_types) [ui_tooltip "only these types"]
                ]
                ui_strut 0.3
                ui_center [ ui_text "^faflags" ]
                loop i (listlen $variable_flag_names) [
                    ui_list [
                        ui_bitfield "" variable_flags (<< 1 $i) [ variable_number = -1]
                        ui_spring 1
                        ui_text (at $variable_flag_names $i)
                        ui_spring 1
                        //ui_bitfield "" variable_no_flags (<< 1 $i) [ variable_number = -1]
                        ui_stay_open [ ui_image "textures/warning" [
                            variable_no_flags = (^ $variable_no_flags (<< 1 @i)) ; variable_number = -1
                        ] 0.5 0 "" [] (? (& $variable_no_flags (<< 1 $i)) 0xff0000 0x999999)]
                    ]
                    if (=s $guirolloveraction variable_flags) [ui_tooltip "any of these flags"]
                    if (=s $guirollovername textures/warning) [ui_tooltip "none of these"]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        variable_index = 0
        variable_number = -1
    ]
]

new_ui variables_gui [
    ui_header "Variables"
    menu_variables
]

cursoundnames = [ "xv" "cv" "cp" "fl" "xr" "mr" "" "" "" "ss" "" "pos" "" "" "" "m" "" "c" "cn" ]
cursoundwidth = [ 4 4 4 4 4 4 0 0 0 3 0 16 0 0 0 2 0 2 3 ]
new_ui cursounds [
    cursoundnum = (getcursound)
    ui_list [
        ui_list [ ui_strut 3 1 ]
        cursoundprop = (listlen $cursoundnames)
        loop j $cursoundprop [
            cursoundw = (at $cursoundwidth $j)
            if $cursoundw [
                cursoundval = (at $cursoundnames $j)
                ui_list [
                    ui_strut $cursoundw 1
                    ui_center [ ui_text (format "^fy%1" $cursoundval) ]
                ]
            ]
        ]
        ui_list [ ui_strut 30 1 ]
    ]
    loop i $cursoundnum [
        ui_list [
            ui_list [
                ui_strut 3 1
                ui_list [ ui_text (format "^fg%1." $i) ]
            ]
            cursoundplay = (getcursound $i 14)
            if $cursoundplay [ 
                cursoundprop = (getcursound $i)
                loop j $cursoundprop [
                    cursoundw = (at $cursoundwidth $j)
                    if $cursoundw [
                        cursoundval = (getcursound $i $j)
                        ui_list [
                            ui_strut $cursoundw 1
                            ui_center [ ui_text $cursoundval ]
                        ]
                    ]
                ]
                ui_list [
                    ui_strut 30 1
                    ui_list [
                        ui_spring
                        ui_text (getsound (getcursound $i 15) (getcursound $i 9) 3)
                    ]
                ]
            ] [
                ui_spring
                ui_text "-"
            ]
        ]
    ]
]
